
;────────────────────────────────────
;技能취득・放棄処理
;※ARGは要求偏差(0を基準に大きいほどこの種別で마력を消費했다がる)
;────────────────────────────────────
@GAIN_TALENT_AI()
#DIM LCOUNT
#DIM 技能数
#DIM 可用마력
#DIMS 全技能名
#DIMS 技能名, 15
#DIMS 決定種

;취득(放棄)可能技能を技能名に格納する
全技能名 = 배합지식/매혹/금단의지식/도구능숙/촬영기능/혀기술/손가락기술/긴박능숙/새드/더러움무시/終端
SPLIT 全技能名, "/", 技能名

;技能についての解説@これみ_201205
;『배합지식』
;미약・로션の薬効上昇、また휴식系栄養剤ACTの回復量上昇
;『매혹』
;죠교대상の肯定的행동に対する障壁が低くなる、また호감도上昇も大きくなる
;『금단의지식』
;휴식系栄養剤ACTの回復量上昇程度。現在は不遇
;『도구능숙』
;全般的な도구効果の上昇、また도구カスタマイズが可能に
;『촬영기능』
;撮影조수ACTが可能になる
;『혀기술』
;舌を使用する행동の効果が高まる
;『손가락기술』
;指を利用する행동の効果が高まる
;『긴박능숙』
;밧줄を利用する행동の効果が高まる。それだけ
;『새드』
;고통を与えることに対して抵抗がなくなり、効果も上がります
;『더러움무시』
;汚れによるデメリットを軽減。現在は若干不遇

;技能数をカウント
技能数 = FINDELEMENT(技能名, "終端")

;この価格を上回る技能が存在すると破綻する
LOCAL = 1000000
FOR LCOUNT, 0, 技能数
	SIF TALENT:(技能名:LCOUNT) == 0 && 技能名:LCOUNT != "새드"
		LOCAL = MIN(LOCAL, GAIN_TALENT_V(技能名:LCOUNT, 0))
NEXT
;使用可能마력判定
可用마력 = GAIN_TALENT_MP_ABLE()

SIF FLAG:디버그
	PRINTFORML 「기능 취득 처리」(가용 마력：{可用마력})

;買えないか、買える도구がもう無いなら帰る
IF 可用마력 < LOCAL || LOCAL == 1000000
	IF FLAG:디버그
		IF LOCAL == 1000000
			PRINTFORMW 취득 가능한 기능이 존재하지 않습니다！
		ELSE
			PRINTFORMW 가용 마력이 부족합니다(그리고{LOCAL - 可用마력})
		ENDIF
	ENDIF
	RETURN RESULT
ENDIF

;変数を初期化
CALL SET_CRI_VAR
FOR LCOUNT, 0, 技能数
	SIF TALENT:(技能名:LCOUNT) == 0 && 技能名:LCOUNT != "새드"
		CALL SET_CRI_VAR, 技能名:LCOUNT, 1
NEXT

;------------------------------------------
;보정処理
;------------------------------------------

LOCALS = GAIN_TALENT

CALL CHA_CRI_VAR, "죠교자の素質"
CALLFORM %LOCALS%_TALENT_T

CALL CHA_CRI_VAR, "죠교대상の素質"
CALLFORM %LOCALS%_TALENT_M

CALL CHA_CRI_VAR, "죠교자の광기"
CALLFORM %LOCALS%_INSANE(全技能名)

CALL CHA_CRI_VAR, "価格による보정"
CALLFORM %LOCALS%_PRICE(全技能名)

CALL CHA_CRI_VAR, "実行判定"
CALLFORM %LOCALS%_ABLE

;------------------------------------------
;취득判定
;------------------------------------------

LOCAL:2 = 0

$GAIN_TALENT

;취득状況による判定(既に취득、마력不발)
;判定処理
FOR LCOUNT, 0, 技能数
	IF TALENT:(技能名:LCOUNT) == 1 && 技能名:LCOUNT != "새드"
		CALL DIM_CRI_VAR(技能名:LCOUNT, -999)
		CONTINUE
	ENDIF
	LOCAL = GET_CRI_VAR(技能名:LCOUNT + "累計")
	IF LOCAL < 0
		LOCAL = GAIN_TALENT_V(技能名:LCOUNT, 0) * (10 + ABS(LOCAL)) / 10
	ELSE
		LOCAL = GAIN_TALENT_V(技能名:LCOUNT, 0) * 10 / (10 + ABS(LOCAL))
	ENDIF
	SIF LOCAL > 可用마력
		CALL DIM_CRI_VAR(技能名:LCOUNT, -999)
NEXT

CALL DEF_CRI_VAR("最大前者")
LOCAL = GET_CRI_VAR("決定")
決定種 = %RESULTS%
CALL ANA_CRI_VAR()
IF LOCAL != -1 && GAIN_TALENT_V(決定種, 0) <= CFLAG:마력
	LOCAL = CFLAG:마력
	CFLAG:마력 -= GAIN_TALENT_V(決定種, 0)
	可用마력 -= GAIN_TALENT_V(決定種, 0)
	TALENT:決定種 = 1
	;錬成時구상呼び出し
	;CALL KOJO_EVENT(110, XX)
	;CALL VOIDLINE_IF(RESULT)
	SKIPDISP 0
	PRINTFORMW %CALLNAME%(은)는 [%決定種%](을)를 취득했습니다
	PRINTFORMW 마력({LOCAL} → {CFLAG:마력})
	IF FLAG:오토모드 > 0 && FLAG:오토모드표시설정 == 1
		SKIPDISP 1
	ENDIF
	LOCAL:2++
	SIF FLAG:디버그
		PRINTFORML (가용 마력：{可用마력})
	GOTO GAIN_TALENT
ENDIF

SIF FLAG:디버그
;OriginalString : 
	PRINTW 

RETURN RESULT

;-------------------------------------------------------------------------
;죠교자の素質を参照
;-------------------------------------------------------------------------
@GAIN_TALENT_TALENT_T

;죠교자が프라이드높음/低い
IF TALENT:프라이드높음
	CALL ADD_CRI_VAR("더러움무시", -6)
ELSEIF TALENT:프라이드낮음
	CALL ADD_CRI_VAR("더러움무시", 6)
ENDIF

;죠교자が무관심/호기심
IF TALENT:무관심
	CALL ADD_CRI_VAR("배합지식", -6)
	CALL ADD_CRI_VAR("금단의지식", -6)
	CALL ADD_CRI_VAR("도구능숙", -6)
	CALL ADD_CRI_VAR("촬영기능", -6)
ELSEIF TALENT:호기심
	CALL ADD_CRI_VAR("배합지식", 6)
	CALL ADD_CRI_VAR("금단의지식", 6)
	CALL ADD_CRI_VAR("도구능숙", 6)
	CALL ADD_CRI_VAR("촬영기능", 6)
ENDIF

;죠교자が억압/해방
IF TALENT:억압
	CALL ADD_CRI_VAR("새드", TALENT:새드 ? 6 # -6)
ELSEIF TALENT:해방
	CALL ADD_CRI_VAR("새드", TALENT:새드 ? -6 # 6)
ENDIF

;죠교자が헌신적
SIF TALENT:헌신적
	CALL ADD_CRI_VAR("더러움무시", 3)
	
;-------------------------------------------------------------------------
;죠교대상の素質を参照
;-------------------------------------------------------------------------
@GAIN_TALENT_TALENT_M

;죠교대상が반항적/솔직
IF TALENT:MASTER:반항적
	CALL ADD_CRI_VAR("매혹", 6)
	CALL ADD_CRI_VAR("긴박능숙", 3)
	CALL ADD_CRI_VAR("새드", 3)
ELSEIF TALENT:MASTER:솔직
	CALL ADD_CRI_VAR("매혹", -6)
	CALL ADD_CRI_VAR("긴박능숙", -3)
	CALL ADD_CRI_VAR("새드", -3)
ENDIF

;죠교대상が무관심/호기심
IF TALENT:MASTER:무관심
	CALL ADD_CRI_VAR("도구능숙", -3)
ELSEIF TALENT:MASTER:호기심
	CALL ADD_CRI_VAR("도구능숙", 3)
ENDIF

;죠교대상が감정결여/정서적
IF TALENT:MASTER:감정결여
	CALL ADD_CRI_VAR("배합지식", 3)
ELSEIF TALENT:MASTER:정서적
	CALL ADD_CRI_VAR("배합지식", -3)
ENDIF

;죠교대상が튀고싶어함
SIF TALENT:MASTER:튀고싶어함
	CALL ADD_CRI_VAR("촬영기능", 6)

;죠교대상が정조관념/정조무관심
IF TALENT:MASTER:정조관념
	CALL ADD_CRI_VAR("매혹", 6)
ELSEIF TALENT:MASTER:정조무관심
	CALL ADD_CRI_VAR("매혹", -3)
ENDIF

;죠교대상が억압/해방
IF TALENT:MASTER:억압
	CALL ADD_CRI_VAR("매혹", 3)
ELSEIF TALENT:MASTER:해방
	CALL ADD_CRI_VAR("매혹", -3)
ENDIF

;죠교대상が수줍음/수치없음
IF TALENT:MASTER:수줍음
	CALL ADD_CRI_VAR("촬영기능", 6)
ELSEIF TALENT:MASTER:수치없음
	CALL ADD_CRI_VAR("촬영기능", -6)
ENDIF

;죠교대상が고통에약함/고통에강함
IF TALENT:MASTER:고통에약함
	CALL ADD_CRI_VAR("긴박능숙", 6)
	CALL ADD_CRI_VAR("새드", 6)
ELSEIF TALENT:MASTER:고통에강함
	CALL ADD_CRI_VAR("긴박능숙", -3)
	CALL ADD_CRI_VAR("새드", -3)
ENDIF

;죠교대상が젖기쉬움/젖기어려움
IF TALENT:MASTER:젖기쉬움
	CALL ADD_CRI_VAR("배합지식", 6)
	CALL ADD_CRI_VAR("혀기술", 3)
ELSEIF TALENT:MASTER:젖기어려움
	CALL ADD_CRI_VAR("배합지식", -6)
	CALL ADD_CRI_VAR("혀기술", -3)
ENDIF

;죠교대상が약독내성
SIF TALENT:MASTER:약독내성
	CALL ADD_CRI_VAR("배합지식", -9)

;죠교대상が자위하기쉬움
SIF TALENT:MASTER:자위하기쉬움
	CALL ADD_CRI_VAR("긴박능숙", 3)

;죠교대상が쾌감에솔직/쾌감을부정
IF TALENT:MASTER:쾌감에솔직
	CALL ADD_CRI_VAR("매혹", -3)
ELSEIF TALENT:MASTER:쾌감을부정
	CALL ADD_CRI_VAR("매혹", 3)
ENDIF

;죠교대상が도Ｍ
IF TALENT:MASTER:도Ｍ
	CALL ADD_CRI_VAR("긴박능숙", 3)
	CALL ADD_CRI_VAR("새드", 3)
ENDIF

;죠교대상が변태적
SIF TALENT:MASTER:변태적
	CALL ADD_CRI_VAR("새드", 6)

;죠교대상が회복빠름/회복느림
IF TALENT:MASTER:회복빠름
	CALL ADD_CRI_VAR("배합지식", -6)
ELSEIF TALENT:MASTER:회복느림
	CALL ADD_CRI_VAR("배합지식", 6)
ENDIF

;------------------------------------------
;죠교자の광기
;------------------------------------------
@GAIN_TALENT_INSANE(ARGS)
#DIM LCOUNT
#DIM 技能数
#DIMS 技能名, 15

SIF !TALENT:광기
	RETURN 0

;취득(放棄)可能技能を技能名に格納する
SPLIT ARGS, "/", 技能名

;技能数をカウント
技能数 = FINDELEMENT(技能名, "終端")

FOR LCOUNT, 0, 技能数
	SIF TALENT:(技能名:LCOUNT) == 0 && 技能名:LCOUNT != "새드"
		CALL MUL_CRI_VAR(技能名:LCOUNT, -50, 100)
NEXT

;------------------------------------------
;価格による보정
;------------------------------------------
@GAIN_TALENT_PRICE(ARGS)
#DIM LCOUNT
#DIM 技能数
#DIMS 技能名, 15

;취득(放棄)可能技能を技能名に格納する
SPLIT ARGS, "/", 技能名

;技能数をカウント
技能数 = FINDELEMENT(技能名, "終端")

FOR LCOUNT, 0, 技能数
	LOCAL = GET_CRI_VAR(技能名:LCOUNT + "累計")
	IF LOCAL > 0
		CALL MUL_CRI_VAR(技能名:LCOUNT, 100 * POWER(2, 10 + TALENT:충동적 - TALENT:자제심 + TALENT:튀고싶어함) / GAIN_TALENT_V(技能名:LCOUNT, 0))
	ELSEIF LOCAL < 0
		CALL MUL_CRI_VAR(技能名:LCOUNT, 100 * GAIN_TALENT_V(技能名:LCOUNT, 0) / POWER(2, 10 + TALENT:충동적 - TALENT:자제심 + TALENT:튀고싶어함))
	ENDIF
NEXT

;------------------------------------------
;実行判定
;------------------------------------------
@GAIN_TALENT_ABLE

;現仕様では새드素質を付けたり外했다りを繰り返しかねないので
CALL DIM_CRI_VAR("새드", -999)
CALL DIM_CRI_VAR("착의플레이선호", -999)

;------------------------------------------
;使用可能마력判定
;------------------------------------------
@GAIN_TALENT_MP_ABLE()
#FUNCTION
;마력不발基準設定
RETURNF LIMIT(CFLAG:마력 * (8 + MANAGE_MP_PROP() + TALENT:습득빠름 - TALENT:습득느림) * 10 / 100, 0, CFLAG:마력)
