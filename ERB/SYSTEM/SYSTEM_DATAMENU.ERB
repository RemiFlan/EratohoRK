;-------------------------------------------------------------------------------
;	DATAMENUで使用するマジックナンバー
;-------------------------------------------------------------------------------
;	とりあえずSAVEとAUTOで90+10で100枠、オプションで変更できるようにするなら
;	マジックナンバーのところを適当なFLAGにでもすればおｋ
;===============================================================================
@SAVE_AREA(ARG)
#FUNCTION
RETURNF LIMIT(100 + ARG, 0, 1000)

@AUTOSAVE_AREA(ARG)
#FUNCTION
RETURNF LIMIT(10 + ARG, 0, SAVE_AREA())

;COPYDATA用GLOBAL:x/GLOBALS:x
;	現段階では코비―원 = GLOBAL:998、코비―선 = GLOBAL:999を使用
;	대화용일시사용 = GLOBALS:99 を使用、TEMP세이브枠に1000 を使用
;	ここを変えれば変更可
@COPYDATA_BY()
#FUNCTION
RETURNF 998

@COPYDATA_TO()
#FUNCTION
RETURNF 999

@COPYDATA_TEMP()
#FUNCTION
RETURNF 1000

@COPYDATA_TAG()
#FUNCTION
RETURNF 99

;-------------------------------------------------------------------------------
;	TITLE_LOADGAME	タイトル画面から로드を選んだ場合の独自処理
;-------------------------------------------------------------------------------
;===============================================================================
@TITLE_LOADGAME
CALL DATAMENU("TITLELOAD")

;-------------------------------------------------------------------------------
;	EVENTLOAD
;-------------------------------------------------------------------------------
;	Rev#でEVENTLOADが見当たらなかったのでこんなところに
;	え、まじで？
;===============================================================================
@EVENTLOAD
LOADGLOBAL
CALLF ANIME_CONFIG("SET", GLOBAL)
SIF GLOBALS:COPYDATA_TAG() != ""
	CALL DATAMENU("NOWCOPY")

REDRAW 0
CALL DAY_EFFECT

;FLAG:2の更新
FLAG:주인님 = FINDTALENT("주인님")
; 플레이시간用
CALLF PLAYTIME("SET")

;-------------------------------------------------------------------------------
;	세이브時用一時変数初期化
;-------------------------------------------------------------------------------
;	みたまんま
;	Rev#の세이브システムでどれを消していいのかわからないので行頭RETURN
;===============================================================================
@VAR_INITIALIZE
#LOCALSIZE 1
RETURN RESULT

;↓以下処理-------------------------------------------------
VARSET TFLAG
VARSET TSTR
FOR LOCAL, 0, VARSIZE("TCVAR")
	CVARSET TCVAR, LOCAL
NEXT
FOR LOCAL, 0, VARSIZE("BASE")
	CVARSET DOWNBASE, LOCAL
NEXT
FOR LOCAL, 0, VARSIZE("PALAM")
	CVARSET PALAM, LOCAL
	CVARSET GOTJUEL, LOCAL
	CVARSET CUP, LOCAL
	CVARSET CDOWN, LOCAL
NEXT
FOR LOCAL, 0, VARSIZE("EX")
	CVARSET EX, LOCAL
	CVARSET NOWEX, LOCAL
NEXT
FOR LOCAL, 0, VARSIZE("SOURCE")
	CVARSET SOURCE, LOCAL
NEXT
FOR LOCAL, 0, VARSIZE("TEQUIP")
	CVARSET TEQUIP, LOCAL
NEXT
RETURN RESULT

;-------------------------------------------------------------------------------
;	SAVEINFO		(使用せず)
;-------------------------------------------------------------------------------
;===============================================================================
; →SHOP.ERBにてコメントアウト

;-------------------------------------------------------------------------------
;	SAVEINFOF(式中関数)
;-------------------------------------------------------------------------------
;	SAVEINFOの式中関数版  PUTFORM？  何それ美味しいの？
;===============================================================================
@SAVEINFOF
#FUNCTIONS
#LOCALSSIZE 1
GETTIME
LOCALS = %RESULTS% %CALLNAME:MASTER% {DAY+1}일째 %GET_TIME()% \@ TARGET >= 0 ? %CALLNAME:TARGET%이(가) %CALLNAME:MASTER%을(를) 조교 중 # \@【%PLAYTIME("GET")%】
RETURNF LOCALS
;LOCALS = %RESULTS% %CALLNAME:MASTER% {DAY+1}日目%GET_TIME()% \@ TARGET >= 0 ? %CALLNAME:TARGET%が%CALLNAME:MASTER%を조교중 # \@【%PLAYTIME("GET")%】
;-------------------------------------------------------------------------------
;	CHKDATAのRESULTSから세이브日時を취득する(式中関数)
;-------------------------------------------------------------------------------
;	例  2010/04/01 12:34:56  →  20100401123456
;	式中関数版CHKDATAのRESULTへの日時취득なんてなかった…！
;===============================================================================
;	ARGS = CHKDATA直後のRESULTSのみ対応
@CHKDATA_DATE(ARGS)
#FUNCTION
RETURNF TOINT(SUBSTRING(ARGS,0,4))*10000000000 + TOINT(SUBSTRING(ARGS,5,2))*100000000 + TOINT(SUBSTRING(ARGS,8,2))*1000000 + TOINT(SUBSTRING(ARGS,11,2))*10000 + TOINT(SUBSTRING(ARGS,14,2))*100 + TOINT(SUBSTRING(ARGS,17,2))

;-------------------------------------------------------------------------------
;	세이브データを全検索して最も新しい세이브データ번호を취득(式中関数)
;-------------------------------------------------------------------------------
;===============================================================================
;	ARGS = "NOAUTO" 오토세이브含まない / その他 오토세이브含める
@ALLCHKDATA_DATE_LATEST(ARGS)
#FUNCTION
#LOCALSIZE 4
#LOCALSSIZE 1
VARSET LOCAL
LOCAL:2 = -1
LOCAL:3 = ARGS == "NOAUTO" ? SAVE_AREA()-AUTOSAVE_AREA() # SAVE_AREA()
FOR LOCAL, 0, LOCAL:3
	CHKDATA LOCAL
	IF RESULT != 0
		CONTINUE
	ELSEIF CHKDATA_DATE(RESULTS) > LOCAL:1 
		LOCAL:1 = CHKDATA_DATE(RESULTS)
		LOCAL:2 = LOCAL
		LOCALS = %RESULTS%
	ENDIF
NEXT
RESULTS = %LOCALS%
RETURNF LOCAL:2


;-------------------------------------------------------------------------------
;	枠拡大오토세이브
;-------------------------------------------------------------------------------
;	SAVE_AREA()-1番から逆算してまでAUTOSAVE_AREA()個
;	「ゲッ、오토세이브上書きされちゃった！」な事故防止用
;===============================================================================
;	LOCAL = COUNT  LOCAL:1 = DATE一時保存  LOCAL:2 = 比較用DATE  LOCAL:3 = 最終 
@SYSTEM_AUTOSAVE
#LOCALSIZE 4
VARSET LOCAL
;오토세이브枠が0以下なら何もしない
SIF AUTOSAVE_AREA() <= 0
	RETURN 0
;デフォルト比較用にありえないDATEを(2999/12/31 23:59:59)セット
LOCAL:2 = 29991231235959
;逆算
;例:SAVE枠100,AUTO枠5 99,                      99-5(94), = 95番から99番まで
FOR LOCAL, SAVE_AREA(-1), SAVE_AREA(-1)-AUTOSAVE_AREA(), -1
	CHKDATA LOCAL
	;既存세이브データが存在するならDATEを、そうでないなら-1を一時保存
	LOCAL:1 = RESULT == 0 ? CHKDATA_DATE(RESULTS) # -1
	;上と比較用DATEを比較してより古いor空いている場合
	IF LOCAL:1 < LOCAL:2
		;比較用DATE更新
		LOCAL:2 = LOCAL:1
		;最も古いもしくは空き세이브データ 更新
		LOCAL:3 = LOCAL
	ENDIF
NEXT
;他所様のバリアントで怖いのでコメントアウト
;CALL VAR_INITIALIZE
SAVEDATA LOCAL:3, @"%SAVEINFOF()%"
RETURN 1

;-------------------------------------------------------------------------------
;	枠拡大クイック로드
;-------------------------------------------------------------------------------
;	最も新しい세이브データを로드
;===============================================================================
@QUICKLOAD
#LOCALSIZE 1
VARSET LOCAL
LOCAL = ALLCHKDATA_DATE_LATEST()
IF LOCAL == -1
	CALL PRINT_DIALOG("세이브データが로드できません",2,2,2)
	RETURN 0
ELSE
	CALL PRINT_DIALOG(@"{LOCAL}번을 로드했다",2,2,0)
	CALL PRINT_DIALOG(RESULTS,0,2,2)
	LOADDATA LOCAL
ENDIF
RETURN 1

;-------------------------------------------------------------------------------
;	세이브データ総合
;-------------------------------------------------------------------------------
;	세이브,コメント세이브,로드,削除,コピーの各세이브データ관리を統合
;
;	使い方
;			↓こういう関数を作る
;	@TITLE_LOADGAME
;	CALL DATAMENU("TITLELOAD")
;
;	SAVEGAMEをCALL DATAMENU("SAVE")に置換
;	LOADGAMEをCALL DATAMENU("LOAD")に置換
;
;	@EVENTLOAD に↓の二行を追加
;	SIF GLOBALS:COPYDATA_TAG() != ""
;		CALL DATAMENU("NOWCOPY")
;																		ENJOY!
;===============================================================================
@DATAMENU(ARGS)
#DIM NOWPAGE,1
#DIM MAXPAGE,1
#DIM NOWMODE,1
#DIM COPYSELECT,1
#DIM SCREEN,1
#LOCALSIZE 2
#LOCALSSIZE 1
COPYSELECT = -1
;初期모드선택
SELECTCASE ARGS
	;TITLEから飛んできた(LOAD意外不可)
	CASE "TITLELOAD"
		NOWMODE = 0
	;세이브모드
	CASE "SAVE"
		NOWMODE = 1
	;コメント付き세이브모드
	CASE "SAVECOMMENT"
		NOWMODE = 2
	;로드모드
	CASE "LOAD"
		NOWMODE = 3
	;削除모드
	CASE "DELETE"
		NOWMODE = 4
	;コピー모드
	CASE "COPY"
		NOWMODE = 5
	;コピー中EVENTLOADから飛んできた
	CASE "NOWCOPY"
		CALL COPYDATA
		NOWMODE = 5
	;引数文字列がおかしかったらRETURN
	CASEELSE
		RETURN -1
ENDSELECT
;初期ページは오토세이브を含まない最新データのあるところ
NOWPAGE = ALLCHKDATA_DATE_LATEST("NOAUTO") / 20

;再描写はここから-------------------------------------------
$PRINT_LIST
SCREEN = LINECOUNT
;最新データの취득
LOCAL:1 = ALLCHKDATA_DATE_LATEST()
;ダイアログ表示
CALL PRINT_DIALOG(DATAMENU_LABEL(NOWMODE,1,COPYSELECT),1,1,1)
;リスト表示
FOR LOCAL, 0, SAVE_AREA()
	;20個ずつの表示
	SIF !RANGE(LOCAL, NOWPAGE * 20, (NOWPAGE + 1) * 20 - 1)
		CONTINUE

	CHKDATA LOCAL
	;선택出来ないものを暗色化
	SIF RESULT != 0 && !( NOWMODE == 1 || NOWMODE == 2 || (NOWMODE == 5 && COPYSELECT != -1) )
		SETCOLOR 0x808080
	;行頭情報	COPY/NEW!/AUTO
	IF LOCAL == COPYSELECT
		SETCOLOR 0xFFFF00
;OriginalString : %" COPY "%
		PRINTFORM %" COPY "%
	ELSEIF LOCAL == LOCAL:1
		SETCOLOR 0xFFFFFF
;OriginalString : %" NEW! "%
		PRINTFORM %" NEW! "%
	ELSEIF RANGE(LOCAL,SAVE_AREA()-AUTOSAVE_AREA(),SAVE_AREA(-1))
;OriginalString : %" AUTO "%
		PRINTFORM %" AUTO "%
	ELSE
;OriginalString : %"      "%
		PRINTFORM %"      "%
	ENDIF
;OriginalString : [{LOCAL,2}] - %RESULTS%
	PRINTFORML [{LOCAL,2}] - %RESULTS%
	RESETCOLOR
NEXT
CALL NEWLINE

;最大ページ数
MAXPAGE = (LOCAL - 1) / 20
;ページセレクタ
CALL PRINT_PAGESELECT(NOWPAGE,MAXPAGE)
;タイトルから飛んできた場合は他機能不可
IF NOWMODE
	CALL NEWLINE
	CALL PRINTC_COMMAND(1010,@"%UNICODE(0x25C0)%%DATAMENU_LABEL(TURNPAGE(NOWMODE, -1, 1, 5))%")
	CALL PRINTC_COMMAND(1011,@"%UNICODE(0x25B6)%%DATAMENU_LABEL(TURNPAGE(NOWMODE, +1, 1, 5))%")
ENDIF
CALL PRINT_RETURN(,,2)
INPUT
SELECTCASE RESULT
	CASE IS < 0
		RETURN 0
	CASE 0 TO SAVE_AREA()
		LOCAL = RESULT
		CHKDATA LOCAL
		GOTO DO_PROCESS
	CASE 1001
		NOWPAGE = TURNPAGE(NOWPAGE, -1, 0, MAXPAGE)
	CASE 1003
		NOWPAGE = TURNPAGE(NOWPAGE, +1, 0, MAXPAGE)
	CASE 1010
		NOWMODE = NOWMODE ? TURNPAGE(NOWMODE, -1, 1, 5) # 0
	CASE 1011
		NOWMODE = NOWMODE ? TURNPAGE(NOWMODE, +1, 1, 5) # 0
ENDSELECT
;コピー모드外れたら코비―원リセット
SIF NOWMODE != 5 && COPYSELECT != -1
	COPYSELECT = -1
$REPRINT
CLEARLINE LINECOUNT - SCREEN
GOTO PRINT_LIST


;実処理ここから----------------------------------------------
$DO_PROCESS
;だみぃ
IF NOWMODE < 0

;세이브(코비―선선택)処理-------------------------
ELSEIF NOWMODE == 1 || NOWMODE == 2 || ( NOWMODE == 5 && ( COPYSELECT != -1 && COPYSELECT != LOCAL) )
	SELECTCASE RESULT
		CASE 0,2,3,4
			CALL PRINT_DIALOG(@"\@ !RESULT ? 세이브데이터가 존재합니다 덮어 쓰시겠습니까？ #  알 수 없는 데이터가 존재합니다 덮어 쓰시겠습니까？ \@",1,1,1)
			CALL PRINT_CHOICE(,"예",1,"아니오",2,)
			SIF RESULT == 2
				GOTO REPRINT
		CASE 1
	ENDSELECT
	;コピー모드時二回目はCALL先で코비―원を로드するのでここで終了
	SIF NOWMODE == 5 && ( COPYSELECT != -1 )
		CALL COPYDATA(COPYSELECT,LOCAL)

	;変数初期化  こわいのでコメントアウト
	;CALL VAR_INITIALIZE
	LOCALS = %SAVEINFOF()%
	;コメント세이브모드時はコメント入力
	IF NOWMODE == 2
		CALL PRINT_DIALOG("コメントを入力してください",1,1,1)
		INPUTS
		LOCALS += @" %RESULTS%"
	ENDIF
	SAVEDATA LOCAL, LOCALS
	CALL PRINT_DIALOG(@"{LOCAL}번째 슬롯에 데이터를 세이브했다",2,2,0)
	CALL PRINT_DIALOG(LOCALS,0,2,2)
	RETURN 1

;로드処理---------------------------------------
ELSEIF NOWMODE == 0 || NOWMODE == 3
	SELECTCASE RESULT
		CASE 0
			CALL PRINT_DIALOG(@"{LOCAL}번을 로드했다",2,2,0)
			CALL PRINT_DIALOG(RESULTS,0,2,2)
			LOADDATA LOCAL
		CASE 2,3,4
			CALL PRINT_DIALOG("로드可能なデータでは有りません",2,2,2)
			GOTO REPRINT
	ENDSELECT

;削除処理-----------------------------------------
ELSEIF NOWMODE == 4 && RESULT != 1
	CALL PRINT_DIALOG(@"{LOCAL}番を削除しても良いですか？",1,1,0)
	CALL PRINT_DIALOG(RESULTS,0,1,1)
	CALL PRINT_CHOICE(,"예",1,"아니오",2,)
	IF RESULT == 1
		DELDATA LOCAL
		CALL PRINT_DIALOG(@"{LOCAL}番を削除しま했다",2,2,0)
		CALL PRINT_DIALOG(RESULTS,0,2,2)
		GOTO PRINT_LIST
	ENDIF

;코비―원선택処理---------------------------------
ELSEIF NOWMODE == 5 && COPYSELECT == -1 && RESULT == 0
	COPYSELECT = LOCAL
	GOTO PRINT_LIST
ENDIF

GOTO REPRINT

;-----------------------------------------------------------
;	DATAMENUで使うラベル表示用関数(式中関数)
;===========================================================
;	ARG = NOWMODE  ARG:1 = HEADER用  ARG:2 = コピー모드のHEADER用
@DATAMENU_LABEL(ARG,ARG:1,ARG:2)
#FUNCTIONS
SELECTCASE ARG
	CASE 1, 2
		SIF ARG:1
			RETURNF @"세이브 - 어느 슬롯에 세이브합니까？ \@ ARG == 2 ? (コメント모드) # \@"
		RETURNF @"\@ARG == 1 ? %FORMCELL("세이브",PRINTCPERLENGTH(-10),2)% # %FORMCELL("コメント세이브",PRINTCPERLENGTH(-10),2)% \@"
	CASE 0, 3
		SIF ARG:1
			RETURNF "로드 - 어느 데이터를 로드합니까？"
		RETURNF FORMCELL("로드",PRINTCPERLENGTH(-10),2)
	CASE 4
		SIF ARG:1
			RETURNF "삭제 - 어느 데이터를 삭제합니까？"
		RETURNF FORMCELL("削除",PRINTCPERLENGTH(-10),2)
	CASE 5
		SIF ARG:1
			RETURNF @"\@ ARG:2 == -1 ? 복사 - 어느 데이터를 복사합니까？ # 복사 - {ARG:2}번 데이터를 어느 슬롯에 복사합니까？ \@"
		RETURNF FORMCELL("コピー",PRINTCPERLENGTH(-10),2)
	CASEELSE
		RETURNF @"ERROR!{ARG,2}"
ENDSELECT


;-----------------------------------------------------------
;	세이브データコピー中用
;-----------------------------------------------------------
;	しっかし見難いなぁｗ
;===========================================================
;	ARG:0 / COPYDATA_BY() = 코비―원  
;	ARG:1 / COPYDATA_TO() = 코비―선
@COPYDATA(ARG:0, ARG:1)
;初めて飛んできた
;現在のデータをTEMP枠に保存し、GLOBAL/Sに元/先/タグ保存して코비―원をLOAD
IF !GLOBAL:COPYDATA_BY() && !GLOBAL:COPYDATA_TO() && GLOBALS:COPYDATA_TAG() == ""
	SAVEDATA COPYDATA_TEMP(), @"TEMP"
	GLOBAL:COPYDATA_BY() = ARG:0
	GLOBAL:COPYDATA_TO() = ARG:1
	GLOBALS:COPYDATA_TAG() = {ARG,2}번 데이터를 {ARG:1,2} 슬롯에 복사했다
	SAVEGLOBAL
	LOADDATA GLOBAL:COPYDATA_BY()
;二回目
;LASTLOAD_NOと코비―원が同一でコピー用タグが真は코비―선にSAVEしてTEMPデータをLOAD
ELSEIF LASTLOAD_NO == GLOBAL:COPYDATA_BY() && GLOBALS:COPYDATA_TAG() != ""
	GLOBAL:COPYDATA_BY() = 0
	SAVEGLOBAL
	SAVEDATA GLOBAL:COPYDATA_TO(), LASTLOAD_TEXT
	LOADDATA COPYDATA_TEMP()
;三回目(最後)
;LASTLOAD_NOとTEMP枠が同一でコピー用タグが真はタグを表示してTEMP枠を削除
ELSEIF LASTLOAD_NO == COPYDATA_TEMP() && GLOBALS:COPYDATA_TAG() != ""
	CALL PRINT_DIALOG(GLOBALS:COPYDATA_TAG(),2,2,2)
	DELDATA COPYDATA_TEMP()
	GLOBAL:COPYDATA_TO() = 0
	GLOBALS:COPYDATA_TAG() =
	SAVEGLOBAL
ENDIF

