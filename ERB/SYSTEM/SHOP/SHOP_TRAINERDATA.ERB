;────────────────────────────────────
;ステータス表示
;────────────────────────────────────
;ちょっと整理@revkoishi(14/05/22)
@PRINT_STATUS(ARG:0)
LOCAL:1 = RESULT
DO
	DRAWLINE
	PRINTFORML ■ %CALLNAME:(ARG:0)%의 스테이터스 ■ \@ ARG:0 != MASTER && TALENT:MASTER:독심능력 == 2 ? (호감도:{CFLAG:(ARG:0):9}) # \@
	PRINTFORM [LV {CFLAG:(ARG:0):죠교레벨, 2}  EXP {CFLAG:(ARG:0):죠교경험, 6}/{CFLAG:(ARG:0):필요경험치, 6}  
	PRINTFORML NEXT {CFLAG:(ARG:0):필요경험치 - CFLAG:(ARG:0):죠교경험, 6}]
	CALL PRINT_BASEL("체력", ARG:0, GETNUM(BASE, "체력"), 32, 1)
	CALL PRINT_BASEL("기력", ARG:0, GETNUM(BASE, "기력"), 32, 1)
	IF ARG:0 == MASTER
		CALL PRINT_BASEL("이성", ARG:0, GETNUM(BASE, "이성"), 32, 1)
		SIF PENIS(ARG:0)
			CALL PRINT_BASEL("精力", ARG:0, GETNUM(BASE, "사정"), 32, 1)
	ELSE
		CALL PRINT_TENSION(ARG:0)
	ENDIF
	CALL NEW_PRINT_TALENT(ARG:0)
	CALL NEW_PRINT_EXP(ARG:0)
	CALL NEW_PRINT_ABL(ARG:0)
	IF FLAG:죠교UI패드화 && FLAG:조교중
;OriginalString : (HTMLBUTTON(@"[%KEYMARK_B%] - 돌아가기", KEY_B) + "　" +HTMLBUTTON(@"[%KEYMARK_X%] - 前のページ", KEY_X))
		CALL HTMLPRINTL(HTMLBUTTON(@"[%KEYMARK_B%] - 돌아가기", KEY_B) + " " +HTMLBUTTON(@"[%KEYMARK_X%] - 전의 페이지", KEY_X))
		DO
			ONEINPUTS
			LOCAL:0 = (RESULTS != KEY_B && RESULTS != KEY_X)
			IF LOCAL:0
				CLEARLINE 1
				REUSELASTLINE 入力値が範囲外です
			ENDIF
		LOOP LOCAL:0
		RESULT = RESULTS == KEY_B ? 100 # 102
	ELSE
		PRINTL [100] - 돌아온다　[102] - 다음의 페이지
		DO
			INPUT
			LOCAL:0 = (RESULT != 100 && RESULT != 102)
			IF LOCAL:0
				CLEARLINE 1
				REUSELASTLINE 入力値が範囲外です
			ENDIF
		LOOP LOCAL:0
	ENDIF
	SIF RESULT == 102
		CALL PRINT_STATUS2(ARG:0)
LOOP RESULT != 100
RETURN LOCAL:1


;未취득の각인も含めカラー表示化。改行位置自動判定に変更。変更後ポイント計算が出来ていなかったのを修正。
;決断的に新仕様へ大工事＋微調整@revkoishi(14/05/28)
@PRINT_STATUS2(ARG:0)
#DIM SURRENDER_LIST, 6, 30
#DIM SURRENDER_SIZE, 6
#DIM LINE_WIDTH
LINE_WIDTH = WIDTH()

IF ARG:0 == MASTER
	PRINTFORML -- □ 각인 □ %"-" * (LINE_WIDTH - 14)%
	IF MARK:MASTER:고통각인 || MARK:MASTER:쾌락각인 || MARK:MASTER:트라우마
		PRINTFORM \@ MARK:MASTER:고통각인 ? %MARKNAME:0%Lv{MARK:MASTER:고통각인, 2}%" "% # \@
		PRINTFORM \@ MARK:MASTER:쾌락각인 ? %MARKNAME:1%Lv{MARK:MASTER:쾌락각인, 2}%" "% # \@
		PRINTFORML \@ MARK:MASTER:트라우마 ? %MARKNAME:4%Lv{MARK:MASTER:트라우마,2} # \@
	ENDIF
	SIF MARK:MASTER:트라우마
		PRINTFORML %CALLNAME:MASTER%의 트라우마가 되는 원인은：[%GET_ACTNAME(CFLAG:MASTER:트라우마)%]
	PRINTFORML                      %MARKNAME:2%        %MARKNAME:3%
	FOR LOCAL:0, 0, CHARANUM
		IF LOCAL:0 == MASTER !| CFLAG:(LOCAL:0):일시퇴피	;일시퇴피中のキャラは表示しない
			PRINTPLAINFORM [{LOCAL:0, 3}] - %CALLNAME:(LOCAL:0), 16, LEFT% 
			PRINTFORML Lv{MARK:(LOCAL:0):굴복각인, 2}            Lv{MARK:(LOCAL:0):반항각인, 2}
		ENDIF
	NEXT
ELSE
	VARSET LOCAL	;0.カウンタ/1.Lv/2.桁位置/3.文字列長/4.각인数/5.処理中の実績번호/6.虹色
	VARSET SURRENDER_LIST
	VARSET SURRENDER_SIZE
	PRINTFORML -- □ 조교 이력 □ %"-" * (LINE_WIDTH - 18)%
	FOR LOCAL:0, 0, 128
		SIF GET_SURRENDER_LV(LOCAL:0, ARG:0) && IS_SURRENDER_ENABLE(LOCAL:0, ARG:0)	;関数名変更@revkoishi(14/06/01)
			SURRENDER_LIST:GET_SURRENDER_LV(LOCAL:0, ARG:0):(SURRENDER_SIZE:GET_SURRENDER_LV(LOCAL:0, ARG:0)++) = LOCAL:0
	NEXT
	FOR LOCAL:1, 1, 6
		PRINTFORM Lv{LOCAL:1}
		LOCAL:2 = 3	;桁位置リセット
		LOCAL:4 = 0	;각인数リセット
		LOCAL:6 = GET_RAINBOW(6 + LOCAL:1)	;虹色
		FOR LOCAL:0, 0, SURRENDER_SIZE:(LOCAL:1)
			LOCAL:5 = SURRENDER_LIST:(LOCAL:1):(LOCAL:0)	;処理中の実績번호更新
			LOCALS:0 = 【%GET_SURRENDER_STR_T(LOCAL:5, ARG:0)%】
			LOCAL:3 = STRLENS(LOCALS:0)
			LOCAL:2 += LOCAL:3
			IF LOCAL:2 > LINE_WIDTH
				PRINTFORM \n   
				LOCAL:2 = 3 + LOCAL:3
			ENDIF
			;カテゴリ分け用色セット関数作ろうかと思ったけど、当面はこれで十分な予感…
			IF LOCAL:5 > 63 ? GETBIT(CFLAG:(ARG:0):각인2, LOCAL:5 - 64) # GETBIT(CFLAG:(ARG:0):각인, LOCAL:5)	;所持각인
				++LOCAL:4							;각인数加算
				IF INRANGE(LOCAL:1, 1, 4)			;Lv4以下は単色
					CALL FPRINT(LOCALS:0, LOCAL:6)
				ELSE								;Lv5以上は虹色
					CALL PRINT_RAINBOW(LOCALS:0, , , RAND:12)
				ENDIF
			ELSE									;未所持각인
				CALL FPRINT(LOCALS:0, 0x606060)		;灰色
			ENDIF
		NEXT
		PRINTFORML \n    {SURRENDER_LV2POINT(LOCAL:1), 2}P×{LOCAL:4, 2}＝{LOCAL:4 * SURRENDER_LV2POINT(LOCAL:1), 2}P
		PRINTSL "-" * LINE_WIDTH
	NEXT
	PRINTFORML 합계:{CFLAG:(ARG:0):굴복P1}P 굴복 각인:LV{MARK:(ARG:0):굴복각인}
ENDIF
IF FLAG:죠교UI패드화 && FLAG:조교중
;OriginalString : (HTMLBUTTON(@"[%KEYMARK_B%] - 돌아가기", KEY_B) + "　" +HTMLBUTTON(@"[%KEYMARK_X%] - 前のページ", KEY_X))
	CALL HTMLPRINTL(HTMLBUTTON(@"[%KEYMARK_B%] - 돌아가기", KEY_B) + " " +HTMLBUTTON(@"[%KEYMARK_X%] - 전의 페이지", KEY_X))
	DO
		ONEINPUTS
		LOCAL:0 = (RESULTS != KEY_B && RESULTS != KEY_X)
		IF LOCAL:0
			CLEARLINE 1
			REUSELASTLINE 不正な入力値です
		ENDIF
	LOOP LOCAL:0
	RESULT = RESULTS == KEY_B ? 100 # 101
ELSE
	PRINTL [100] - 돌아온다　[101] - 전의 페이지
	CALL INPUT_RANGE(100, 101)
ENDIF
RETURN RESULT


;一部参照がずれていたので修正＆５列に変更＆表示の調整＆色設定@revkoishi(14/05/26)
@NEW_PRINT_EXP(ARG:0)
#DIM NODISP, 10
#DIM LINE_WIDTH
LINE_WIDTH = WIDTH()
LOCAL:1 = 0				;表示カウンタ
;表示順は性別によってありえないものを抜くだけにしま했다。以下抜かれるもの
VARSET NODISP, -1
IF TALENT:(ARG:0):후타나리
	NODISP = 14, 41				;후타나리：14,시오후키경험/41,ＢＬ경험
ELSEIF TALENT:(ARG:0):남자
	NODISP =  1,  6, 14, 40, 52	;　남자：1,Ｖ경험/6,분유경험/14,시오후키경험/40,레즈경험/52,Ｖ확장경험
ELSE
	NODISP =  5, 41				;　여자：5,사정경험/41,ＢＬ경험
ENDIF
PRINTFORML -- □ 경험 □ %"-" * (LINE_WIDTH - 14)%
FOR LOCAL:0, 0, 70
	SIF STRLENSU(EXPNAME:(LOCAL:0)) && !MATCH(NODISP, LOCAL:0)
{
		CALL FPRINT(@"\@ EXP:(ARG:0):(LOCAL:0) ? %EXPNAME:(LOCAL:0), 12% # ？？？？？？ \@:{EXP:(ARG:0):(LOCAL:0), 5}" + 
                    @"\@ ++LOCAL:1 % 5 ? 　 # \n \@", EXP:(ARG:0):(LOCAL:0) ? GET_RAINBOW(9 + EXP_TYPE(LOCAL:0)) # 0x606060)
}
NEXT
PRINTFORM \@ LINEISEMPTY() ? # \n \@


;８列に変更＆表示の調整＆色設定@revkoishi(14/05/26)
@NEW_PRINT_ABL(ARG:0)
#DIM NODISP, 10
#DIM LINE_WIDTH
LINE_WIDTH = WIDTH()
LOCAL:1 = 0				;表示カウンタ
;表示順は性別によってありえないものを抜くだけにしま했다。以下抜かれるもの
VARSET NODISP, -1
IF TALENT:(ARG:0):후타나리
	NODISP = 10			;후타나리：10,ＢＬ끼
ELSEIF TALENT:(ARG:0):남자
	NODISP =  4,  9, 14	;　남자：4,Ｖ감각/9,레즈끼/14,레즈중독
ELSE
	NODISP = 10			;　여자：10,ＢＬ끼/
ENDIF
PRINTFORML -- □ 능력 □ %"-" * (LINE_WIDTH - 14)%
FOR LOCAL:0, 0, 30
	SIF STRLENSU(ABLNAME:(LOCAL:0)) && !MATCH(NODISP, LOCAL:0)
{
		CALL FPRINT(@"%ABLNAME:(LOCAL:0), 8%:{ABL:(ARG:0):(LOCAL:0), 2}\@ ++LOCAL:1 % 8 ? 　 # \n \@", 
                    ABL:(ARG:0):(LOCAL:0) ? GET_RAINBOW(3 + ABL_TYPE(LOCAL:0)) # 0x606060)
}
NEXT
PRINTFORM \@ LINEISEMPTY() ? # \n \@


;画面幅の有効活用＆LOCAL地獄でとても読みにくい処理だったので整理(했다つもり)。微調整@revkoishi(14/05/26)
@NEW_PRINT_TALENT(ARG:0)
#DIM LINE_WIDTH
#DIM TALENT_LIST, 8, 50
#DIM TALENT_SIZE, 8

;変数初期化
VARSET LOCAL
VARSET TALENT_LIST
VARSET TALENT_SIZE
LINE_WIDTH = WIDTH()	;とりあえず基準となる幅を他所から勝손に拝借。27*4+0=108桁

PRINTFORML -- □ 소질 □ %"-" * (LINE_WIDTH - 14)%
IF FLAG:소질표시방법	;素質分類表示
	FOR LOCAL:0, 0, 150	;分類ループ
		SIF TALENT:(ARG:0):(LOCAL:0)
			TALENT_LIST:TALENT_TYPE(LOCAL:0):(TALENT_SIZE:TALENT_TYPE(LOCAL:0)++) = LOCAL:0
	NEXT
	;性別は特殊なので別処理とし、ここで表示する
	PRINTFORM %TALENT_TYPENAME(1), 8, RIGHT%：
{
	CALL FPRINTL(@"[%GET_SEX(ARG:0)%] " + \@ TALENT:(ARG:0):처녀 ? [%GET_TALENTNAME(ARG:0, GETNUM(TALENT, "처녀"))%]%" "% # \@ + 
                \@ TALENT:(ARG:0):동정 ? [%GET_TALENTNAME(ARG:0, GETNUM(TALENT, "동정"))%]%" "% # \@, GET_RAINBOW(1))
}
	FOR LOCAL:0, 2, 8	;表示ループ。非表示と、表示済みの性別は飛ばす
		IF TALENT_SIZE:(LOCAL:0)	;その分類の素質が無ければ飛ばす
			;素質分類名を表示
			PRINTFORM %TALENT_TYPENAME(LOCAL:0), 8, RIGHT%：
			LOCAL:2 = 10						;桁位置(↑表示後の位置)
			LOCAL:4 = GET_RAINBOW(LOCAL:0)		;虹色
			FOR LOCAL:1, 0, TALENT_SIZE:(LOCAL:0)	;その分類の所持素質数分ループ
				;素質名を취득([素質名] )
				LOCALS:0 = [%GET_TALENTNAME(ARG:0, TALENT_LIST:(LOCAL:0):(LOCAL:1))%]%" "%
				LOCAL:3 = STRLENS(LOCALS:0)	;↑の文字数(素質名+[]+スペース)
				LOCAL:2 += LOCAL:3			;桁位置加算
				IF LOCAL:2 > LINE_WIDTH		;このまま表示すると途中改行されそうな場合は改行処理を行う
					;改行処理(折り返し、行頭インデント、桁位置再計算)
					PRINTFORM \n%"          "%
					LOCAL:2 = 10 + LOCAL:3
				ENDIF
				;素質名を表示
				CALL FPRINT(LOCALS:0, LOCAL:4)
			NEXT
			PRINTL 
		ENDIF
	NEXT
ELSE					;素質번호順表示
	LOCAL:2 = 0	;桁位置
	FOR LOCAL:0, 0, 150	;表示ループ。非表示は飛ばす
		IF TALENT:(ARG:0):(LOCAL:0) && TALENT_TYPE(LOCAL:0)
			LOCALS:0 = [%GET_TALENTNAME(ARG:0, LOCAL:0)%]%" "%
			LOCAL:3 = STRLENS(LOCALS:0)
			LOCAL:2 += LOCAL:3
			IF LOCAL:2 > LINE_WIDTH	;改行処理。行頭インデント無し
				PRINTL 
				LOCAL:2 = LOCAL:3
			ENDIF
			CALL FPRINT(LOCALS:0, GET_RAINBOW(TALENT_TYPE(LOCAL:0)))
		ENDIF
	NEXT
	PRINTL 
ENDIF
RETURN RESULT


;────────────────────────────────────
;죠교자のテンション表示
;────────────────────────────────────
;バーの色が少しキツイ感が했다ので微調整。無駄にボタン化するのを回避@revkoishi(14/05/22)
@PRINT_TENSION(ARG:0)
LOCAL:1 = RESULT
SELECTCASE TALENT:MASTER:독심능력
	CASE 1
		PRINTFORML 이성[%TENSION(ARG:0, GETNUM(BASE, "이성"))%]\n흥미[%TENSION(ARG:0, GETNUM(BASE, "흥미"))%]
		PRINTFORML 가립[%TENSION(ARG:0, GETNUM(BASE, "초조함"))%]\n만족[%TENSION(ARG:0, GETNUM(BASE, "만족"))%]
	CASE 2
		LOCAL:0 = GETCOLOR()
		CALL PRINT_BASEL("이성", ARG:0, GETNUM(BASE, "이성"), 32, 1)
		CALL PRINT_BASEL("흥미", ARG:0, GETNUM(BASE, "흥미"), 32, 1)
		CALL PRINT_BASEL("초조", ARG:0, GETNUM(BASE, "초조함"), 32, 1)
		CALL PRINT_BASEL("만족", ARG:0, GETNUM(BASE, "만족"), 32, 1)
		PRINTPLAINFORM 죄악감[{CFLAG:ARG:5}] 아라이먼트
		SETCOLOR CFLAG:(ARG:0):아라이멘도 >= 0 ? 0x0041FF # 0xFF2800
		BAR CFLAG:(ARG:0):아라이멘도 + 100, 199, 32
		SETCOLOR LOCAL:0
		PRINTPLAINFORM [{CFLAG:(ARG:0):아라이멘도}]
		PRINTL 
	CASEELSE
		PRINTFORML %CALLNAME:(ARG:0)%의 기분에 대해 아직 잘 모른다…
ENDSELECT
RETURN LOCAL:1
