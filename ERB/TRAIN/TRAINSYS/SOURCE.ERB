@SOURCE_CHECK
#LOCALSIZE 20
;────────────────────────────────────
;죠교대상행동による精神的負荷を設定（地の文・구상で参照する）
;────────────────────────────────────
;선택당했다REACTの負荷を保存
;消費기력が最も低いCOMなら0、消費기력が100未満なら1、そうでなければ2
TFLAG:REACT부하 = GET_COMEFFORT(TRAINNAME:SELECTCOM)

;────────────────────────────────────
;REACT분류による快SOURCE等보정
;────────────────────────────────────
CALL REACT_TYPE_CHECK

;────────────────────────────────────
;変数整理
;────────────────────────────────────
CALL SOURCE_CHECK_DEBUG("SOURCE.ERB外", 1)

;처녀상실・동정상실のリセット
CVARSET TCVAR, 50, 0
CVARSET TCVAR, 51, 0

TFLAG:전회질싸 = 0

;────────────────────────────────────
;죠교대상행동による기력消費
;────────────────────────────────────
CALLF COMABLE_CONT(TRAINNAME:SELECTCOM, 1)
DOWNBASE:MASTER:기력 += COM_VITALITY(, TRAINNAME:SELECTCOM)
CALL SOURCE_CHECK_DEBUG("죠교대상행동")

;────────────────────────────────────
;遅延당했다죠교자행동の実行
;────────────────────────────────────
RESULT = 0
;죠교자행동が죠교대상の행동によって左右される場合の処理
CALL ACTION_APPLY2
SIF RESULT
;OriginalString : 
	PRINTW

;戻りが1でなければ呼ぶ
IF RESULT != 1
	;@ACTION_APPLY2대상外の一般処理
	CALL REACTION_MESSAGE
;OriginalString : 
	PRINTW
ENDIF
CALL SOURCE_CHECK_DEBUG("죠교자추가행동")

;────────────────────────────────────
;服薬などによる소변ゲージ発生
;────────────────────────────────────
;소변が発生して初めてMAXBASE:MASTER:소변が用意される
;조루、싫증に영향される
SIF BASE:MASTER:소변 > MAXBASE:MASTER:소변
	MAXBASE:MASTER:소변 = 10000 - TALENT:MASTER:조루 * 5000 + TALENT:MASTER:싫증 * 5000

;────────────────────────────────────
;징계・모드の処理
;────────────────────────────────────
;징계・모드中は関連SOURCEにボーナスが入る
IF TFLAG:징계・플래그 > 0
	TIMES SOURCE:MASTER:고통 , 1.25
	TIMES SOURCE:MASTER:구속 , 1.25
	TIMES SOURCE:MASTER:트라우마 , 1.15
ENDIF

;────────────────────────────────────
;기력０による上下の処理（前半）
;────────────────────────────────────
;기력0で快感にボーナス
IF BASE:MASTER:기력 <= 0
	TIMES SOURCE:MASTER:쾌Ｃ, 1.25
	TIMES SOURCE:MASTER:쾌Ｖ, 1.25
	TIMES SOURCE:MASTER:쾌Ａ, 1.25
	TIMES SOURCE:MASTER:쾌Ｂ, 1.25
ENDIF

IF BASE:기력 <= 0
	TIMES SOURCE:쾌Ｃ, 1.25
	TIMES SOURCE:쾌Ｖ, 1.25
	TIMES SOURCE:쾌Ａ, 1.25
	TIMES SOURCE:쾌Ｂ, 1.25
ENDIF

;────────────────────────────────────
;快感のソース
;────────────────────────────────────
;연속ボーナス
;연속数が1ずつ増えていく
;元は倍々に増えてたけど、短期決戦に持ち込まれても面白くないと思ったので@これみ
IF TFLAG:ACT == TFLAG:전ACT
	TFLAG:동일행동보너스 += 1
ELSE
	TFLAG:동일행동보너스 = 1
ENDIF
;절정用文字列初期化
VARSET STR, "", 1030, 1038

;快感系ソースを乗算する装備アイテムを呼び出す
CALL EQUIP_CHECK2

;SOURCE計算
SIF SOURCE:MASTER:쾌Ｃ + SOURCE:MASTER:쾌Ｖ + SOURCE:MASTER:쾌Ａ + SOURCE:MASTER:쾌Ｂ > 0
	CALL CALC_SOURCE00
CALL SOURCE_CHECK_DEBUG("SOURCE00")

SIF SOURCE:쾌Ｃ + SOURCE:쾌Ｖ + SOURCE:쾌Ａ + SOURCE:쾌Ｂ > 0
	CALL CALC_SOURCE40
CALL SOURCE_CHECK_DEBUG("SOURCE40")

;────────────────────────────────────
;行為・精神・反応のソース
;────────────────────────────────────
FOR LOCAL, 10, 40
	;SOURCE計算
	IF SOURCE:MASTER:LOCAL > 0
		CALLFORM CALC_SOURCE{LOCAL}
		CALL SOURCE_CHECK_DEBUG(@"SOURCE{LOCAL}")
	ENDIF
NEXT

;────────────────────────────────────
;조수보정という名のサボり
;────────────────────────────────────
;조수が죠교자に協力すると、죠교대상の全UPが1.5倍になる
IF TFLAG:조수의방침 == 1
	FOR LOCAL, 0, 14
		TIMES CUP:MASTER:LOCAL, 1.50
	NEXT
ENDIF

;────────────────────────────────────
;죠교대상の反応による変動（最初はこれがなかったがより正しく죠교대상の行為を反応するためやむ得なく追加しま했다）
;────────────────────────────────────
;쾌감을 받아들인다/안에 내!
IF IS_NOWCOMNAME("쾌감을 받아들인다/안에 내!/가게해줘！")
	TIMES CUP:MASTER:공순 , 1.10
	TIMES CUP:MASTER:불쾌 , 0.50
	TIMES CUP:MASTER:억울 , 0.25
;상냥하게 응답한다/적극적으로 한다
ELSEIF IS_NOWCOMNAME("상냥하게 응답한다/적극적으로 한다")
	TIMES CUP:MASTER:반항 , 0.80
	TIMES CUP:MASTER:불쾌 , 0.60
	TIMES CUP:MASTER:억울 , 0.20
;애정을 담아 한다/더 하드하게 해/기분좋게 해
ELSEIF IS_NOWCOMNAME("애정을 담아 한다/더 하드하게 해/기분 좋다!/기분좋게 해")
	TIMES CUP:MASTER:공포 , 0.50
	TIMES CUP:MASTER:공순 , 1.25
	TIMES CUP:MASTER:불쾌 , 0.20
	TIMES CUP:MASTER:억울 , 0.05
	TFLAG:호감도업 += 1
ENDIF

;────────────────────────────────────
;相性による上下の処理(미구현)
;────────────────────────────────────
;R = NO:PLAYER
;IF RELATION:R != 0
;	PRINTFORML ＜相性{RELATION:R/100}.{RELATION:R%100}倍＞
;ENDIF

;────────────────────────────────────
;죠교자の체력/기력追加消費（最初はソースだけでこれを解決했다かったがうまく表現できませんで했다…）
;────────────────────────────────────
;ACT분류 または <ACT>

;회화/<매도>
IF TFLAG:ACT분류 == 0 || IS_NOWACTNAME("매도")
	DOWNBASE:기력 += 25
;애무
ELSEIF TFLAG:ACT분류 == 1
	DOWNBASE:체력 += 30
	DOWNBASE:기력 += 50
;도구
ELSEIF TFLAG:ACT분류 == 2
	DOWNBASE:체력 += 40
	DOWNBASE:기력 += 35
;성교
ELSEIF TFLAG:ACT분류 == 3
	DOWNBASE:체력 += 60
	DOWNBASE:기력 += 50
;수치
ELSEIF TFLAG:ACT분류 == 4
	;<자위과시>
	SIF IS_NOWACTNAME("자위과시")
		DOWNBASE:기력 += 40
	;<수치플레이>/<야외플레이>
	SIF IS_NOWACTNAME("수치플레이/야외플레이")
		DOWNBASE:체력 += 40
	DOWNBASE:체력 += 15
	DOWNBASE:기력 += 40
;봉사
ELSEIF TFLAG:ACT분류 == 5
	;<이라마치오>
	SIF IS_NOWACTNAME("이라마치오")
		DOWNBASE:기력 += 40
	DOWNBASE:체력 += 5
	DOWNBASE:기력 += 30
;가학/<매도>
ELSEIF TFLAG:ACT분류 == 6 && !IS_NOWACTNAME("매도")
	DOWNBASE:체력 += 50
	DOWNBASE:기력 += 15
;이상/<방치플레이>
ELSEIF TFLAG:ACT분류 == 7 && !IS_NOWACTNAME("방치플레이")
	DOWNBASE:체력 += 35
	DOWNBASE:기력 += 60
ENDIF

;────────────────────────────────────
;기력０による上下の処理（後半）
;────────────────────────────────────
;죠교대상기력0時、効果にボーナス
IF BASE:MASTER:기력 <= 0
	TIMES CUP:MASTER:욕정 , 1.40
	TIMES CUP:MASTER:공포 , 1.20
	TIMES CUP:MASTER:반항 , 0.60
	TIMES CUP:MASTER:불쾌 , 0.80
ENDIF

LOCAL = DOWNBASE:MASTER:기력 - BASE:MASTER:기력
;저항불능なら
IF TFLAG:저항불능
	;기력は減らずに체력にまわされる
	DOWNBASE:MASTER:체력 += DOWNBASE:MASTER:기력
	DOWNBASE:MASTER:기력 = 0
;기력が負になる減少量なら
ELSEIF LOCAL > 0
	;기력減少量の1/4が체력にまわされ、이성も減少する
	DOWNBASE:MASTER:체력 += LOCAL
	DOWNBASE:MASTER:이성 += LOCAL / 2
ENDIF

;────────────────────────────────────
;죠교대상のゲージの変動
;────────────────────────────────────
BASE:MASTER:체력 -= DOWNBASE:MASTER:체력
BASE:MASTER:기력 -= DOWNBASE:MASTER:기력
BASE:MASTER:사정 -= DOWNBASE:MASTER:사정
BASE:MASTER:모유 -= DOWNBASE:MASTER:모유
BASE:MASTER:소변 -= DOWNBASE:MASTER:소변
;이성減少量보정
SIF TALENT:MASTER:자제심
	TIMES DOWNBASE:MASTER:이성 , 0.75
SIF TALENT:MASTER:충동적
	TIMES DOWNBASE:MASTER:이성 , 1.25
BASE:MASTER:이성 -= DOWNBASE:MASTER:이성

;────────────────────────────────────
;죠교자のゲージの変動
;────────────────────────────────────
;흥미の処理、죠교대상の反応や변화を見せないとつまらない

;LOCAL:1 = 肯定系計
LOCAL:1 = SOURCE:MASTER:굴종 + SOURCE:MASTER:달성 + SOURCE:MASTER:열락
;LOCAL:2 = 쾌락系計
LOCAL:2 = CUP:MASTER:쾌Ｃ + CUP:MASTER:쾌Ｖ + CUP:MASTER:쾌Ａ + CUP:MASTER:쾌Ｂ + CUP:MASTER:욕정
;LOCAL:3 = 순종系計
LOCAL:3 = CUP:MASTER:굴복 + CUP:MASTER:공포 + CUP:MASTER:공순 + CUP:MASTER:고통 + CUP:MASTER:치정
;LOCAL:4 = 抵抗系計
LOCAL:4 = CUP:MASTER:반항 * 2 + CUP:MASTER:불쾌 + CUP:MASTER:억울
;LOCAL:5 = 죠교자쾌락系計		これがあっても良い気が했다ので追加してみる@/L
LOCAL:5 = CUP:쾌Ｃ + CUP:쾌Ｖ + CUP:쾌Ａ + CUP:쾌Ｂ
;肯定/쾌락/순종系計のいずれかが規定値に達していれば흥미増加
;(무관심にマイナス、호기심にプラス보정)
LOCAL = MAX(LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:5)
BASE:흥미 += LINE_CALC(LOCAL, "300→10,1500→50,LIMIT/10～50") + (TALENT:호기심 - TALENT:무관심) * LINE_CALC(LOCAL, "300→5,1500→20,LIMIT/5～20")

;민감の죠교자は負の反応にうんざり
;민감時、抵抗系計が規定値に達していれば흥미減少
SIF TALENT:민감
	BASE:흥미 -= LINE_CALC(LOCAL:4, "0→0,1500→150,LIMIT/0～150")

;自然소모
;(흥미が1/3を切ると소모速度が下がる)
;죠교레벨による소모加速をオミット했다版
;これ機械的すぎてひどいなｗ
IF BASE:흥미 > MAXBASE:흥미 / 3
	BASE:흥미 -= MAXBASE:흥미 * 20 / 200
ELSE
	BASE:흥미 -= MAXBASE:흥미 * 20 / 300
ENDIF

;흥미を0-MAXBASEの範囲に収める
BASE:흥미 = LIMIT(BASE:흥미, 0, MAXBASE:흥미)

;흥미低下による기력소모
;(活発な성격であるほど기력소모量は大きい)
SIF BASE:흥미 < 200
	DOWNBASE:기력 += (300 - BASE:흥미) / (10 + TALENT:무관심 * 3 - TALENT:호기심 * 3 + TALENT:감정결여 * 2 - TALENT:정서적 * 2)

;초조함の処理
;抵抗系計から보정して초조함を算出
DOWNBASE:초조함 -= LIMIT(LOCAL:4 / 15, 20, 100) + (TALENT:민감 - TALENT:신중함) * LIMIT(LOCAL:4 / 75 + 5, 10, 25) + (TALENT:건방짐 - TALENT:얌전한) * LIMIT(LOCAL:4 / 150, 2, 10)

;애원/날뛴다/要求を거부
;(REACT時素質によって追加보정)
IF IS_COMGRONAME("용서를 빌다")
	DOWNBASE:초조함 -= 25 - TALENT:마음씨착한 * 25 + TALENT:심술궂음 * 10 + TALENT:새드 * 25 - TALENT:MASTER:연모 * 5 - TALENT:MASTER:예속 * 10 + PALAM:MASTER:반항 / 250
ELSEIF IS_COMGRONAME("날뛴다")
	DOWNBASE:초조함 -= 50 + PALAM:MASTER:반항 / 200 - TALENT:신중함 * 15 + TALENT:민감 * 20 + TALENT:프라이드높음 * 20 + MARK:반항각인 * 2
ELSEIF IS_COMGRONAME("거부")
	DOWNBASE:초조함 -= 25 + PALAM:MASTER:굴복 / 200 - TALENT:신중함 * 25 + TALENT:민감 * 15 + TALENT:마음씨착한 * 5 + TALENT:프라이드높음 * 10 + MARK:굴복각인
ENDIF

;광기でランダム変動、この設定に賛同できない人はかなりいるかもしれません…
SIF TALENT:광기
	DOWNBASE:초조함 -= (10 - RAND:20) * (1 + RAND:10)

;초조함時、죠교대상が순종であれば만족度に応じて초조함減衰
SIF DOWNBASE:초조함 > MAXBASE:초조함 / 3 && CUP:MASTER:반항 < 10
	DOWNBASE:초조함 += BASE:만족 / 20

;분노時、죠교強度に応じて초조함減衰
IF TCVAR:상태변화 == 6
	;공순보정
	LOCAL = CUP:MASTER:공순 * POWER(2, TALENT:마음씨착한 - TALENT:심술궂음 + 10) / 1024
	;굴복보정
	LOCAL += (CUP:MASTER:굴복 + MARK:굴복각인 * 50) * POWER(2, TALENT:솔직 + 10) / 1024
	DOWNBASE:초조함 += LOCAL
ENDIF

;BASE値変動処理
BASE:체력 -= DOWNBASE:체력
BASE:기력 -= DOWNBASE:기력
BASE:사정 -= DOWNBASE:사정
BASE:모유 -= DOWNBASE:모유
BASE:소변 -= DOWNBASE:소변

;いらいらによる이성소모
;その分だけ초조함減衰
;이성0時はその半分さらに苛立つ
IF BASE:초조함 > 700
	LOCAL = (BASE:초조함 - 500) / (5 + TALENT:광기 * RAND:3)
	IF BASE:이성 > 0
		DOWNBASE:이성 += LOCAL
		DOWNBASE:초조함 += LOCAL
	ELSE
		DOWNBASE:초조함 -= LOCAL / 2
	ENDIF
ENDIF
;BASE値変動処理
BASE:초조함 -= DOWNBASE:초조함
;초조함を0-MAXBASEの範囲に収める
BASE:초조함 = LIMIT(BASE:초조함, 0, MAXBASE:초조함)

;이성減少量が素質によって보정される
SIF TALENT:자제심
	TIMES DOWNBASE:이성 , 0.75
SIF TALENT:충동적
	TIMES DOWNBASE:이성 , 1.25
;BASE値変動処理
BASE:이성 -= DOWNBASE:이성

;BASE値変動処理
BASE:만족 -= DOWNBASE:만족

;────────────────────────────────────
;コマンド実行時の구상
;────────────────────────────────────
CALL KOJO_REACT

IF TCVAR:MASTER:REACT자세변화
	PRINTL 
	CALL POSE_SHOW
	TCVAR:MASTER:REACT자세변화 = 0
ENDIF
DRAWLINE

;────────────────────────────────────
;절정時の구상
;────────────────────────────────────
PRINTL 
FLAG:지문제어 = 0
RESULT = 0
;ビット和での指定が不具合の温床になってる気配があったので省略してみる@/L
;공중육변기플레이時の呼び出しは規制する //1.1seriesまでの暫定処置、ということで@/L
SIF !IS_NOWACTNAME("공중육변기플레이") && (MASTER_EX() || TARGET_EX())	;MASTER_EX(79) || TARGET_EX(15)
	CALL KOJO_EVENT(20)
IF RESULT
	PRINTL 
	DRAWLINE
ENDIF

RESULT = 0
SIF MASTER_EX(4) || TARGET_EX(4)
	CALL KOJO_EVENT_ASSI(20)

RESULT = 0
IF MASTER_EX(8) || TARGET_EX(8)
	ASSI = ASSI:2
	CALL KOJO_EVENT_ASSI(20)
	ASSI = ASSI:1
ENDIF

;RESULT = 0
;IF MASTER_EX(16) || TARGET_EX(16)
;	ASSI = ASSI:3
;	CALL KOJO_EVENT_ASSI(20)
;	ASSI = ASSI:1 < 0 ? ASSI:3 # ASSI:1
;ENDIF

;────────────────────────────────────
;절정関連の表示
;────────────────────────────────────
CALL PRINT_SOURCE00_40

;의존도の計算
VARSET LOCAL
;사정orＣ절정と他３種절정の和で計算
LOCAL = (MASTER_EX(, 1) || MASTER_EX(, 12)) + MASTER_EX(, 3) + MASTER_EX(, 3) + MASTER_EX(, 4)
;２乗の１０倍を基礎値とする
LOCAL = LOCAL*LOCAL*10
;방뇨・분유でも+10する
LOCAL += (MASTER_EX(, 11) + MASTER_EX(, 13)) * 10
;주인님に질내사정されていれば20を加える
LOCAL += TE_PLAYER(1) == 4 && TE_PLAYER(0) == 1 ? 20 # 0

;사정orＣ절정と他３種절정の和で計算
LOCAL:1 = (TARGET_EX(, 1) || TARGET_EX(, 12)) + TARGET_EX(, 3) + TARGET_EX(, 3) + TARGET_EX(, 4)
;２乗の１０倍を基礎値とする
LOCAL:1 = LOCAL:1*LOCAL:1*10
;분유でも+10する
LOCAL:1 += TARGET_EX(, 11) * 10
;노예に질내사정されていれば20を加える
LOCAL:1 += ME_PLAYER(1) == 4 && ME_PLAYER(0) == 2 ? 20 # 0

;의존도변화
CALL ADD_DEPEND(LOCAL:1, LOCAL)


;────────────────────────────────────
;밀어넘어짐終了
;────────────────────────────────────
;IF (SUMARRAY(NOWEX, 0, 4) || NOWEX:MASTER:사정) && TEQUIP:밀어넘어지는중 == 1
IF (NOWEX:MASTER:Ｃ절정 || NOWEX:MASTER:Ｖ절정 || NOWEX:MASTER:Ａ절정 || NOWEX:MASTER:Ｂ절정 || NOWEX:MASTER:사정) && TEQUIP:밀어넘어지는중 == 1
	TEQUIP:밀어넘어지는중 = 0
	TFLAG:밀어넘어짐경과 = TFLAG:경과시간
	PRINTFORMW %CALLNAME:TARGET%은(는) %CALLNAME:MASTER%(을)를 절정 시켜 만족한 것 같다…
ENDIF
IF TFLAG:밀어넘어짐경과 && (TFLAG:경과시간 - TFLAG:밀어넘어짐경과) > 15 && TEQUIP:밀어넘어지는중
	TEQUIP:밀어넘어지는중 = 0
	TFLAG:밀어넘어짐경과 = TFLAG:경과시간
	PRINTFORMW %CALLNAME:TARGET%은(는) %CALLNAME:MASTER%(을)를 산들요는 만족한 것 같다…
ENDIF

;────────────────────────────────────
;각인취득のチェック、そして각인による素質変動
;────────────────────────────────────
CALL MARK_CHECK_SOURCE

;────────────────────────────────────
;경험のチェック、죠교ソースの表示
;────────────────────────────────────
CALL EXP_CHECK
FOR LOCAL, 0, 50
	PRINTFORM \@ SOURCE:MASTER:LOCAL > 0 ? %SOURCENAME:LOCAL%({SOURCE:MASTER:LOCAL}) # \@
NEXT
FOR LOCAL, 0, 50
	PRINTFORM \@ SOURCE:LOCAL > 0 ? 죠교자%SOURCENAME:LOCAL%({SOURCE:LOCAL}) # \@
NEXT
PRINTL 

;────────────────────────────────────
;数値変動の表示
;────────────────────────────────────
;相性
;SIF RELATION:(NO:PLAYER) && RELATION:(NO:PLAYER) != 100
;	PRINTFORML ＜相性%TOSTR(RELATION:(NO:PLAYER), "0\\.00")%倍＞

;체력・기력の減少
PRINTFORML 체력-{DOWNBASE:MASTER:체력} 기력-{DOWNBASE:MASTER:기력} 이성-{DOWNBASE:MASTER:이성}
PRINTFORML （%CALLNAME:TARGET%）체력-{DOWNBASE:체력} 기력-{DOWNBASE:기력}

;CUP:MASTER:고통の1/30を마력として취득
TFLAG:취득마력 += CUP:MASTER:고통 / 30

;체력・기력０、피폐のメッセージもあるのでこれは필요ないと思います
;SIF BASE:MASTER:체력 <= 0
;	PRINTL ★체력０★
;SIF BASE:MASTER:기력 <= 0
;	PRINTL ★기력０★
;SIF BASE:체력 <= 0
;	PRINTL ★체력０★（죠교자）
;SIF BASE:기력 <= 0
;	PRINTL ★기력０★（죠교자）

;────────────────────────────────────
;能力上昇の呼び出し
;ここでは大まかな条件で絞り、詳細な判定はABLUP_**関数で行う
;────────────────────────────────────

;죠교대상能力

;순종
;굴복 + 공순、または공포が上昇している時
;また、죠교대상の反応が 날뛴다/要求に応じない であれば判定しない
SIF (CUP:MASTER:굴복 + CUP:MASTER:공순 > 0 || CUP:MASTER:공포 > 0) && TFLAG:REACT분류 < 6
	CALL ABLUP_0M
;욕망
;욕정上昇時
SIF CUP:MASTER:욕정 > 0
	CALL ABLUP_1M
;기교
;조교중容が봉사系であり、죠교자が快感を得た時
SIF TFLAG:ACT분류 == 5 && CUP:쾌Ｃ + CUP:쾌Ｖ + CUP:쾌Ａ + CUP:쾌Ｂ > 0
	CALL ABLUP_2M
;Ｘ감각
;죠교대상が快感を得た時
FOR LOCAL, 0, 4
	SIF CUP:MASTER:LOCAL > 0
		CALLFORM ABLUP_{3 + LOCAL}M
NEXT
;봉사정신
;조교중容が봉사系/성교봉사であるか、죠교자삽입중体位が기승위である場合
;なおかつ죠교대상が적극적으로従い、快感+욕정合計が1000突破
SIF (TFLAG:ACT분류 == 5 || TFLAG:ACT분류 == 10  || TEQUIP:성교중 == 3 ) && IS_COMGRONAME("적극적으로 한다") && CUP:MASTER:쾌Ｃ + CUP:MASTER:쾌Ｖ + CUP:MASTER:쾌Ａ + CUP:MASTER:쾌Ｂ + CUP:MASTER:욕정 > 1000
	CALL ABLUP_7M
;노출증
;痴情上昇時
SIF CUP:MASTER:치정 > 0
	CALL ABLUP_8M
;레즈끼
;죠교대상、죠교자がともに男ではない
SIF TALENT:MASTER:남자 == 0 && TALENT:남자 == 0
	CALL ABLUP_9M
;ＢＬ끼
;죠교대상、죠교자がともに男
SIF TALENT:MASTER:남자 && TALENT:남자
	CALL ABLUP_10M
;마조끼
;고통 + 불쾌上昇量が억울上昇量を上回る
SIF CUP:MASTER:고통 + CUP:MASTER:불쾌 > CUP:MASTER:억울
	CALL ABLUP_11M
;자위중독
;죠교대상자위중
SIF GETBIT(TEQUIP:자위중, 0)
	CALL ABLUP_12M
;정액중독
;죠교자または죠교대상が사정
SIF NOWEX:사정 || NOWEX:MASTER:사정
	CALL ABLUP_13M
;레즈중독
;죠교대상、죠교자がともに男ではない
SIF TALENT:MASTER:남자 == 0 && TALENT:남자 == 0
	CALL ABLUP_14M
;고통중독
;고통上昇量が억울上昇量を上回る
SIF CUP:MASTER:고통 > CUP:MASTER:억울
	CALL ABLUP_15M
;구속중독
;죠교대상が何らかの구속を受ける
SIF TEQUIP:밧줄 || TEQUIP:눈가리개 || TEQUIP:볼개그 || TEQUIP:삼각목마
	CALL ABLUP_16M

;죠교자能力

;욕망
;죠교자が快感を得た時
SIF CUP:쾌Ｃ + CUP:쾌Ｖ + CUP:쾌Ａ + CUP:쾌Ｂ > 0
	CALL ABLUP_1T
;기교
;죠교대상절정時
SIF NOWEX:MASTER:Ｃ절정 + NOWEX:MASTER:Ｖ절정 + NOWEX:MASTER:Ａ절정 + NOWEX:MASTER:Ｂ절정 > 0
	CALL ABLUP_2T
;Ｘ감각
;죠교자が快感を得た時
FOR LOCAL, 0, 4
	SIF CUP:LOCAL > 0
		CALLFORM ABLUP_{3 + LOCAL}T
NEXT
;봉사정신
;조교중容が애무/도구系
SIF TFLAG:ACT분류 == 1 || TFLAG:ACT분류 == 2
	CALL ABLUP_7T
;노출증
;現在条件なし
;SIF ???
	;CALL ABLUP_8T
;레즈끼
;죠교대상、죠교자がともに男ではない
SIF TALENT:MASTER:남자 == 0 && TALENT:남자 == 0
	CALL ABLUP_9T
;ＢＬ끼
;죠교대상、죠교자がともに男
SIF TALENT:MASTER:남자 && TALENT:남자
	CALL ABLUP_10T
;정액중독
;죠교자または죠교대상が사정
SIF NOWEX:사정 || NOWEX:MASTER:사정
	CALL ABLUP_13T
;레즈중독
;죠교대상、죠교자がともに男ではない
SIF TALENT:MASTER:남자 == 0 && TALENT:남자 == 0
	CALL ABLUP_14T
;죠교系列技能
;対応する죠교系列について判定
SIF 0 <= TFLAG:ACT분류 && TFLAG:ACT분류 <= 8
	CALLFORM ABLUP_{20 + TFLAG:ACT분류}T
;성교
;조교중容が성교봉사系
SIF TFLAG:ACT분류 == 10
	CALL ABLUP_23T

@SOURCE_CHECK_DEBUG(ARGS, ARG)
RETURN 0
SIF ARG
	VARSET LOCAL
NOSKIP
	PRINTFORML %ARGS% {DOWNBASE:MASTER:기력}(+{DOWNBASE:MASTER:기력 - LOCAL})
ENDNOSKIP
LOCAL = DOWNBASE:MASTER:기력
RETURN 0
