;────────────────────────────────────
;죠교대상快感（ＣＶＡＢ）のソースと愛液、절정の処理（노출ソース、이탈ソース、달성ソース、쾌Ｃ、쾌Ｖ、쾌Ａ、쾌Ｂ、윤활、욕망、굴복、공순、억울、체력、기력、사정、모유、소변、이성、흥미、호감도、경험치）
;────────────────────────────────────
@CALC_SOURCE00

LOCALS = CALC_SOURCE00

;最初の変動
CALLFORM %LOCALS%_TALENT

;快感（ＣＶＡＢ）の上昇
CALLFORM %LOCALS%_PLEASURE

;욕망、굴복、공순の上昇
CALLFORM %LOCALS%_DESIRE

;억울の上昇
CALLFORM %LOCALS%_DEPRESSION

;愛液処理
CALLFORM %LOCALS%_MOIST

;절정、체력、기력、사정、모유、소변などの処理
CALLFORM %LOCALS%_EX

;────────────────────────────────────
;最初の変動
;────────────────────────────────────
@CALC_SOURCE00_TALENT
#DIM LCOUNT
;ＣＶＡＢ部位について
FOR LCOUNT, 0, 4
	;민감と둔감を処理
	;Ｘ민감
	SIF TALENT:MASTER:(100 + LCOUNT * 2)
		TIMES SOURCE:MASTER:LCOUNT, 1.50
	;Ｘ둔감
	SIF TALENT:MASTER:(101 + LCOUNT * 2)
		TIMES SOURCE:MASTER:LCOUNT, 0.70
	;죠교대상の상태
	IF 0
	ELSEIF M_COND("피폐")
		TIMES SOURCE:MASTER:LCOUNT, 0.80
	ELSEIF M_COND("쇠약")
		TIMES SOURCE:MASTER:LCOUNT, 0.65
	ELSEIF M_COND("무기력")
		TIMES SOURCE:MASTER:LCOUNT, 0.70
	ELSEIF M_COND("정욕")
		TIMES SOURCE:MASTER:LCOUNT, 1.20
	ENDIF
NEXT

;────────────────────────────────────
;快感（ＣＶＡＢ）の上昇
;────────────────────────────────────
@CALC_SOURCE00_PLEASURE
#DIM LCOUNT

;욕정PALAMが[0→4, 1000→7, 2000→9, 3000→10, 4000→11, 5000→12]をLOCALへ(4～12)
LOCAL = LIMIT(GET_REVISION_SQRT(PALAM:MASTER:욕정, 5000, 8) + 4, 4, 12)
FOR LCOUNT, 0, 4
	;연속ボーナス(연속횟수倍に増加するボーナス)
	CUP:MASTER:LCOUNT += SOURCE:MASTER:LCOUNT * LOCAL * TFLAG:동일행동보너스 / 10
	;조교중に集中的に責られると弱くなる気がする(快Ｘ보정処理)
	CUP:MASTER:LCOUNT = CUP:MASTER:LCOUNT * CFLAG:MASTER:(30 + LCOUNT) / 50
NEXT
;快Ｘ総計の1/100を마력として취득
;TFLAG:취득마력 += SUMARRAY(CUP:MASTER, 0, 4) / 100
TFLAG:취득마력 += (CUP:MASTER:쾌Ｃ + CUP:MASTER:쾌Ｖ + CUP:MASTER:쾌Ａ + CUP:MASTER:쾌Ｂ) / 100

;────────────────────────────────────
;욕망、굴복、공순の上昇
;────────────────────────────────────
@CALC_SOURCE00_DESIRE
#DIM LCOUNT

;ABL:욕망をみる
FOR LCOUNT, 0, 4
	;ABL:MASTER:욕망が[0→5%, 5→30%]を快ＸSOURCEに乗算
	LOCAL = SOURCE:MASTER:LCOUNT * LINE_CALC(ABL:MASTER:욕망, "0→5,5→30,LIMIT/5～30") / 100
	;욕정PALAMに加算
	CUP:MASTER:욕정 += LOCAL
	;쾌Ａのみ굴복PALAMに加算
	SIF LCOUNT == 2
		CUP:MASTER:굴복 += LOCAL
	;쾌Ｖのみ공순PALAMに加算
	SIF LCOUNT == 1
		CUP:MASTER:공순 += LOCAL / 2
NEXT

;────────────────────────────────────
;억울の上昇
;────────────────────────────────────
@CALC_SOURCE00_DEPRESSION

;죠교대상に쾌감을부정、수줍음、억압素質が存在すれば
IF TALENT:MASTER:억압 || TALENT:MASTER:수줍음 || TALENT:MASTER:쾌감을부정
	;快ＸSOURCE総計の1/10を用意
	;LOCAL = SUMARRAY(SOURCE, 0, 4) / 10
	LOCAL = SOURCE:MASTER:쾌Ｃ + SOURCE:MASTER:쾌Ｖ + SOURCE:MASTER:쾌Ａ + SOURCE:MASTER:쾌Ｂ
	;ABL:MASTER:욕망が[0→100%, 5→10%]をさらに乗算
	LOCAL = LOCAL * LINE_CALC(ABL:MASTER:욕망, "0→100,5→10,LIMIT/100～10") / 100
;存在しなければ0
ELSE
	LOCAL = 0
ENDIF
;정욕の상태ならなしにする
SIF M_COND("정욕")
	LOCAL = 0
;억울PALAMに加算
CUP:MASTER:억울 += LOCAL

;────────────────────────────────────
;愛液処理
;────────────────────────────────────
@CALC_SOURCE00_MOIST

;快Ｘ総計が合わせて10超過ならその20%の液体のソースが加わる（남자はダメ）
;LOCAL = SUMARRAY(CUP:MASTER, 0, 4)
LOCAL = CUP:MASTER:쾌Ｃ + CUP:MASTER:쾌Ｖ + CUP:MASTER:쾌Ａ + CUP:MASTER:쾌Ｂ
IF LOCAL > 10 && TALENT:MASTER:남자 == 0
	;젖기쉬움と젖기어려움はここで処理
	SIF TALENT:MASTER:젖기쉬움
		TIMES LOCAL , 2.00
	SIF TALENT:MASTER:젖기어려움
		TIMES LOCAL , 0.50
	;윤활PALAMに加算
	CUP:MASTER:윤활 += LOCAL / 5
ENDIF

;────────────────────────────────────
;절정、체력、기력、사정、모유、소변などの処理
;────────────────────────────────────
@CALC_SOURCE00_EX
#DIM LCOUNT
#DIM DYNAMIC 절정強度, 4
#DIM DYNAMIC 참다強度
#DIM DYNAMIC 애태움強度, 4
#DIM DYNAMIC 快感負担
#DIM DYNAMIC 사정負担
#DIM DYNAMIC 분유負担
#DIM DYNAMIC 시오후키負担
#DIM DYNAMIC 절정負担
#DIM DYNAMIC 絞り強度

;발기判定(페니스存在時のみ)
IF PENIS(MASTER)
	;쾌Ｃの1/10、他の快Ｘの1/20を加算
	TCVAR:MASTER:금회발기 += CUP:MASTER:쾌Ｃ / 10 + (CUP:MASTER:쾌Ｖ + CUP:MASTER:쾌Ａ + CUP:MASTER:쾌Ｂ) / 20
	;욕정の1/10を加算
	TCVAR:MASTER:금회발기 += CUP:MASTER:욕정 / 10
	;고통時減算、ただし마조끼ABLにより増加へ向かう
	TCVAR:MASTER:금회발기 += CUP:MASTER:고통 * (GET_ABL(MASTER, "마조끼") - 20) / 300
	;치정時加算、노출증ABLによりさらに増大
	TCVAR:MASTER:금회발기 += CUP:MASTER:치정 * GET_ABL(MASTER, "노출증") / 30
ENDIF

;変数のリセット
VARSET 절정強度
VARSET 참다強度
VARSET 애태움強度
VARSET 快感負担
VARSET 사정負担
VARSET 분유負担
VARSET 시오후키負担
VARSET 절정負担
VARSET 絞り強度

;ディープスロートをリセット
DEEP_THROAT = 0

;절정Ｃ
;쾌Ｃ、쾌ＣPALAM合計から刺激強度を用意
LOCAL = CUP:MASTER:쾌Ｃ + PALAM:MASTER:쾌Ｃ

;페니스があるなら발기도が高い、페니스がないなら윤활が高く쾌Ｃ1000以上のとき
;それぞれ미약・로션で簡単にイかせられるのもまた良しか
;가버려で쾌Ｃ存在時
IF ((TCVAR:MASTER:발기도 >= 1000 && PENIS(MASTER)) || (PALAM:MASTER:윤활 >= 250 && !PENIS(MASTER) && PALAM:MASTER:쾌Ｃ >= 1000)) && TCVAR:MASTER:가버려 && CUP:MASTER:쾌Ｃ > 0
	;참다中、가버려に耐えられるだけのABLが存在し、가게해줘！COMではなく、애태움상태ではない
	IF (TCVAR:MASTER:참다 && TCVAR:MASTER:가버려 < ABL:MASTER:Ｃ감각 * 3) && SELECTCOM != 70 && TCVAR:MASTER:애태움도 == 0
		;刺激が強ければ가버려상태が限界に近づく
		TCVAR:MASTER:가버려 += LOCAL / 2000
		;참다強度値に今回の刺激の強さを代入
		참다強度 = MAX(LOCAL / 10000, 1)
	;애태움中
	ELSEIF TCVAR:MASTER:애태움도 > 1
		;애태움도を1ずつ減らす
		TCVAR:MASTER:애태움도 -= 1
		;1未満にはならない
		TCVAR:MASTER:애태움도 = MAX(TCVAR:MASTER:애태움도, 1)
		;애태움強度値に今回の刺激の強さを代入
		애태움強度:0 = MAX(LOCAL / 10000, 1)
	;절정強度処理
	ELSE
		절정強度:0 = CALC_SOURCE00_EX_P(0, LOCAL)
	ENDIF
ENDIF

;절정Ｖ
;절정強度処理
절정強度:1 = CALC_SOURCE00_EX_P(1, CUP:MASTER:쾌Ｖ + PALAM:MASTER:쾌Ｖ)

;절정Ａ
;절정強度処理
절정強度:2 = CALC_SOURCE00_EX_P(2, CUP:MASTER:쾌Ａ + PALAM:MASTER:쾌Ａ)

;절정Ｂ
;절정強度処理
절정強度:3 = CALC_SOURCE00_EX_P(3, CUP:MASTER:쾌Ｂ + PALAM:MASTER:쾌Ｂ)

;절정すると동일행동보너스を半減する
SIF MAXARRAY(절정強度) > 0
	TFLAG:동일행동보너스 = MAX(TFLAG:동일행동보너스 / 2, 1)

;절정強度合計から절정負担を計算
절정負担 = 500 * SUMARRAY(절정強度)

;快感による체력기력이성소모
;快ＸSOURCE合計が[0→0, 2000→44, 4000→63, 6000→77, 8000→89, 10000→100]を快感負担へ
快感負担 = GET_REVISION_SQRT(SUMARRAY(SOURCE, 0, 4), 10000, 100)
;죠교の度合いによる軽減
;죠교대상죠교레벨+욕망ABLが[0→100%, 2→91%, 4→83%, 6→75%, 8→67%, 10→59%, 12→51%]を快感負担に乗算(50～100%)
LOCAL = GET_REVISION_SQRT_PROP(CFLAG:MASTER:죠교레벨 + ABL:MASTER:욕망, 1, 5, 12, 50)
快感負担 -= 快感負担 * LIMIT(LOCAL, 50, 100) / 100
;快感に対する反応
;消極的な受容もしくは참다플래그ありの場合
SIF IS_COMGRONAME("소극적으로 한다/용서를 빌다") || TCVAR:MASTER:참다
	TIMES 快感負担, 2.00
;受容の場合		서투르다고 모욕한다が派生：こきおろすで『태연하게 한다』扱いになる場合は弾く
SIF IS_COMGRONAME("적극적으로 한다/태연하게 한다/受け入れる") && !TCVAR:MASTER:참다 && !IS_NOWCOMNAME("서투르다고 모욕한다")
	TIMES 快感負担, 0.50

;페니스存在時
IF PENIS(MASTER)
	;절정決定で발기도が高く가버려
	IF 절정強度:0 > 0 && TCVAR:MASTER:발기도 >= 1000 && TCVAR:MASTER:가버려
		;사정ゲージがない
		IF BASE:MASTER:사정 == 0
			STR:1034 = 드라이사정
			DOWNBASE:MASTER:체력      += 200
			DOWNBASE:MASTER:기력      += 80
			NOWEX:MASTER:사정          = 1
			TCVAR:MASTER:사정          = 1
			TCVAR:MASTER:드라이사정        = 1
			TCVAR:MASTER:드라이사정수     += 1
		;強절정
		ELSEIF 절정強度:0 == 2
			STR:1034 = 대량사정
			사정負担 = 500
			NOWEX:MASTER:사정 = 3
			TCVAR:MASTER:사정 = 3
		;통상の절정
		ELSE
			STR:1034 = 사정
			사정負担 = 300
			NOWEX:MASTER:사정 = 2
			TCVAR:MASTER:사정 = 2
		ENDIF
	;特殊な사정判定
	;조루(高쾌Ｃ時、사정可能でＣ감각ABLが低い時確率で)
	ELSEIF (CUP:MASTER:쾌Ｃ + PALAM:MASTER:쾌Ｃ) >= 10000 && BASE:MASTER:사정 > 0 && ABL:MASTER:Ｃ감각 < RAND:(ABL:MASTER:Ｃ감각 + 3)
		LOCAL = 0
		;ＶＡ절정強度が十分高いか
		LOCAL += 절정強度:1 + 절정強度:2 > 2 + RAND:2 - TALENT:MASTER:조루 + TALENT:MASTER:싫증
		;ＶＡＢ절정強度が十分高いか
		LOCAL += 절정強度:1 * 3 + 절정強度:2 * 3 + 절정強度:3 * 2 > 6 + RAND:3 - TALENT:MASTER:조루 + TALENT:MASTER:싫증
		;쾌Ｃが十分高かったとき
		LOCAL += CUP:MASTER:쾌Ｃ > 1000 && RAND:4 > 1 - TALENT:MASTER:조루 + TALENT:MASTER:싫증
		;それぞれ確率で조루사정
		IF LOCAL
			STR:1034 = 사정（조루）
			사정負担 = 300
			NOWEX:MASTER:사정 = 4
			TCVAR:MASTER:사정 = 4
			CDOWN:MASTER:쾌Ｃ = (PALAM:MASTER:쾌Ｃ + CUP:MASTER:쾌Ｃ) / 2
		ENDIF
	;前立腺責め사정
	ELSEIF TCVAR:MASTER:전립선자극
		;가버려or쾌Ｃ8000以上時、対応部位절정するか전립선자극が強いと、強制사정
		IF (TCVAR:MASTER:가버려 ||PALAM:MASTER:쾌Ｃ >= 8000) && ((TALENT:MASTER:남자 && 절정強度:2 > 0) || (TALENT:MASTER:후타나리 && 절정強度:1 > 0) || TCVAR:MASTER:전립선자극 > 1)
			;사정ゲージがない
			IF BASE:MASTER:사정 == 0
				STR:1034 = 드라이사정（前立腺）
				DOWNBASE:MASTER:체력  += 200
				DOWNBASE:MASTER:기력  += 80
				NOWEX:MASTER:사정      = 1
				TCVAR:MASTER:사정      = 1
				TCVAR:MASTER:드라이사정    = 2
				TCVAR:MASTER:드라이사정수 += 1
			ELSE
				STR:1034 = 사정（前立腺）
				사정負担 = 300
				NOWEX:MASTER:사정 = 5
				TCVAR:MASTER:사정 = 5
			ENDIF
		ENDIF
	ENDIF
	;空でない사정時は정액汚れを追加
	IF NOWEX:MASTER:사정 > 1
		;죠교대상의 페니스
		CALL SET_STAIN("페니스", "정액", MASTER)
		;죠교자の(コンビネーションなら조수も含める)
		IF 0
		;바기나(성교봉사時)
		ELSEIF MC_PLAYER(1) == 4
			CALL SET_STAIN("바기나", "정액", TARGET)           ;죠교자の바기나
			;SOURCE_MESSAGE的にも성교봉사時の질내사정はTARGETになっているので、조수関連はとりあえず(面倒なので)見送り
			;コンビネーション時の(ASSI:1)も処理するなら条件を吟味する필요もありそう
		;항문(성교봉사時)
		ELSEIF MC_PLAYER(1) == 5
			CALL SET_STAIN("항문", "정액", TARGET)             ;죠교자の항문
		;손(손으로 애무)
		ELSEIF GET_ACTNAME(GET_NORMALACTNUM(TFLAG:ACT)) == "손으로 애무" && MC_PLAYER(1) == 2
			CALL SET_STAIN("손", "정액", TARGET)                 ;죠교자の손
			;コンビネーション派生時
			SIF ASSI:1 > 0 && TCVAR:(ASSI:1):조수방침 == GET_ASSIMENUNUM("コンビネーション")
				CALL SET_STAIN("손", "정액", (ASSI:1))           ;조수１の손
		;입(입으로 애무)/顔射자위절정
		ELSEIF (GET_ACTNAME(GET_NORMALACTNUM(TFLAG:ACT)) == "입으로 애무" && MC_PLAYER(1) == 1) || ME_PLAYER(1) == 9
			CALL SET_STAIN("입", "정액", TARGET)                 ;죠교자の입
			;コンビネーション派生時
			SIF ASSI:1 > 0 && TCVAR:(ASSI:1):조수방침 == GET_ASSIMENUNUM("コンビネーション")
				CALL SET_STAIN("입", "정액", (ASSI:1))           ;조수１の입
		;바기나(성기마찰한다)
		ELSEIF GET_ACTNAME(GET_NORMALACTNUM(TFLAG:ACT)) == "성기마찰한다" && MC_PLAYER(1) == 8
			CALL SET_STAIN("바기나", "정액", TARGET)           ;죠교자の바기나
			;コンビネーション派生時
			SIF ASSI:1 > 0 && TCVAR:(ASSI:1):조수방침 == GET_ASSIMENUNUM("コンビネーション")
				CALL SET_STAIN("바기나", "정액", (ASSI:1))     ;조수１の바기나
		;가슴(파이즈리한다)
		ELSEIF GET_ACTNAME(GET_NORMALACTNUM(TFLAG:ACT)) == "파이즈리한다" && MC_PLAYER(1) == 6
			CALL SET_STAIN("가슴", "정액", TARGET)                 ;죠교자の가슴
			;コンビネーション派生時
			SIF ASSI:1 > 0 && TCVAR:(ASSI:1):조수방침 == GET_ASSIMENUNUM("コンビネーション")
				CALL SET_STAIN("가슴", "정액", (ASSI:1))           ;조수１の가슴
		;발(풋잡하기)
		ELSEIF GET_ACTNAME(GET_NORMALACTNUM(TFLAG:ACT)) == "풋잡하기" && MC_PLAYER(1) == 7
			CALL SET_STAIN("발", "정액", TARGET)                 ;죠교자の발
			;コンビネーション派生時
			SIF ASSI:1 > 0 && TCVAR:(ASSI:1):조수방침 == GET_ASSIMENUNUM("コンビネーション")
				CALL SET_STAIN("발", "정액", (ASSI:1))           ;조수１の발
		;その他
		ELSE
			;何もしない
		ENDIF
		;후타나리ではじめての사정は이탈が付く
		SIF TALENT:MASTER:후타나리 && EXP:MASTER:사정경험 == 0 && CFLAG:MASTER:죠교레벨 < 10
			SOURCE:MASTER:이탈 += 500 * (25 - ABL:MASTER:순종 - CFLAG:MASTER:죠교레벨) / 25
	ENDIF
ENDIF

;Ｂ절정時、모유체질であれば
IF 절정強度:3 && TALENT:MASTER:모유체질
	;모유不발
	IF BASE:MASTER:모유 < 50 + RAND:5 * 10
		STR:1035 = 空분유
		분유負担 = 50 + 절정強度:3 * 10
		DOWNBASE:MASTER:체력 += 100
		DOWNBASE:MASTER:기력 += 40
		NOWEX:MASTER:분유     = 1
		TCVAR:MASTER:분유     = 1
		;NOWEX:분유では『空분유』と통상の분유に1を割り当ててしまっているので、TCVAR:분유と번호がずれる
		;現状NOWEXの번호をずら했다くないので、TCVAR:분유だけ손当しておくことにする@(2014/11/08)/L
	;強절정
	ELSEIF 절정強度:3 == 2
		STR:1035 = 大量분유
		분유負担 = 500
		NOWEX:MASTER:분유 = 2
		TCVAR:MASTER:분유 = 3
	;통상の절정
	ELSE
		STR:1035 = 분유
		분유負担 = 300
		NOWEX:MASTER:분유 = 1
		TCVAR:MASTER:분유 = 2
	ENDIF
	;죠교대상가슴に모유汚れ
	CALL SET_STAIN("가슴", "모유", MASTER)
	;죠교자の(조수？ なんですかそれ？)
	;손
	SIF MB_PLAYER(1) == 2
		CALL SET_STAIN("손", "모유", TARGET)
	;입
	SIF MB_PLAYER(1) == 1
		CALL SET_STAIN("입", "모유", TARGET)
	;페니스
	SIF MB_PLAYER(1) == 3
		CALL SET_STAIN("페니스", "모유", TARGET)
	;はじめての분유は이탈が付く
	SIF EXP:분유경험 == 0 && CFLAG:MASTER:죠교레벨 < 20
		SOURCE:MASTER:이탈 += 400 * (15 - GET_ABL(MASTER, "순종")/ 20 - CFLAG:MASTER:죠교레벨 / 2) / 15
ENDIF

;Ｖ절정時、전립선자극強があり、후타나리でないなら
IF 절정強度:1 && !TALENT:MASTER:후타나리 && TCVAR:MASTER:전립선자극 > 1
	;強절정
	IF 절정強度:1 == 2
		STR:1038 = 大量시오후키
		시오후키負担 = 500
		NOWEX:MASTER:시오후키 = 2
		TCVAR:MASTER:시오후키 = 2
	;통상の절정
	ELSE
		STR:1038 = 시오후키
		시오후키負担 = 300
		NOWEX:MASTER:시오후키 = 1
		TCVAR:MASTER:시오후키 = 1
	ENDIF
ELSE
	STR:1038 = 
ENDIF

;STR:1036(절정表示用文字列)初期化
STR:1036 = 
;절정種を順に記入
SIF 절정強度:0
	STR:1036 += "＆Ｃ"
SIF 절정強度:1
	STR:1036 += "＆Ｖ"
SIF 절정強度:2
	STR:1036 += "＆Ａ"
SIF 절정強度:3
	STR:1036 += "＆Ｂ"
;다중절정時
IF STRLENSU(STR:1036) > 2
	;書式を整える
	STR:1036 = %SUBSTRINGU(STR:1036, 1)%절정
	;文字数で절정種を判定
	;사중절정
	IF STRLENSU(STR:1036) > 8
		;사중절정時には特別な文字列を記入
		STR:1036 = 四 重 絶 頂
		TIMES 절정負担, 2.00
		TIMES 사정負担, 1.40
		TIMES 분유負担, 1.40
		TIMES 시오후키負担, 1.40
		TIMES 快感負担, 1.50
	;三重절정
	ELSEIF STRLENSU(STR:1036) > 6
		TIMES 절정負担, 1.65
		TIMES 사정負担, 1.20
		TIMES 분유負担, 1.20
		TIMES 시오후키負担, 1.20
		TIMES 快感負担, 1.30
	;二重절정
	ELSE
		TIMES 절정負担, 1.30
		TIMES 사정負担, 1.10
		TIMES 분유負担, 1.10
		TIMES 시오후키負担, 1.10
		TIMES 快感負担, 1.30
	ENDIF
;単절정、無절정時
ELSE
	;文字列初期化
	STR:1036 = 
ENDIF

;절정は달성の主なソースの一つ
SOURCE:MASTER:달성 += 사정負担 + 분유負担 + 절정負担

;노출
SOURCE:MASTER:노출 += 30 * (SUMARRAY(절정強度) + 2) + 사정負担 / 2 + 분유負担 / 2 + 시오후키負担 / 2

;空でない사정時(사정ゲージの減りを보정)
IF NOWEX:MASTER:사정 > 1
	;죠교자욕망、기교、정액중독ABLにより基準値生成
	絞り強度 = GET_ABL(TARGET, "욕망") + GET_ABL(TARGET, "기교") + GET_ABL(TARGET, "정액중독")
	;성기마찰한다
	IF IS_NOWACTNAME("성기마찰한다")
		;애무ABLを加算
		絞り強度 += GET_ABL(TARGET, "애무") * 2
	;죠교자Ｖを利用했다성교중
	ELSEIF IS_NOWACTNAME("역강간/정상위시킨다/후배위시킨다/대면좌위시킨다/배면좌위시킨다")
		;Ｖ감각、성교ABLを加算
		絞り強度 += GET_ABL(TARGET, "Ｖ감각") + GET_ABL(TARGET, "성교")
		;음호素質により보정
		絞り強度 = 絞り強度 * (2 + TALENT:음호) / 2
	;죠교자Ａを利用했다성교중
	ELSEIF IS_NOWACTNAME("항문섹스시킨다")
		;Ａ감각、성교ABLを加算
		絞り強度 += GET_ABL(TARGET, "Ａ감각") + GET_ABL(TARGET, "성교")
		;음고素質により보정
		絞り強度 = 絞り強度 * (2 + TALENT:음고) / 2
	;손으로 애무
	ELSEIF IS_NOWACTNAME("손으로 애무")
		;애무ABLを加算
		絞り強度 += GET_ABL(TARGET, "애무") * 2
		;손가락기술素質により보정
		絞り強度 = 絞り強度 * (2 + TALENT:손가락기술) / 2
	;입으로 애무
	ELSEIF IS_NOWACTNAME("입으로 애무")
		;애무ABLを加算
		絞り強度 += GET_ABL(TARGET, "애무") * 2
		;혀기술、거친혀素質により보정
		絞り強度 = 絞り強度 * (3 + TALENT:혀기술 + TALENT:거친혀) / 3
	;파이즈리한다
	ELSEIF IS_NOWACTNAME("파이즈리한다")
		;애무ABLを加算
		絞り強度 += GET_ABL(TARGET, "애무") * 2
		;거유、빈유、음유素質により보정
		絞り強度 = 絞り強度 * (3 + TALENT:거유 - TALENT:빈유 + TALENT:음유) / 3
	;풋잡하기
	ELSEIF IS_NOWACTNAME("풋잡하기")
		;애무ABLを加算
		絞り強度 += GET_ABL(TARGET, "애무") * 2
		;새드、각선미素質により보정
		絞り強度 = 絞り強度 * (4 + TALENT:새드 + TALENT:각선미) / 4
	ENDIF
	;W系で１．５倍
	SIF ASSI:1 > 0 && TCVAR:(ASSI:1):조수방침 == GET_ASSIMENUNUM("コンビネーション")
		TIMES 絞り強度, 1.50
	;애태움플레이で보정
	絞り強度 = 絞り強度 * (TCVAR:MASTER:가버려 + 10) / 10
	;現在の精力/最大精力で보정
	絞り強度 = 絞り強度 * (BASE:MASTER:사정 + 1000) / (MAXBASE:MASTER:사정 + 1000)
	;最大精力による보정
	絞り強度 = 絞り強度 * (MAXBASE:MASTER:사정 + 5000) / 10000
ENDIF

;체력と기력と精力の소모
DOWNBASE:MASTER:체력 += 20 * 절정強度:0 + 40 * 절정強度:1 + 60 * 절정強度:2 + 20 * 절정強度:3 + 사정負担 / 10 + 분유負担 / 10 + 시오후키負担 / 10 + 快感負担 * (TFLAG:동일행동보너스 + 1) / 2
DOWNBASE:MASTER:기력 += 5 * 절정強度:0 + 10 * 절정強度:1 + 15 * 절정強度:2 + 5 * 절정強度:3 + 사정負担 / 30 + 분유負担 / 30 + 시오후키負担 / 30 + 快感負担 * (TFLAG:동일행동보너스 + 1) * 2 / 10
DOWNBASE:MASTER:사정 += 사정負担 * (100 + 絞り強度) / 100
;今回の소모精力BASE分마력취득
TFLAG:취득마력 += MIN(BASE:MASTER:사정, DOWNBASE:MASTER:사정) / 10
;限界を超えて絞られた場合체력が削られる
SIF DOWNBASE:MASTER:사정 > BASE:MASTER:사정
	DOWNBASE:MASTER:체력 += 200 * (2 - TALENT:MASTER:정력절륜 + TALENT:MASTER:정력박약) / 2
;모유の소모
DOWNBASE:MASTER:모유 += 분유負担
;限界を超えて絞られた場合체력が削られる
SIF DOWNBASE:MASTER:모유 > BASE:MASTER:모유
	DOWNBASE:MASTER:체력 += 200 * (2 - TALENT:MASTER:정력절륜 + TALENT:MASTER:정력박약) / 2

;실금
BASE:MASTER:소변 += 절정強度:0 * 1000 + 사정負担 + CUP:MASTER:쾌Ｃ / 5
;소변が限界を突破し、오줌싸개がある場合
IF BASE:MASTER:소변 > MAXBASE:MASTER:소변 && TALENT:MASTER:오줌싸개
	STR:1037 = 실금
	NOWEX:MASTER:방뇨   = 1
	TCVAR:MASTER:방뇨   = 1
	TFLAG:실금          = 1
	SOURCE:MASTER:노출 += 200
	SOURCE:MASTER:이탈 += 100
	SOURCE:MASTER:굴종 += 200
	BASE:MASTER:소변    = 0
ENDIF

;이성소모
DOWNBASE:MASTER:이성 += 快感負担 / 2 + (SUMARRAY(절정強度) + DOWNBASE:MASTER:사정 / 300 + NOWEX:MASTER:분유 + NOWEX:MASTER:방뇨 + 5) * 10
;쾌락에따른이성저하
SIF TFLAG:쾌락에따른이성저하 == 0 && 快感負担 > 20
	TFLAG:쾌락에따른이성저하 = 3 + 快感負担 / 30 + RAND:3
SIF TFLAG:쾌락에따른이성저하 > 0 && 快感負担 > 10
	TFLAG:쾌락에따른이성저하 += 1 + RAND:2

;죠교자の흥미を引く現象
;절정
SIF SUMARRAY(절정強度) > 0
	BASE:흥미 += 30 + 25 * (SUMARRAY(절정強度) + 8) / 8
;사정
SIF 사정負担
	BASE:흥미 += 10 + 10 * (사정負担 + 500) / 500
;분유
SIF 분유負担
	BASE:흥미 += 10 + 10 * (분유負担 + 500) / 500

;죠교部位の누적値(보정値計算)
FOR LCOUNT, 0, 4
	SIF 절정強度:LCOUNT
		CFLAG:MASTER:(30 + LCOUNT) = 50 + (CFLAG:MASTER:(30 + LCOUNT) - 50) / 2
NEXT

;호감도と경험치변화をカウント
LOCAL = (SUMARRAY(절정強度) + DOWNBASE:MASTER:사정 / 300 + NOWEX:MASTER:분유) * (SUMARRAY(절정強度) + 8) / 8
TFLAG:호감도업 += LOCAL
TFLAG:경험치업 += LOCAL

;NOWEXにデータを入れる（절정時구상に使う）
FOR LCOUNT, 0, 4
	NOWEX:MASTER:LCOUNT = 절정強度:LCOUNT
NEXT

;NOWEXをTCVAR절정系に保存
TCVAR:MASTER:Ｃ절정 = NOWEX:MASTER:Ｃ절정
TCVAR:MASTER:Ｖ절정 = NOWEX:MASTER:Ｖ절정
TCVAR:MASTER:Ａ절정 = NOWEX:MASTER:Ａ절정
TCVAR:MASTER:Ｂ절정 = NOWEX:MASTER:Ｂ절정

;사정時、関連플래그を設定
IF NOWEX:MASTER:사정
	;죠교대상사정후경과턴참조系플래그をリセット
	VARSET TCVAR:MASTER:0, 0, 90, 100
	;성교봉사時
	IF V_SEX(MASTER) || A_SEX(MASTER)
		;Ｖ섹스の場合
		IF V_SEX(MASTER)
			;TFLAG:질내사정후경과턴は403xx seriesあたりで抹消予定@/L
			TFLAG:질내사정후경과턴            = 1
			TCVAR:MASTER:질내사정후경과턴참조 = 1
		;Ａ섹스の場合
		ELSE
			TCVAR:MASTER:장내사정후경과턴참조 = 1
		ENDIF
	;입による애무で사정의場合
	ELSEIF GET_ACTNAME(GET_NORMALACTNUM(TFLAG:ACT)) == "입으로 애무" || TEQUIP:죠교대상Ｃ사용 == 2
		;죠교자が정욕かつ구음경험が一定以上ならディープスロート発生
		SIF IS_CONDITION(TARGET, "정욕") && EXP:TARGET:구음경험 >= 50
			DEEP_THROAT = 1
	ENDIF
	;TFLAG:사정후경과턴は403xx seriesあたりで抹消予定@/L
	TFLAG:사정후경과턴                  = 1
	TCVAR:MASTER:사정후경과턴참조       = 1
	CFLAG:MASTER:(250 + NOWEX:MASTER:사정) += 1
	CFLAG:MASTER:사정횟수                  += 1
	TCVAR:MASTER:가게해줘거동제어용         = 0
ENDIF
;절정횟수플래그を設定
FOR LCOUNT, 0, 4
	SIF 절정強度:LCOUNT
		CFLAG:MASTER:(256 + LCOUNT)++
NEXT

;절정횟수を増やす
FOR LCOUNT, 0, 4
	EX:MASTER:LCOUNT += NOWEX:MASTER:LCOUNT
NEXT
SIF PENIS(MASTER)
	EX:MASTER:사정 += NOWEX:MASTER:사정
EX:MASTER:분유 += NOWEX:MASTER:분유
EX:MASTER:방뇨 += NOWEX:MASTER:방뇨
SIF !PENIS(MASTER)	;시오후키追加@revkoishi(14/05/26)
	EX:MASTER:시오후키 += NOWEX:MASTER:시오후키

;【굴복:43】【굴복:55】の処理はSOURCE_MARK.ERBの@MARK_CHECK_SOURCE_SURRENDERに移設@/L

SIF !TEQUIP:성교봉사중 || IS_NOWACTNAME("Ｗ역강간/타니와타리")	;既に抜けていたらクリア。引っ張りすぎ？@revkoishi(14/07/15)
	CALLF _UNEXTRACTION(MASTER, 1, 0)

;죠교자に사정処理と안빼고判定。微調整@revkoishi(14/06/07)
IF NOWEX:MASTER:사정	;죠교자に사정
	IF MC_PLAYER(1) == 4			;질내사정
		CALLF CALC_SOURCE00_EX_R(0)	;죠교자に사정処理(Ｖ)
	ELSEIF MC_PLAYER(1) == 5		;항문へ사정
		CALLF CALC_SOURCE00_EX_R(1)	;죠교자に사정処理(Ａ)
	ENDIF
ELSEIF TEQUIP:TARGET:페니스밴드 && (NOWEX:MASTER:Ｃ절정 || NOWEX:MASTER:Ｖ절정)	;페니스밴드성교봉사で절정
	IF TEQUIP:TARGET:성교봉사중 && TEQUIP:TARGET:성교봉사중 != 6	;질내절정
		CALLF CALC_SOURCE00_EX_R(0, 1)
	ELSEIF TEQUIP:TARGET:성교봉사중 == 6							;항문で절정
		CALLF CALC_SOURCE00_EX_R(1, 1)
	ENDIF
ENDIF

;いきそう（暫定）
;가버려でなく、페니스があれば발기도が十分高いか、페니스がなければ윤활が十分高いとき、쾌Ｃが存在すれば
IF TCVAR:MASTER:가버려 == 0 && ((TCVAR:MASTER:발기도 >= 1000 && PENIS(MASTER)) || (PALAM:MASTER:윤활 >= 250 && !PENIS(MASTER) && PALAM:MASTER:쾌Ｃ >= 1000)) && CUP:MASTER:쾌Ｃ > 0
	;C감각が低いとより低い値でいきそうに、また이성低下でC値にかかわらずいきそうになる。
	VARSET LOCAL
	;C刺激 > C감각 判定
	LOCAL += (CUP:MASTER:쾌Ｃ + PALAM:MASTER:쾌Ｃ) >= MAX(GET_ABL(MASTER, "Ｃ감각") * 100, 1000)
	;技能 > 이성 判定
	LOCAL += GET_ABL(TARGET, "기교") - GET_ABL(MASTER, "기교") > RAND:(MAX(BASE:MASTER:이성 / 10, 1))
	;이성が全くなければ無条件で가버려に
	LOCAL += BASE:MASTER:이성 == 0
	;どれかの判定に引っかかれば
	IF LOCAL
		TCVAR:MASTER:가버려 += 5
;OriginalString : %CALLNAME:MASTER%は가버려되었다…
		PRINTFORMW %CALLNAME:MASTER%(은)는 이 기초가 되었다…
	ENDIF
ENDIF

;快感を참다時、참다に成功
IF 참다強度 && TCVAR:MASTER:참다
	IF PENIS(MASTER)
;OriginalString : %CALLNAME:MASTER%は必死になって사정を참다している…
		PRINTFORMW %CALLNAME:MASTER%(은)는 필사적으로 사정을 참고 있다…
	ELSE
;OriginalString : %CALLNAME:MASTER%は必死になって절정を참다している…
		PRINTFORMW %CALLNAME:MASTER%(은)는 필사적으로 절정을 참고 있다…
	ENDIF
ENDIF

;자위の場合は焦らさない
IF IS_NOWACTNAME("자위") && (GETBIT(TEQUIP:자위중, 0) || (GETBITAND(TEQUIP:자위중, 0, 1)))
	TCVAR:MASTER:애태움도 = 0
ENDIF

;애태움中、애태움に成功
IF TCVAR:MASTER:애태움도 > 1 || SUMARRAY(애태움強度) > 0
;OriginalString : %CALLNAME:MASTER%は절정寸前で焦らされている…
	PRINTFORMW %CALLNAME:MASTER%(은)는 절정 직전에 초조하게 해지고 있다…
ENDIF

;애태움
;가버려で애태움中でなく사정もなく징계・中でもない
IF TCVAR:MASTER:가버려 && !TCVAR:MASTER:애태움도 && !NOWEX:MASTER:사정 && !TFLAG:징계・플래그
	VARSET LOCAL
	;보정値を素質、ABLによって増減
	LOCAL += 5 + TALENT:새드 + TALENT:심술궂음 - TALENT:마음씨착한 + (ABL:가학 >= ABL:애무 ? 1 # -1)
	;보정値が十分高ければ
	IF RAND:LOCAL >= RAND:5
		;기교ABLによって애태움도設定
		TCVAR:MASTER:애태움도 += MIN(ABL:기교 / 5 + 2, 4)
	ENDIF
	;애태움が開始されれば地の文表示
	SIF TCVAR:MASTER:애태움도 > 1
		{
		PRINTFORMW %CALLNAME:TARGET%은(는) \@ TALENT:심술궂음 || TALENT:새드 || !ALI(0, TARGET) 
                   ? 심술궂은 # 장난스러운 \@ 미소를 띄우면서 %CALLNAME:MASTER%(울)를 애태우기 시작했다…
		}
ENDIF

;절정強度処理
;ARG:0 = 快Ｘ種(ＣＶＡＢ)
;ARG:1 = 今回の到達強度
;RESULT = 절정強度
@CALC_SOURCE00_EX_P(ARG, ARG:1)
#FUNCTION
#DIM LCOUNT
#DIM 절정境界, 3
;절정境界セット
절정境界 = 90
절정境界:1 = 10000
절정境界:2 = 20000
;LOCAL初期化
VARSET LOCAL
;절정境界分だけループする
FOR LCOUNT, 1, 3
	;절정境界を越えると、절정強度が1ずつ上がる
	SIF ARG:1 < 절정境界:LCOUNT
		BREAK
	LOCAL++
NEXT
;가버려時、Ｃ절정が必ず起こる
SIF (ARG == 0) && TCVAR:MASTER:가버려
	LOCAL = MAX(LOCAL, 1)
;절정時
IF LOCAL >= 1
	;절정境界:N、절정境界:0の数値に応じてPALAM減少
	;(下限を突破しないよう調整)
	CDOWN:MASTER:ARG = MIN(절정境界:LOCAL * 절정境界 / 100, ARG:1)
	;절정表示用文字列初期化
	STR:(1030 + ARG) = 
	;強度が2なら"強"を追加
	SIF LOCAL == 2
		STR:(1030 + ARG) += "強"
	;절정種を記入
	STR:(1030 + ARG) += EXNAME:ARG
	;強度に応じて마력취득
	TFLAG:취득마력 += 50 * LOCAL
	;CDOWN:MASTER:快Ｘで下げても절정以上なら
	;その値-1になるように調整（10000で절정なら9999）
	;※これがこの位置にあることで애태움、참다の価値が
	;  上昇するのではないかと思って試験的に移動。
	;  不当に価値が上昇してしまったときはIF文から出すこと
	SIF ARG:1 - CDOWN:MASTER:ARG >= 절정境界:1
		CDOWN:MASTER:ARG = ARG:1 - 절정境界:1 + 1
ENDIF
;절정強度を返す
RETURNF LOCAL


;-------------------------------------------------
;関数名:CALC_SOURCE00_EX_R
;概　要:죠교자に사정処理(＋안빼고判定)
;引　数:ARG:0…삽입箇所(0.Ｖ/1.Ａ)
;　　　 ARG:1…페니스밴드の使用有無(0.未使用/1.使用)
;戻り値:안빼고判定結果を表す真偽値(0か1)。0は抜く、1は안빼고(継続)
;備　考:式中関数
;諸変数加算/セット処理と안빼고の判定を行う
;ちょっとだけ改造@revkoishi(14/06/07)
;-------------------------------------------------
@CALC_SOURCE00_EX_R(ARG:0, ARG:1)
#FUNCTION
CALLF _UNEXTRACTION(MASTER, 1, TFLAG:안빼고 ? TFLAG:안빼고 # 0)	;안빼고遅延判定用保持。既に抜けていたらクリア@revkoishi(14/07/14)

;諸変数加算
IF !ARG:1	;페니스밴드未使用時
	SELECTCASE ARG:0
		CASE 0	;Ｖ삽입時
			IF NOWEX:MASTER:사정 != 1	;空でない사정時
				CFLAG:TARGET:질정 += MIN(BASE:MASTER:사정, DOWNBASE:MASTER:사정)
				STAIN:TARGET:바기나 |= 4
				TFLAG:전회질싸 = 2
			ELSE						;드라이사정時
				TFLAG:전회질싸 = 4
			ENDIF
		CASE 1	;Ａ삽입時
			IF NOWEX:MASTER:사정 != 1	;空でない사정時
				STAIN:TARGET:항문 |= 4
				TFLAG:전회질싸 = 6
			ELSE						;드라이사정時
				TFLAG:전회질싸 = 8
			ENDIF
	ENDSELECT
ENDIF

;안빼고欲求보정値生成
LOCAL:0 = 0
LOCAL:0 += GET_ABL(TARGET, "욕망") * 3							;죠교자욕망
LOCAL:0 += GET_ABL(TARGET, "기교")								;죠교자기교
LOCAL:0 += GET_ABL(TARGET, \@ ARG:0 ? Ａ감각 # Ｖ감각 \@) * 2	;죠교자Ｖ감각/Ａ감각
LOCAL:0 += GET_ABL(TARGET, "성교") * 2							;죠교자성교ABL
IF NOWEX:MASTER:사정 == 1	;드라이사정時
	LOCAL:0 -= 150											;보정値減少
ELSE						;非드라이사정時
	LOCAL:0 += !TFLAG:전턴성교봉사           ? 150 # 0	;삽입れたばかりならさらに加算
	LOCAL:0 += (TFLAG:금일의방침 == 7 && !ARG:0) ? 300 # 0	;금일의방침が애만들기で、Ｖ삽입중ならさらに加算
ENDIF
LOCAL:0 = (LOCAL:0 * 2) / (TFLAG:안빼고 + 2)	;안빼고횟수(0=1, 1=2/3, 2=1/2, 3=2/5, 4=1/3,……)
LOCAL:0 /= 10	;안빼고보정値によって안빼고挑戦判定

;안빼고挑戦判定
LOCAL:1 = 0	;挑戦結果
IF PERCENT(GET_ASYMPTOTE(LOCAL:0, 100, 500))	;挑戦判定が成立했다場合
	;挑戦する場合さらに안빼고技術判定に進む
	;残り精力が100%なら50%の成功ボーナスが貰える。0%なら0%
	;페니스밴드사용중なら除算しない。とりあえず0を返しておくけど望ましいと思う形に整えてもらっても構わない@/L
	LOCAL:0 = !ARG:1 ? BASE:MASTER:사정 * 50 / MAXBASE:MASTER:사정 # 0
	;ボーナスの残り分は기교ABLで補う。
	;残り精力が100%なら50%分を기교ABLで補う。0%なら100%
	LOCAL:0 = GET_ABL(TARGET, "기교") * (100 - LOCAL:0) / 100 + LOCAL:0
	;ここでTEQUIPを判定しても過程の情報がわからない＝途中でＶ⇔Ａ挿し換えのパターンが拾えないので、ここでは扱えません
	;そしてコンビネーションACTでの사정は結局のところ죠교자に中出しなので안빼고判定しても差し支えありません
	;필요なのは抜けた時＝ACT時に即座に플래그を折ることであり、それはこの関数でやる内容ではないと思われるので
	;この関数は判定だけします。それを良しとしないならば、あとはわからないのでがんばって＞＜@revkoishi
	LOCAL:1 = PERCENT(LOCAL:0)	;判定
ENDIF

;成功すれば안빼고開始・継続、失敗すれば抜く
TFLAG:안빼고          = LOCAL:1 ? TFLAG:안빼고 + 1      # 0
TCVAR:MASTER:애태움도 = LOCAL:1 ? TCVAR:MASTER:애태움도 # 0
RETURNF LOCAL:1	;判定結果を返す。使うかどうかはともかく
;TFLAG:안빼고が立たない場合はこの箇所で確実にリセットする。立った場合は確実に抜け시킨다@/L

;IS_UNEXTRACTIONと_UNEXTRACTIONはCOMMON_GETTER_TRAIN.ERBに移設
