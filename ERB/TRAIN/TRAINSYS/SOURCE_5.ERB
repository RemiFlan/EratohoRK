;────────────────────────────────────
;죠교자快感（ｃｖａｂ）のソースと절정の処理（쾌Ｃ、쾌Ｖ、쾌Ａ、쾌Ｂ、체력、기력、사정、모유、이성、흥미）
;────────────────────────────────────
@CALC_SOURCE40

LOCALS = CALC_SOURCE40

;最初の変動
CALLFORM %LOCALS%_TALENT

;快感（ＣＶＡＢ）の上昇
CALLFORM %LOCALS%_PLEASURE

;절정、체력、기력、사정、모유、소변などの処理
CALLFORM %LOCALS%_EX

;────────────────────────────────────
;最初の変動
;────────────────────────────────────
@CALC_SOURCE40_TALENT
#DIM LCOUNT
;페니스밴드装備中は쾌Ｃが쾌Ｖに変換
;変換率…쾌Ｃを20％にし、残りの値にｖ감각+1/ｃ감각+1を掛けて２で割って쾌Ｖに加算する
IF TEQUIP:죠교자페니스밴드
	SOURCE:쾌Ｖ += (SOURCE:쾌Ｃ*8*(((ABL:Ｖ감각+1)*100)/(ABL:Ｃ감각+1)))/2000
	TIMES SOURCE:쾌Ｃ, 0.20
ENDIF

;ＣＶＡＢ部位について
FOR LCOUNT, 0, 4
	;민감と둔감は最初で処理
	;Ｘ민감
	SIF TALENT:(100 + LCOUNT * 2)
		TIMES SOURCE:LCOUNT, 1.50
	;Ｘ둔감
	SIF TALENT:(101 + LCOUNT * 2)
		TIMES SOURCE:LCOUNT, 0.70
	;죠교자の상태
	IF 0
	ELSEIF T_COND("피폐")
		TIMES SOURCE:LCOUNT, 0.80
	ELSEIF T_COND("쇠약")
		TIMES SOURCE:LCOUNT, 0.65
	ELSEIF T_COND("무기력")
		TIMES SOURCE:LCOUNT, 0.70
	ELSEIF T_COND("정욕")
		TIMES SOURCE:LCOUNT, 1.20
	ENDIF
NEXT

;────────────────────────────────────
;快感（ＣＶＡＢ）の上昇
;────────────────────────────────────
@CALC_SOURCE40_PLEASURE
#DIM LCOUNT

;욕정PALAMが[0→4, 1000→7, 2000→9, 3000→10, 4000→11, 5000→12]をLOCALへ(4～12)
LOCAL = LIMIT(GET_REVISION_SQRT(PALAM:MASTER:욕정, 5000, 8) + 4, 4, 12)
FOR LCOUNT, 0, 4
	;연속ボーナス(연속횟수倍に増加するボーナス)
	CUP:LCOUNT += SOURCE:LCOUNT * LOCAL * TFLAG:동일행동보너스 / 10
	;조교중に集中的に責られると弱くなる気がする(快Ｘ보정処理)
	CUP:LCOUNT = CUP:LCOUNT * CFLAG:(30 + LCOUNT) / 50
NEXT

;────────────────────────────────────
;절정、체력、기력、사정、모유、소변などの処理
;────────────────────────────────────
@CALC_SOURCE40_EX
#DIM LCOUNT
#DIM DYNAMIC 절정強度, 4
#DIM DYNAMIC 快感負担
#DIM DYNAMIC 사정負担
#DIM DYNAMIC 분유負担
#DIM DYNAMIC 절정負担

;절정Ｃ
SIF (!PENIS(TARGET) || TCVAR:발기 > 1)
	절정強度:0 = CALC_SOURCE40_EX_P(0, CUP:쾌Ｃ + PALAM:쾌Ｃ)

;절정Ｖ
절정強度:1 = CALC_SOURCE40_EX_P(1, CUP:쾌Ｖ + PALAM:쾌Ｖ)

;절정Ａ
절정強度:2 = CALC_SOURCE40_EX_P(2, CUP:쾌Ａ + PALAM:쾌Ａ)

;절정Ｂ
절정強度:3 = CALC_SOURCE40_EX_P(3, CUP:쾌Ｂ + PALAM:쾌Ｂ)

;절정強度合計から절정負担を計算
절정負担 = 1000 * SUMARRAY(절정強度)

;快感による체력기력이성소모
;快ＸSOURCE合計が[0→0, 2000→44, 4000→63, 6000→77, 8000→89, 10000→100]を快感負担へ
快感負担 = GET_REVISION_SQRT(SUMARRAY(SOURCE, 40, 44), 10000, 100)

;죠교の度合いによる軽減
;죠교자죠교레벨+욕망ABLが[0→100%, 2→91%, 4→83%, 6→75%, 8→67%, 10→59%, 12→51%]を快感負担に乗算(50～100%)
LOCAL = GET_REVISION_SQRT_PROP(CFLAG:죠교레벨 + ABL:욕망, 1, 5, 12, 50)
快感負担 -= 快感負担 * LIMIT(LOCAL, 50, 100) / 100

;페니스存在時
IF PENIS(TARGET)
	;절정決定時
	IF 절정強度:0 > 0
		;사정ゲージがない
		IF BASE:사정 == 0
			STR:1044 = 드라이사정（죠교자）
			DOWNBASE:체력  += 100
			DOWNBASE:기력  += 80
			NOWEX:사정      = 1
			TCVAR:사정      = 1
			TCVAR:드라이사정    = 1
			TCVAR:드라이사정수 += 1
		;強절정
		ELSEIF 절정強度:0 == 2
			STR:1044 = 대량사정（죠교자）
			사정負担 = 500
			NOWEX:사정 = 3
			TCVAR:사정 = 3
		;통상の절정
		ELSE
			STR:1044 = 사정（죠교자）
			사정負担 = 300
			NOWEX:사정 = 2
			TCVAR:사정 = 2
		ENDIF
	ENDIF
	;空でない사정時
	IF NOWEX:사정 > 1
		;정액汚れの追加
		;────────────────────────────────────
		;ACTが派生すると애무物が変わる場合があるので@IS_NOWACTNAMEではなく@TC_PLAYERで
		;一々ACT파생も参照するのは面倒だし！
		;────────────────────────────────────
		;죠교자의 페니스
		CALL SET_STAIN("페니스", "정액", TARGET)
		;죠교대상の
		;바기나(성교봉사時)
		SIF TC_PLAYER(1) == 4
			CALL SET_STAIN("바기나", "정액", MASTER)
		;항문
		SIF TC_PLAYER(1) == 5
			CALL SET_STAIN("항문", "정액", MASTER)
		;손
		SIF TC_PLAYER(1) == 2
			CALL SET_STAIN("손", "정액", MASTER)
		;입
		SIF TC_PLAYER(1) == 1
			CALL SET_STAIN("입", "정액", MASTER)
		;가슴
		SIF TC_PLAYER(1) == 6
			CALL SET_STAIN("가슴", "정액", MASTER)
		[SKIPSTART]
		Ｖ/Ａは中出し判定以降に処理を移設
		;바기나
		SIF TC_PLAYER(1) == 4
			CALL SET_STAIN("바기나", "정액", MASTER)
		;항문
		SIF TC_PLAYER(1) == 5
			CALL SET_STAIN("항문", "정액", MASTER)
		[SKIPEND]
	ENDIF
ENDIF

;中/外に사정
IF TEQUIP:성교중 && NOWEX:사정
	;中出し判定
	LOCAL = 0
	;안에 내면 안돼!かつ용서를 빌다失敗
	IF IS_NOWCOMNAME("안에 내면 안돼!") && !IS_COMGRONAME("용서를 빌다")
		;트라우마加算
		SOURCE:MASTER:트라우마 += (BASE:사정 == 1 ? 200 # 500) - CFLAG:MASTER:죠교레벨 * 10
		;中出し確認
		LOCAL = 1
	;안에 내!または호의(M→T)が十分に存在
	ELSEIF IS_NOWCOMNAME("안에 내!") || CFLAG:M호의 > RAND:8000
		;中出し確認
		LOCAL = 1
	ENDIF
	;中出し時
	IF LOCAL
		;Ｖ성교
		IF TEQUIP:성교중 < 6
			;汚れ追加(드라이사정時除く)
			STAIN:MASTER:바기나 |= (BASE:사정 == 1 ? 0 # 4)
			;中出し기록(드라이사정情報も記入)
			TFLAG:전회질싸 = (BASE:사정 == 1 ? 3 # 1)
			;죠교대상(주인님)の질내사정量を増やす
			CFLAG:M질정 += DOWNBASE:사정
			;죠교대상の바기나に정액汚れを追加
			CALL SET_STAIN("바기나", "정액", MASTER)
		;Ａ성교
		ELSE
			;汚れ追加(드라이사정時除く)
			STAIN:MASTER:항문 |= (BASE:사정 == 1 ? 0 # 4)
			;中出し기록(드라이사정情報も記入)
			TFLAG:전회질싸 = (BASE:사정 == 1 ? 7 # 5)
			;죠교대상の항문に정액汚れを追加
			CALL SET_STAIN("항문", "정액", MASTER)
		ENDIF
	ENDIF
ENDIF

;Ｂ절정時、모유체질であれば
IF 절정強度:3 && TALENT:모유체질
	;모유不발
	IF BASE:모유 < 50 + RAND:5 * 10
		STR:1045 = 空분유（죠교자）
		분유負担 = 50 + 절정強度:3 * 10
		DOWNBASE:체력 += 100
		DOWNBASE:기력 += 80
		NOWEX:분유 = 1
		TCVAR:분유 = 1
		;NOWEX:분유では『空분유』と통상の분유に1を割り当ててしまっているので、TCVAR:분유と번호がずれる
		;現状NOWEXの번호をずら했다くないので、TCVAR:분유だけ손当しておくことにする@(2014/11/08)/L
	;強절정
	ELSEIF 절정強度:3 == 2
		STR:1045 = 大量분유（죠교자）
		분유負担 = 500
		NOWEX:분유 = 2
		TCVAR:분유 = 3
	;통상の절정
	ELSE
		STR:1045 = 분유（죠교자）
		분유負担 = 300
		NOWEX:분유 = 1
		TCVAR:분유 = 2
	ENDIF
	;죠교자가슴に모유汚れ
	CALL SET_STAIN("가슴", "모유", TARGET)
	;죠교대상の
	;손
	SIF TB_PLAYER(1) == 2
		CALL SET_STAIN("손", "모유", MASTER)
	;입
	SIF TB_PLAYER(1) == 1
		CALL SET_STAIN("입", "모유", MASTER)
	;페니스
	SIF TB_PLAYER(1) == 3
		CALL SET_STAIN("페니스", "모유", MASTER)
ENDIF

;STR:1046(절정表示用文字列)初期化
STR:1046 = 
;절정種を順に記入
SPLIT "＆Ｃ/＆Ｖ/＆Ａ/＆Ｂ", "/", LOCALS
FOR LCOUNT, 0, 4
	SIF 절정強度:LCOUNT
		STR:1046 += LOCALS:LCOUNT
NEXT
;다중절정時
IF STRLENSU(STR:1046) > 2
	;書式を整える
	STR:1046 = %SUBSTRINGU(STR:1046, 1)%절정（죠교자）
	;文字数で절정種を判定
	;사중절정
	IF STRLENSU(STR:1046) > 13
		;사중절정時には特別な文字列を記入
		STR:1046 = 四 重 絶 頂（죠교자）
		TIMES 절정負担, 2.00
		TIMES 사정負担, 1.40
		TIMES 분유負担, 1.40
		TIMES 快感負担, 1.50
	;三重절정
	ELSEIF STRLENSU(STR:1046) > 11
		TIMES 절정負担, 1.65
		TIMES 사정負担, 1.20
		TIMES 분유負担, 1.20
		TIMES 快感負担, 1.30
	;二重절정
	ELSE
		TIMES 절정負担, 1.30
		TIMES 사정負担, 1.10
		TIMES 분유負担, 1.10
		TIMES 快感負担, 1.30
	ENDIF
;単절정、無절정時
ELSE
	;文字列初期化
	STR:1046 = 
ENDIF

;절정は달성の主なソースの一つ
SOURCE:MASTER:달성 += 사정負担 + 분유負担 + 절정負担

;체력と기력と精力の소모
DOWNBASE:체력 += 20 * 절정強度:0 + 40 * 절정強度:1 + 60 * 절정強度:2 + 20 * 절정強度:3 + 사정負担 / 10 + 분유負担 / 10 + 快感負担 * (TFLAG:동일행동보너스 + 1) / 2
DOWNBASE:기력 += 10 * 절정強度:0 + 20 * 절정強度:1 + 30 * 절정強度:2 + 10 * 절정強度:3 + 사정負担 / 15 + 분유負担 / 15 + 快感負担 * (TFLAG:동일행동보너스 + 1) * 4 / 10
DOWNBASE:사정 = 사정負担
;限界を超えて絞られた場合체력が削られる
SIF DOWNBASE:사정 > BASE:사정
	DOWNBASE:체력 += 200 * (2 - TALENT:정력절륜 + TALENT:정력박약) / 2
;모유の소모
DOWNBASE:모유 += 분유負担
;限界を超えて絞られた場合체력が削られる
SIF DOWNBASE:모유 > BASE:모유
	DOWNBASE:체력 += 200 * (2 - TALENT:정력절륜 + TALENT:정력박약) / 2

;이성
DOWNBASE:이성 += 快感負担 + (SUMARRAY(절정強度) + DOWNBASE:사정 / 300 + NOWEX:분유 + 5) * 20

;죠교部位の누적値(보정値計算)
FOR LCOUNT, 0, 4
	SIF 절정強度:LCOUNT
		CFLAG:(30 + LCOUNT) = 50 + (CFLAG:(30 + LCOUNT) - 50) / 2
NEXT

;NOWEXにデータを入れる（절정時구상に使う）
FOR LCOUNT, 0, 4
	NOWEX:LCOUNT = 절정強度:LCOUNT
NEXT

;NOWEXをTCVAR절정系に保存
TCVAR:Ｃ절정 = NOWEX:Ｃ절정
TCVAR:Ｖ절정 = NOWEX:Ｖ절정
TCVAR:Ａ절정 = NOWEX:Ａ절정
TCVAR:Ｂ절정 = NOWEX:Ｂ절정

;사정時、関連플래그を設定
IF NOWEX:사정
	;죠교자사정후경과턴참조系플래그をリセット
	VARSET TCVAR:0, 0, 90, 100
	;Ｖ성교時
	SIF V_SEX()
		TCVAR:질내사정후경과턴참조 = 1
	;Ａ성교時
	SIF A_SEX()
		TCVAR:장내사정후경과턴참조 = 1
	TCVAR:사정후경과턴참조 = 1
	CFLAG:사정횟수            += 1
ENDIF

;죠교자の발기

;快感系ソースを受けるか、키스か、발핥기강제時。
SIF (SOURCE:쾌Ｃ || SOURCE:쾌Ｖ || SOURCE:쾌Ａ || SOURCE:쾌Ｂ || IS_NOWACTNAME("키스/발핥기강제/Ｗ키스")) && PENIS(TARGET)
	TCVAR:발기++
;ぱんつ・성기見せ탈의時。
SIF P_DATUICHECK(1) > 1 && PENIS(TARGET)
	TCVAR:발기++
;ブラ・가슴見せ탈의時。
SIF B_DATUICHECK(1) > 1 && PENIS(TARGET)
	TCVAR:발기++
;죠교대상절정時。
SIF (NOWEX:MASTER:Ｃ절정 || NOWEX:MASTER:Ｖ절정 || NOWEX:MASTER:Ａ절정 || NOWEX:MASTER:Ｂ절정 || NOWEX:MASTER:사정 || NOWEX:MASTER:분유 || NOWEX:MASTER:시오후키 || NOWEX:MASTER:방뇨) && PENIS(TARGET)
	TCVAR:발기++
;새드の場合、가학系ACT時。
SIF MENU(TFLAG:ACT분류) == "가학" && TALENT:새드 && PENIS(TARGET)
	TCVAR:발기++
;헌신적の場合、애무系ACT時。
SIF MENU(TFLAG:ACT분류) == "애무" && TALENT:헌신적 && PENIS(TARGET)
	TCVAR:발기++
;수동적の場合、봉사系ACT時。
SIF MENU(TFLAG:ACT분류) == "봉사" && TALENT:수동적 && PENIS(TARGET)
	TCVAR:발기++
;심술궂음の場合、수치系ACT時。
SIF MENU(TFLAG:ACT분류) == "수치" && TALENT:심술궂음 && PENIS(TARGET)
	TCVAR:발기++
;광기の場合、이상系ACT時。
SIF MENU(TFLAG:ACT분류) == "이상" && TALENT:광기 && PENIS(TARGET)
	TCVAR:발기++

;사정すると1に돌아가기
SIF NOWEX:사정
	TCVAR:발기 = 1

;사정時、関連플래그を設定
IF NOWEX:사정
	CFLAG:(250 + NOWEX:사정) += 1
	CFLAG:사정횟수 += 1
ENDIF
;절정횟수플래그を設定
FOR LCOUNT, 0, 4
	SIF 절정強度:LCOUNT
		CFLAG:(256 + LCOUNT)++
NEXT

;절정횟수を増やす
FOR LCOUNT, 0, 4
	EX:LCOUNT += NOWEX:LCOUNT
NEXT
SIF PENIS(TARGET)
	EX:사정 += NOWEX:사정
EX:분유 += NOWEX:분유

;いきそう（暫定）
;가버려でなく、페니스があれば발기しているか、페니스がないとき、쾌Ｃが存在すれば
IF TCVAR:TARGET:가버려 == 0 && (!PENIS(TARGET) || TCVAR:발기 > 1) && CUP:쾌Ｃ > 0
	;C감각が低いとより低い値でいきそうに、また이성低下でC値にかかわらずいきそうになる。
	VARSET LOCAL
	;C刺激 > C감각 判定
	LOCAL += (CUP:쾌Ｃ + PALAM:쾌Ｃ) >= MAX(GET_ABL(TARGET, "Ｃ감각") * 100, 1000)
	;技能 > 이성 判定
	LOCAL += GET_ABL(MASTER, "기교") - GET_ABL(TARGET, "기교") > RAND:(MAX(BASE:TARGET:이성 / 10, 1))
	;이성が全くなければ無条件で가버려に
	LOCAL += BASE:TARGET:이성 == 0
	;どれかの判定に引っかかれば
	IF LOCAL
		TCVAR:TARGET:가버려 += 5
;OriginalString : %CALLNAME:TARGET%は가버려되었다…
		PRINTFORMW %CALLNAME:TARGET%(은)는 이 기초가 되었다…
	ENDIF
ENDIF

;절정強度処理
;ARG:0 = 快Ｘ種(ＣＶＡＢ)
;ARG:1 = 今回の到達強度
;RESULT = 절정強度
@CALC_SOURCE40_EX_P(ARG, ARG:1)
#FUNCTION
#DIM LCOUNT
#DIM 절정境界, 3
;절정境界セット
절정境界 = 90
절정境界:1 = 10000
절정境界:2 = 20000
;LOCAL初期化
VARSET LOCAL
;절정境界分だけループする
FOR LCOUNT, 1, 3
	;절정境界を越えると、절정強度が1ずつ上がる
	SIF ARG:1 < 절정境界:LCOUNT
		BREAK
	LOCAL++
NEXT
;가버려時、쾌ＣがあればＣ절정が必ず起こる
SIF (ARG == 0) && TCVAR:TARGET:가버려 && CUP:쾌Ｃ > 0
	LOCAL = MAX(LOCAL, 1)
;절정時
IF LOCAL >= 1
	;절정境界:N、절정境界:0の数値に応じてPALAM減少
	;(下限を突破しないよう調整)
	CDOWN:ARG = MIN(절정境界:LOCAL * 절정境界 / 100, ARG:1)
	;절정表示用文字列初期化
	STR:(1040 + ARG) = 
	;強度が2なら"強"を追加
	SIF LOCAL == 2
		STR:(1040 + ARG) += "強"
	;절정種を記入
	STR:(1040 + ARG) += EXNAME:ARG + "（죠교자）"
	;CDOWN:快Ｘで下げても절정以上なら
	;その値-1になるように調整（10000で절정なら9999）
	SIF ARG:1 - CDOWN:ARG >= 절정境界:1
		CDOWN:ARG = ARG:1 - 절정境界:1 + 1
ENDIF
;절정強度を返す
RETURNF LOCAL

