;────────────────────────────────────
;090,휴식시킨다
;────────────────────────────────────
;リアクション前（Ｒ）
@ACTION_APPLY_90
;구속系を一括解除
TEQUIP:밧줄 = 0
TEQUIP:눈가리개 = 0
TEQUIP:볼개그 = 0
TEQUIP:삼각목마 = 0
;안면승마系解除
CALL FACESITTING_SET(0)
;죠교자、죠교대상자위を中断
TEQUIP:자위중 = 0
;조수삽입系解除
TFLAG:조수에삽입중 = 0
TFLAG:조수연속교합 = 0
;汚れ初期化
CALL STAIN_RESET
TFLAG:경과시간 += 7

;リアクション後（노출、이탈、달성、체력、기력、사정、분유、소변）
@ACTION_APPLY2_90
;죠교자죠교레벨、회복빠름、회복느림素質に応じて죠교자の체력、기력回復量増減
LOCAL:3 = 300 + (10 + GET_TRAINLV(TARGET) + TALENT:회복빠름 * 5 - TALENT:회복느림 * 5) * 10
LOCAL:4 = 300 + (6 + GET_TRAINLV(TARGET) + TALENT:회복빠름 * 5 - TALENT:회복느림 * 5) * 20
BASE:체력 += LOCAL:3
BASE:기력 += LOCAL:4
;조교자소모도減少
TFLAG:조교자소모도 -= 2
;リアクションが자위를 시작한다だった場合
IF IS_NOWCOMNAME("자위를 시작한다")
	;ACT파생に勝손に자위をし始めたをセット
	TFLAG:ACT파생 = 2
	;REACT분류に要求に応じないをセット		@COM50.자위를 시작한다の改変に伴いコメントアウト@/L
	;CALL SET_COMGRO("거부")
;지속系도구장착中だった場合
ELSEIF TEQUIP:바이브 || TEQUIP:항문바이브 || TEQUIP:항문비즈 || TEQUIP:관장그릇＋플러그 || TEQUIP:유두캡
	;도구それぞれの사용중に応じて보정値用意
	LOCAL = TEQUIP:바이브 * 3 + TEQUIP:항문바이브 * 3 + TEQUIP:항문비즈 + TEQUIP:관장그릇＋플러그 * 4 + TEQUIP:유두캡 * 2
	;죠교대상죠교레벨、회복빠름、회복느림素質、도구別보정値に応じて죠교대상の체력、기력回復量増減
	LOCAL:1 = 150 + (10 + CFLAG:MASTER:죠교레벨 + TALENT:MASTER:회복빠름 * 5 - TALENT:MASTER:회복느림 * 5) * 10 - 20 * LOCAL
	LOCAL:2 = 150 + (6 + CFLAG:MASTER:죠교레벨 + TALENT:MASTER:회복빠름 * 5 - TALENT:MASTER:회복느림 * 5) * 20 - 5 * LOCAL
	BASE:MASTER:체력 += LOCAL:1
	BASE:MASTER:기력 += LOCAL:2
	;ACT파생に도구のせいであまり休めなかったをセット
	TFLAG:ACT파생 = 1
;その他の場合
ELSE
	;죠교대상죠교레벨、회복빠름、회복느림素質に応じて죠교대상の체력、기력回復量増減
	LOCAL:1 = 300 + (10 + CFLAG:MASTER:죠교레벨 + TALENT:MASTER:회복빠름 * 5 - TALENT:MASTER:회복느림 * 5) * 10
	LOCAL:2 = 300 + (6 + CFLAG:MASTER:죠교레벨 + TALENT:MASTER:회복빠름 * 5 - TALENT:MASTER:회복느림 * 5) * 20
	BASE:MASTER:체력 += LOCAL:1
	BASE:MASTER:기력 += LOCAL:2
	;죠교대상소모도減少
	TFLAG:소모도 -= 1
	;ACT파생に普通に休んだをセット
	TFLAG:ACT파생 = 0
ENDIF
;変数保持処理
FOR LOCAL, 1, 5
	CALLF ACTION_APPLY2_90V(-LOCAL, LOCAL:LOCAL)
NEXT

;変数保持関数
@ACTION_APPLY2_90V(ARG, ARG:1)
#FUNCTION
;ARGが負であればLOCAL:(-ARG)にARG:1を保持し
;正であればLOCAL:ARG을반환
;
;例:(-1, 200) LOCAL:1に200を保持
;   ( 1)      LOCAL:1の200을반환

;ARGが0の時処理するものは無い
IF ARG == 0
;ARGが負なら値を保持
ELSEIF ARG < 0
	LOCAL:(-ARG) = ARG:1
;ARGが正なら値을반환
ELSE
	RETURNF LOCAL:ARG
ENDIF
RETURNF 0

;────────────────────────────────────
;091,영양제를 먹인다
;────────────────────────────────────
;リアクション前（Ｒ）（이탈）
@ACTION_APPLY_91
;필요ならば미약を使用
CALL LOVEPOTION_USE
;邪魔になるものを一括解除
TEQUIP:볼개그 = 0
TEQUIP:삼각목마 = 0
;안면승마系解除
CALL FACESITTING_SET(0)
;이탈SOURCE増加、죠교대상죠교레벨、순종によって減少
SOURCE:MASTER:이탈 = 200 - CFLAG:MASTER:죠교레벨 * 10 - GET_ABL(MASTER, "순종") * 2
;이탈SOURCEが負の値をとらないよう調整
SOURCE:MASTER:이탈 = SOURCE:MASTER:이탈 < 0 ? 0 # SOURCE:MASTER:이탈
;죠교자、죠교대상자위を中断
TEQUIP:자위중 = 0
;조수삽입系解除
TFLAG:조수에삽입중 = 0
TFLAG:조수연속교합 = 0
TFLAG:경과시간 += 2

;リアクション後（노출、이탈、달성、체력、기력、사정、분유、소변）
@ACTION_APPLY2_91
;リアクションが거부한다だった場合
IF IS_NOWCOMNAME("거부한다")
	;조교자소모도減少
	TFLAG:조교자소모도 -= 1
	;ACT파생に栄養剤を捨てたをセット
	TFLAG:ACT파생 = 1
	;REACT분류に要求に応じないをセット
	CALL SET_COMGRO("거부")
;その他の場合
ELSE
	;이탈SOURCE増加
	SOURCE:MASTER:이탈 += 250
	;죠교자배합지식、금단의지식、죠교대상약독내성素質、죠교자죠교레벨に応じて죠교대상の체력、기력回復量増減
	LOCAL:1 = 20 * (2 + TALENT:배합지식 + TALENT:금단의지식 - TALENT:MASTER:약독내성) * (10 + GET_TRAINLV(TARGET))
	LOCAL:2 = 10 * (2 + TALENT:배합지식 + TALENT:금단의지식 - TALENT:MASTER:약독내성) * (15 + GET_TRAINLV(TARGET))
	BASE:MASTER:체력 += LOCAL:1
	BASE:MASTER:기력 += LOCAL:2
	;죠교대상소변増加
	BASE:MASTER:소변 += 300 + RAND:7 * 50
	;죠교자、죠교대상소모도減少
	TFLAG:조교자소모도 -= 2
	TFLAG:소모도 -= 2
	;ACT파생に栄養剤を飲んだをセット
	TFLAG:ACT파생 = 0
	;栄養剤히트値上昇(ヒート値が残っていた場合上昇量増加)
	FLAG:영양히트 += FLAG:영양히트 > 0 ? 8 # 5
ENDIF
;変数保持処理
FOR LOCAL, 1, 3
	CALLF ACTION_APPLY2_91V(-LOCAL, LOCAL:LOCAL)
NEXT

;変数保持関数
@ACTION_APPLY2_91V(ARG, ARG:1)
#FUNCTION
;ARGが負であればLOCAL:(-ARG)にARG:1を保持し
;正であればLOCAL:ARG을반환
;
;例:(-1, 200) LOCAL:1に200を保持
;   ( 1)      LOCAL:1の200을반환

;ARGが0の時処理するものは無い
IF ARG == 0
;ARGが負なら値を保持
ELSEIF ARG < 0
	LOCAL:(-ARG) = ARG:1
;ARGが正なら値을반환
ELSE
	RETURNF LOCAL:ARG
ENDIF
RETURNF 0

;────────────────────────────────────
;092,치료한다
;────────────────────────────────────
;リアクション前（Ｒ）（접촉）
@ACTION_APPLY_92
;필요ならば미약を使用
CALL LOVEPOTION_USE
;삼각목마解除
TEQUIP:삼각목마 = 0
;안면승마系解除
CALL FACESITTING_SET(0)
;죠교자죠교레벨に応じて접촉SOURCE増加
SOURCE:MASTER:접촉 = 100 + GET_TRAINLV(TARGET) * 10
;죠교자、죠교대상자위を中断
TEQUIP:자위중 = 0
;조수삽입系解除
TFLAG:조수에삽입중 = 0
TFLAG:조수연속교합 = 0
TFLAG:경과시간 += 5
[SKIPSTART]
これらのTEQUIPをCOM도구를 제거한다で解除すると날뛴다扱いになるのは不自然と考える

;その他TEQUIPを解除
TEQUIP:눈가리개 = 0
TEQUIP:관장그릇＋플러그 = 0
TEQUIP:항문바이브 = 0
TEQUIP:바이브 = 0
TEQUIP:오나홀 = 0
TEQUIP:클리캡 = 0
TEQUIP:볼개그 = 0
TEQUIP:페니스밴드 = 0
TEQUIP:항문비즈 = 0
[SKIPEND]
;リアクション後（노출、이탈、달성、체력、기력、사정、분유、소변）
@ACTION_APPLY2_92
;REACT분류が날뛴다だった場合취소
IF IS_COMGRONAME("날뛴다")
	;ACT파생に暴れたため치료できなかったをセット
	TFLAG:ACT파생 = 1
ELSE
	;공포각인취득を中断（？）
	TFLAG:공포각인취득 = 0
	;죠교대상기교、죠교자죠교레벨によって보정値を用意
	LOCAL:1 = 25 * (5 + ABL:MASTER:기교) * (10 + GET_TRAINLV(TARGET))
	LOCAL:2 = LOCAL:1 * 2 / 3
	;보정値に応じて노출、달성SOURCE増加
	SOURCE:MASTER:노출 += 100 + LOCAL:1 / (1 + RAND:4)
	SOURCE:MASTER:달성 += LOCAL:1 / 10
	;보정値に応じて죠교대상체력、기력回復
	BASE:MASTER:체력 += LOCAL:1
	BASE:MASTER:기력 += LOCAL:2
	;죠교자、죠교대상소모도減少
	TFLAG:조교자소모도 -= 1
	TFLAG:소모도 -= 3
	;ACT파생に体の隅々まで撫で回して치료했다をセット
	TFLAG:ACT파생 = 0
	;치료히트値上昇(ヒート値が残っていた場合上昇量増加)
	FLAG:치료히트 += FLAG:치료히트 > 0 ? 8 # 5
ENDIF
;変数保持処理
FOR LOCAL, 1, 3
	CALLF ACTION_APPLY2_92V(-LOCAL, LOCAL:LOCAL)
NEXT

;変数保持関数
@ACTION_APPLY2_92V(ARG, ARG:1)
#FUNCTION
;ARGが負であればLOCAL:(-ARG)にARG:1を保持し
;正であればLOCAL:ARG을반환
;
;例:(-1, 200) LOCAL:1に200を保持
;   ( 1)      LOCAL:1の200을반환

;ARGが0の時処理するものは無い
IF ARG == 0
;ARGが負なら値を保持
ELSEIF ARG < 0
	LOCAL:(-ARG) = ARG:1
;ARGが正なら値을반환
ELSE
	RETURNF LOCAL:ARG
ENDIF
RETURNF 0
