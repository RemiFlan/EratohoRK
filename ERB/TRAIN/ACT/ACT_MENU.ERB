;────────────────────────────────────
;죠교類別の判定 (회화0/애무1/도구2/성교3/수치4/봉사5/가학6/이상7/사역8/성봉10)
;────────────────────────────────────
@ACT_MENU
#DIM LCOUNT

;変数を初期化 ;변수 초기화
CALL SET_CRI_VAR
FOR LCOUNT, 0, 11
	;휴식をはじく
	;사역は処理しない
	SIF LCOUNT == 8 || LCOUNT == 9
		CONTINUE
	CALL SET_CRI_VAR, MENU(LCOUNT), 1
NEXT

LOCALS = ACT_MENU

CALL CHA_CRI_VAR, "筋書き"
CALLFORM %LOCALS%_STORY_T

CALL CHA_CRI_VAR, "状況判断"
CALLFORM %LOCALS%_ESTIMATE_T

CALL CHA_CRI_VAR, "죠교자の素質"
CALLFORM %LOCALS%_TALENT_T

CALL CHA_CRI_VAR, "죠교대상の素質"
CALLFORM %LOCALS%_TALENT_M

CALL CHA_CRI_VAR, "죠교자が임신"
CALLFORM %LOCALS%_MATERNITY_T

CALL CHA_CRI_VAR, "죠교자が처녀"
CALLFORM %LOCALS%_MAIDEN_T

CALL CHA_CRI_VAR, "죠교대상が처녀"
CALLFORM %LOCALS%_MAIDEN_M

CALL CHA_CRI_VAR, "죠교대상が동정"
CALLFORM %LOCALS%_CHERRY_M

CALL CHA_CRI_VAR, "죠교자の能力"
CALLFORM %LOCALS%_ABL_T

CALL CHA_CRI_VAR, "죠교대상の能力"
CALLFORM %LOCALS%_ABL_M

CALL CHA_CRI_VAR, "性別関係"
CALLFORM %LOCALS%_SEX

CALL CHA_CRI_VAR, @"本日の방침(%POLICY("금일")%)"
CALLFORM %LOCALS%_POLICY

CALL CHA_CRI_VAR, @"今回の방침(%POLICY("今")%)"
CALLFORM %LOCALS%_COURCE

CALL CHA_CRI_VAR, "죠교대상の윤활"
CALLFORM %LOCALS%_MOIST_M

CALL CHA_CRI_VAR, "죠교대상の욕정"
CALLFORM %LOCALS%_DESIRE_M

CALL CHA_CRI_VAR, "죠교대상の굴복"
CALLFORM %LOCALS%_YIELD_M

CALL CHA_CRI_VAR, "죠교대상の공순"
CALLFORM %LOCALS%_ALLEGIANCE_M

CALL CHA_CRI_VAR, "죠교대상の사정ゲージ"
CALLFORM %LOCALS%_ECSTASY_M

IF TCVAR:MASTER:요강제발기
	CALL CHA_CRI_VAR, "요강제발기"
ELSE
	CALL CHA_CRI_VAR, "발기도"
ENDIF
CALLFORM %LOCALS%_ERECTION_M

CALL CHA_CRI_VAR, "죠교자の快感"
CALLFORM %LOCALS%_PREASURE_T

CALL CHA_CRI_VAR, "죠교자の체력"
CALLFORM %LOCALS%_PHYSICAL_T

CALL CHA_CRI_VAR, "죠교자の기력"
CALLFORM %LOCALS%_MENTAL_T

CALL CHA_CRI_VAR, "죠교자の이성"
CALLFORM %LOCALS%_REASON_T

CALL CHA_CRI_VAR, "죠교자の흥미"
CALLFORM %LOCALS%_INTEREST_T

CALL CHA_CRI_VAR, "죠교자の초조함"
CALLFORM %LOCALS%_IRRITATION_T

CALL CHA_CRI_VAR, "죠교자の호감도"
CALLFORM %LOCALS%_LIKE_T

CALL CHA_CRI_VAR, "죠교대상の호감도"
CALLFORM %LOCALS%_LIKE_M

CALL CHA_CRI_VAR, @"죠교자の상태(%CONDITION(TCVAR:상태변화)%)"
CALLFORM %LOCALS%_CONDITION_T

CALL CHA_CRI_VAR, @"죠교대상の상태(%CONDITION(TCVAR:MASTER:상태변화)%)"
CALLFORM %LOCALS%_CONDITION_M

CALL CHA_CRI_VAR, "죠교대상の疲労"
CALLFORM %LOCALS%_TIRED_M

CALL CHA_CRI_VAR, "初손회화보정"
CALLFORM %LOCALS%_FIRST

CALL CHA_CRI_VAR, "거울所持ボーナス"
CALLFORM %LOCALS%_MILLER

CALL CHA_CRI_VAR, "더 하드하게 해"
CALLFORM %LOCALS%_PLEASE_HARD

CALL CHA_CRI_VAR, "용서를 빌다"
CALLFORM %LOCALS%_PLEASE_HELP

CALL CHA_CRI_VAR, "気持よくして"
CALLFORM %LOCALS%_PLEASE_PREASURE

CALL CHA_CRI_VAR, "날뛴다"
CALLFORM %LOCALS%_VIOLENT

CALL CHA_CRI_VAR, "도구解除"
CALLFORM %LOCALS%_UNEQUIP

CALL CHA_CRI_VAR, "밀어넘어짐연속애무보정"
CALLFORM %LOCALS%_PETTING_CONTINUE

CALL CHA_CRI_VAR, "シックスナイン誘発"
CALLFORM %LOCALS%_69

CALL CHA_CRI_VAR, "죠교자の服装"
CALLFORM %LOCALS%_CLOTH_T

CALL CHA_CRI_VAR, "죠교대상の服装"
CALLFORM %LOCALS%_CLOTH_M

CALL CHA_CRI_VAR, "미약"
CALLFORM %LOCALS%_DRAG

CALL CHA_CRI_VAR, "바이브"
CALLFORM %LOCALS%_VIBE

CALL CHA_CRI_VAR, "항문바이브"
CALLFORM %LOCALS%_AVIBE

CALL CHA_CRI_VAR, "항문비즈"
CALLFORM %LOCALS%_ABEADS

CALL CHA_CRI_VAR, "관장＋항문プラグ"
CALLFORM %LOCALS%_APLUG

CALL CHA_CRI_VAR, "클리캡"
CALLFORM %LOCALS%_CRICAP

CALL CHA_CRI_VAR, "오나홀"
CALLFORM %LOCALS%_ONAHOLE

CALL CHA_CRI_VAR, "유두캡"
CALLFORM %LOCALS%_NIPPLECAP

CALL CHA_CRI_VAR, "밧줄"
CALLFORM %LOCALS%_ROPE

CALL CHA_CRI_VAR, "눈가리개"
CALLFORM %LOCALS%_EYEMASK

CALL CHA_CRI_VAR, "볼개그"
CALLFORM %LOCALS%_GAG

CALL CHA_CRI_VAR, "삼각목마"
CALLFORM %LOCALS%_HORSE

CALL CHA_CRI_VAR, "야외플레이"
CALLFORM %LOCALS%_OUT

CALL CHA_CRI_VAR, "거울"
CALLFORM %LOCALS%_T_MILLER

CALL CHA_CRI_VAR, "도구解除制限"
CALLFORM %LOCALS%_KILL_UNEQUIP

CALL CHA_CRI_VAR, "죠교자자위중"
CALLFORM %LOCALS%_MASTURBATION_T

CALL CHA_CRI_VAR, "죠교대상자위중"
CALLFORM %LOCALS%_MASTURBATION_M

CALL CHA_CRI_VAR, "징계・"
CALLFORM %LOCALS%_OSHIOKI

CALL CHA_CRI_VAR, "삽입보정"
CALLFORM %LOCALS%_INSERT

CALL CHA_CRI_VAR, "애태움中"
CALLFORM %LOCALS%_IRRITATE

CALL CHA_CRI_VAR, "Ａ섹스回避"
CALLFORM %LOCALS%_AGUARD

CALL CHA_CRI_VAR, "Ａ섹스体位制限"
CALLFORM %LOCALS%_ASEX_LIMITATION

CALL CHA_CRI_VAR, "구상보정"
CALLFORM %LOCALS%_KOJO_CORRECTION_T

CALL CHA_CRI_VAR, "実行判定"
CALLFORM %LOCALS%_ABLE

;가능한ACT계열수リセット
;※ヒート値の보정に使用するのでここで計算
TFLAG:가능한ACT계열수 = 0
;가능한ACT계열수セット
FOR LCOUNT, 0, 11
	;휴식をはじく
	;사역は処理しない
	SIF LCOUNT == 8 || LCOUNT == 9
		CONTINUE
	;-98以上を可能と判断する(多分今は大丈夫だと思うけど)@これみ
	SIF GET_CRI_VAR(MENU(LCOUNT) + "累計") > -99
		TFLAG:가능한ACT계열수++
NEXT

CALL CHA_CRI_VAR, "ヒート値"
CALLFORM %LOCALS%_HEAT

CALL CHA_CRI_VAR, "ACT보정値"
CALLFORM %LOCALS%_CORRECTION

;──────────────────────────────
;최종판정
;──────────────────────────────
CALL DEF_CRI_VAR("最大우선指定-애무/성봉/성교/도구/봉사/수치/가학/사역/이상/회화")
CALLF GET_CRI_VAR("累計")
CALL ACT_MENU_RESULT()
CALL ANA_CRI_VAR()

;──────────────────────────────
;ACT분류判定
;引き分ける時の우선順は애무1＞성봉10＞성교3＞도구2＞봉사5＞수치4＞가학6＞사역8＞이상7＞회화0 ;무승부시의 우선 순서는 애무
;
;ARG = 0:基準値保存 1:分類呼び出し 2:ARGSで指定했다分類を無効化
;ARG = 0 : 기준치 저장 1 : 분류 호출 2 : ARGS로 지정했다 분류를 비활성화
;──────────────────────────────
@ACT_MENU_RESULT(ARG = 0, ARGS)
#DIM LCOUNT
#DIM LTEMP, 2
#DIMS LTEMPS

;ARGが0なら
IF ARG == 0
	;結果を保存
	FOR LCOUNT, 0, 9
		LOCAL:LCOUNT = RESULT:LCOUNT
	NEXT
ENDIF
;分類名読み込み
;※사역を処理するよう되었다ら追加する
SPLIT "회화/애무/도구/성교/수치/봉사/가학/이상/성봉", "/", LOCALS
;ARGが1なら
IF ARG == 1
	;우선順読み込み
	LTEMPS = 애무/성봉/성교/도구/봉사/수치/가학/사역/이상/회화
	;最우선分類を探す
	LTEMP = -99
	LTEMP:1 = -1
	FOR LCOUNT, 0, 9
		;数値が小さい、無効な場合は無視
		IF LOCAL:LCOUNT < LTEMP || LOCAL:LCOUNT <= -99
			CONTINUE
		;数値が同じ場合は우선順に従って切り替え
		ELSEIF LOCAL:LCOUNT == LTEMP
			SIF STRFIND(LTEMPS, LOCALS:LCOUNT) > STRFIND(LTEMPS, LOCALS:(LTEMP:1))
				CONTINUE
		;もちろん数値が大きい場合も切り替え
		ENDIF
		;切り替え時は分類ポインタを動かす
		LTEMP = LOCAL:LCOUNT
		LTEMP:1 = LCOUNT
	NEXT
	;最우선分類が存在しなければ1을반환
	SIF LTEMP:1 == -1
		RETURN 1
	;最우선分類を今回の分類に指定する
	TFLAG:ACT분류 = MENUNUM(LOCALS:(LTEMP:1))
	RETURN 0
;ARGが2なら
ELSEIF ARG == 2
	;指定당했다分類を探す
	LTEMP = FINDELEMENT(LOCALS, ARGS)
	;存在しなければ飛ばす
	SIF LTEMP == -1
		RETURN -1
	;存在すれば分類を判定대상から外す
	LOCAL:LTEMP = -99
	;今回の分類が指定당했다ものならば、未선택に戻す
	SIF TFLAG:ACT분류 == MENUNUM(LOCALS:LTEMP)
		TFLAG:ACT분류 = -1
ENDIF

;-------------------------------------------------------------------------
;筋書き보정 ;줄거리 보정
;-------------------------------------------------------------------------
@ACT_MENU_STORY_T

	;실험 코드
	;CALL ADD_CRI_VAR("성교", 999)
	;CALL ADD_CRI_VAR("성봉", 999)
	
	
[SKIPSTART]
;엑스트라모드以外では
IF FLAG:모드 != 9
	;성교、성봉にマイナス보정(20日경과で完全に解消)
	CALL ADD_CRI_VAR("성교", MIN(DAY / 2 - 10, 0))
	CALL ADD_CRI_VAR("성봉", MIN(DAY / 2 - 10, 0))
ENDIF
[SKIPEND]

;-------------------------------------------------------------------------
;状況判断
;-------------------------------------------------------------------------
@ACT_MENU_ESTIMATE_T
#DIM LCOUNT

FOR LCOUNT, 0, 11
	SIF LCOUNT == 9
		CONTINUE
	CALL ADD_CRI_VAR(MENU(LCOUNT), EST_TO_ACTM("취득", MENU(LCOUNT)))
NEXT

;-------------------------------------------------------------------------
;죠교자の素質を参照
;-------------------------------------------------------------------------
@ACT_MENU_TALENT_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;죠교자が프라이드높음/低い
IF TALENT:LOCAL:프라이드높음
	CALL ADD_CRI_VAR("봉사",  9)
ELSEIF TALENT:LOCAL:프라이드낮음
	CALL ADD_CRI_VAR("봉사", -9)
ENDIF

;죠교자が무관심/호기심
IF TALENT:LOCAL:무관심
	CALL ADD_CRI_VAR("회화", -15)
	CALL ADD_CRI_VAR("애무",  -6)
	CALL ADD_CRI_VAR("도구", , -9)
ELSEIF TALENT:LOCAL:호기심
	CALL ADD_CRI_VAR("회화", 3, 18)
	CALL ADD_CRI_VAR("애무", 6)
	CALL ADD_CRI_VAR("도구", 3,  9)
	CALL ADD_CRI_VAR("성봉", 3,  6)
ENDIF

;죠교자が억압/해방
IF TALENT:LOCAL:억압
	CALL ADD_CRI_VAR("도구",  -6)
	CALL ADD_CRI_VAR("성교",  -6)
	CALL ADD_CRI_VAR("수치", -12)
	CALL ADD_CRI_VAR("가학",  -3)
	CALL ADD_CRI_VAR("이상",  -9)
	CALL ADD_CRI_VAR("성봉",  -3)
ELSEIF TALENT:LOCAL:해방
	CALL ADD_CRI_VAR("도구",   6)
	CALL ADD_CRI_VAR("성교",   6)
	CALL ADD_CRI_VAR("수치",  12)
	CALL ADD_CRI_VAR("가학",   3)
	CALL ADD_CRI_VAR("이상",   9)
	CALL ADD_CRI_VAR("성봉",   3)
ENDIF

;죠교자が수줍음/수치없음
IF TALENT:LOCAL:수줍음
	CALL ADD_CRI_VAR("수치", -9)
	CALL ADD_CRI_VAR("성봉", -6)
ELSEIF TALENT:LOCAL:수치없음
	CALL ADD_CRI_VAR("수치",  9)
	CALL ADD_CRI_VAR("성봉",  6)
ENDIF

;죠교자が혀기술
SIF TALENT:LOCAL:혀기술
	CALL ADD_CRI_VAR("애무", 9)

;죠교자が바느질(바늘所持時)
SIF TALENT:LOCAL:바느질 && ITEM:바늘
	CALL ADD_CRI_VAR("가학", 9)

;죠교자が거친혀
SIF TALENT:LOCAL:거친혀
	CALL ADD_CRI_VAR("애무", -9)

;죠교자が器用な指
SIF TALENT:LOCAL:손가락기술
	CALL ADD_CRI_VAR("애무", 9)

;죠교자が허리기술
IF TALENT:LOCAL:허리기술
	CALL ADD_CRI_VAR("성교", 9)
	CALL ADD_CRI_VAR("성봉", 6)
ENDIF

;죠교자が긴박능숙(밧줄所持時)
SIF TALENT:LOCAL:긴박능숙 && ITEM:밧줄
	CALL ADD_CRI_VAR("가학", 9)

;죠교자が도구능숙
SIF TALENT:LOCAL:도구능숙
	CALL ADD_CRI_VAR("도구", 9)

;죠교자が자위하기쉬움
SIF TALENT:LOCAL:자위하기쉬움
	CALL ADD_CRI_VAR("수치", 9)

;죠교자が헌신적
SIF TALENT:LOCAL:헌신적
	CALL ADD_CRI_VAR("봉사", -9)

;죠교자が수동적
SIF TALENT:LOCAL:수동적
	CALL ADD_CRI_VAR("봉사", 9)

;죠교자が쾌감에솔직/否定
IF TALENT:LOCAL:쾌감에솔직
	CALL ADD_CRI_VAR("성교",  9)
	CALL ADD_CRI_VAR("봉사",  6)
	CALL ADD_CRI_VAR("성봉",  9)
ELSEIF TALENT:LOCAL:쾌감을부정
	CALL ADD_CRI_VAR("성교", -9)
	CALL ADD_CRI_VAR("봉사", -6)
	CALL ADD_CRI_VAR("성봉", -6)
ENDIF

;죠교자が변태적
IF TALENT:LOCAL:변태적
	CALL ADD_CRI_VAR("가학", 3)
	CALL ADD_CRI_VAR("이상", 3)
ENDIF

;죠교자が마음씨착한
SIF TALENT:LOCAL:마음씨착한
	CALL ADD_CRI_VAR("가학", -9)

;죠교자が유치
IF TALENT:LOCAL:유치
	CALL ADD_CRI_VAR("애무", , 15)
	CALL ADD_CRI_VAR("수치", -9)
	CALL ADD_CRI_VAR("가학", -3)
	CALL ADD_CRI_VAR("이상", , 12)
ENDIF

;죠교자が광기
SIF TALENT:LOCAL:광기
	CALL ADD_CRI_VAR("이상", , 20)
	
;-------------------------------------------------------------------------
;죠교대상の素質を参照
;-------------------------------------------------------------------------
@ACT_MENU_TALENT_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? MASTER # ARG
;죠교대상が위압감
IF TALENT:LOCAL:위압감
	CALL ADD_CRI_VAR("봉사", -3)
	CALL ADD_CRI_VAR("가학", -3)
ENDIF

;죠교대상が매혹
IF TALENT:LOCAL:매혹
	CALL ADD_CRI_VAR("애무", , 2)
	CALL ADD_CRI_VAR("성교", , 4)
	CALL ADD_CRI_VAR("성봉", , 5)
ENDIF

;죠교대상が친절함/까다로움
IF TALENT:LOCAL:친절함
	CALL ADD_CRI_VAR("회화",  3)
	CALL ADD_CRI_VAR("도구",   , -2)
	CALL ADD_CRI_VAR("봉사",   ,  2)
	CALL ADD_CRI_VAR("성봉",   ,  2)
ELSEIF TALENT:LOCAL:까다로움
	CALL ADD_CRI_VAR("회화", -3)
	CALL ADD_CRI_VAR("도구",   ,  2)
	CALL ADD_CRI_VAR("봉사",   , -2)
	CALL ADD_CRI_VAR("성봉",   , -2)
ENDIF

;-------------------------------------
;죠교자が임신中
;-------------------------------------
@ACT_MENU_MATERNITY_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG
;임신中
IF TALENT:LOCAL:임신
	CALL ADD_CRI_VAR("가학",  -5)
	CALL ADD_CRI_VAR("이상", -10)
	CALL ADD_CRI_VAR("성봉",  -5)
ENDIF

;-------------------------------------------------------------------------
;동정・처녀関係
;引数は죠교자・죠교대상
;죠교대상が처녀の時、無闇に성교しない仕様です。동정も同じ処理を行うが効果はやや低いです。
;죠교대상が처녀で합의未취득かつ、죠교대상が여자
;-------------------------------------------------------------------------
@ACT_MENU_MAIDEN_T, ARG = -1, ARG:1 = -1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG
LOCAL:1 = ARG == -1 ? MASTER # ARG:1
;죠교자が처녀で、죠교대상に페니스が有る
IF TALENT:LOCAL:처녀 && PENIS(LOCAL:1)
	;죠교자が정조관념/정조무관심
	LOCAL:1 = CFLAG:LOCAL:호의 + CFLAG:LOCAL:M호의 / 2
	IF TALENT:LOCAL:정조관념
		CALL ADD_CRI_VAR("성교", LINE_CALC(LOCAL:1, "0→-12,1200→0,LIMIT/-24～0"))
		CALL ADD_CRI_VAR("성봉", LINE_CALC(LOCAL:1, "0→-12,2400→0,LIMIT/-24～0"))
	ELSEIF TALENT:LOCAL:정조무관심
		CALL ADD_CRI_VAR("성교", LINE_CALC(LOCAL:1, "0→-4,400→0,LIMIT/-8～0"))
		CALL ADD_CRI_VAR("성봉", LINE_CALC(LOCAL:1, "0→-4,800→0,LIMIT/-8～0"))
	ELSE
		CALL ADD_CRI_VAR("성교", LINE_CALC(LOCAL:1, "0→-8,800→0,LIMIT/-16～0"))
		CALL ADD_CRI_VAR("성봉", LINE_CALC(LOCAL:1, "0→-8,1600→0,LIMIT/-16～0"))
	ENDIF
	;죠교자が겁쟁이
	IF TALENT:LOCAL:겁쟁이
		CALL ADD_CRI_VAR("성교", -4)
		CALL ADD_CRI_VAR("성봉", -2)
	ENDIF
	;죠교자が일선을넘지않음
	IF TALENT:LOCAL:일선을넘지않음
		CALL ADD_CRI_VAR("성교", -4)
		CALL ADD_CRI_VAR("성봉", -2)
	ENDIF
ENDIF

;-----------------------------------------
;죠교대상が처녀
;-----------------------------------------
@ACT_MENU_MAIDEN_M, ARG = -1, ARG:1 = 1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG
LOCAL:1 = ARG == -1 ? MASTER # ARG:1
IF TALENT:(LOCAL:1):처녀 && !CFLAG:LOCAL:합의 && !TALENT:(LOCAL:1):후타나리
	CALL ADD_CRI_VAR("성교", -5, -2)
	;죠교자が겁쟁이/자제심/일선을넘지않음/신중함
	SIF TALENT:LOCAL:겁쟁이 || TALENT:LOCAL:자제심 || TALENT:LOCAL:일선을넘지않음 || TALENT:LOCAL:신중함
		CALL ADD_CRI_VAR("성교", -3)
	;죠교자が굳셈/충동적/민감、または죠교자の상태が정욕/분노/싫증/광기のいずれか
	SIF TALENT:LOCAL:굳셈 || TALENT:LOCAL:충동적 || TALENT:LOCAL:민감 || TCVAR:상태변화 > 4
		CALL ADD_CRI_VAR("성교", , 6)
	;後ろが使える상태ならペナルティを軽減(죠교대상のＡ경험3以上)
	SIF EXP:(LOCAL:1):Ａ경험 > 2
		CALL ADD_CRI_VAR("성교", 3)
	;죠교자の죠교Lvが1以上
	SIF CFLAG:LOCAL:죠교레벨 > 0
		CALL ADD_CRI_VAR("성교", GET_TRAINLV(LOCAL) / 3)
	;죠교자への호의が1000超過
	SIF CFLAG:LOCAL:M호의 > 1000
		CALL ADD_CRI_VAR("성교", CFLAG:LOCAL:M호의 / 500)
ENDIF

@ACT_MENU_CHERRY_M, ARG = -1, ARG:1 = -1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG
LOCAL:1 = ARG == -1 ? MASTER # ARG:1
;죠교대상が동정で합의未취득かつ、죠교대상이남자または후타나리처녀
IF TALENT:(LOCAL:1):동정 && !CFLAG:합의 && (TALENT:(LOCAL:1):남자 || TALENT:(LOCAL:1):처녀)
	CALL ADD_CRI_VAR("성교", -3, -1)
	;죠교자が겁쟁이/자제심/일선을넘지않음/신중함
	SIF TALENT:LOCAL:겁쟁이 || TALENT:LOCAL:자제심 || TALENT:LOCAL:일선을넘지않음 || TALENT:LOCAL:신중함
		CALL ADD_CRI_VAR("성교", -2)
	;죠교자が굳셈/충동적/민감、または죠교자の상태が정욕/분노/싫증/광기のいずれか
	SIF TALENT:LOCAL:굳셈 || TALENT:LOCAL:충동적 || TALENT:LOCAL:민감 || TCVAR:상태변화 > 4
		CALL ADD_CRI_VAR("성교", , 3)
	;後ろが使える상태ならペナルティを軽減(죠교대상のＡ경험2以上)
	SIF EXP:(LOCAL:1):Ａ경험 > 1
		CALL ADD_CRI_VAR("성교", 2)
	;죠교자の죠교Lvが1以上
	SIF CFLAG:LOCAL:죠교레벨 > 0
		CALL ADD_CRI_VAR("성교", GET_TRAINLV(LOCAL) / 3)
	;죠교자への호의が1000超過
	SIF CFLAG:LOCAL:M호의 > 1000
		CALL ADD_CRI_VAR("성교", CFLAG:LOCAL:M호의 / 500)
ENDIF

;-------------------------------------------------------------------------
;죠교자の能力を参照
;-------------------------------------------------------------------------
@ACT_MENU_ABL_T, ARG = -1
#LOCALSIZE 1
#DIM LCOUNT
LOCAL = ARG == -1 ? TARGET # ARG

;既にある物を活かす時に使い、
;新規開拓時は符号を逆にして使うこと（今はいいや）
[SKIPSTART]

;基本的な能力보정
FOR LCOUNT, 0, 11
	;죠교種別から対応するABLを割り出し、ABL보정値の1/5を加算
	CALL ADD_CRI_VAR(MENU(LCOUNT), GET_ABL(LOCAL, ABLNAME:(20 + LCOUNT)) * 2 / 10)
NEXT

;ＣＶ감각の合計値が[0→0, 1→1, 4→2, 7→3]を加算(0～3)
CALL ADD_CRI_VAR("성교", LINE_CALC(ABL:LOCAL:Ｃ감각 + ABL:LOCAL:Ｖ감각, "1→1,7→3,LIMIT/0～3"))

;죠교자のＶ감각
CALL ADD_CRI_VAR("성봉", GET_ABL(LOCAL, "Ｖ감각") / 20)

;죠교자の노출증
SIF ABL:LOCAL:노출증
	CALL ADD_CRI_VAR("수치", GET_ABL(LOCAL, "노출증") / 20)

[SKIPEND]

;-------------------------------------------------------------------------
;죠교대상の能力を参照
;-------------------------------------------------------------------------
@ACT_MENU_ABL_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? MASTER # ARG

;죠교대상の순종と봉사정신の合計値が[0→0, 1→1, 4→2, 7→3]を加算(0～3)
CALL ADD_CRI_VAR("봉사", LINE_CALC(ABL:LOCAL:순종 + ABL:LOCAL:봉사정신, "1→1,7→3,LIMIT/0～3"))
;죠교대상の순종と봉사정신の合計値が[1→0, 4→1, 7→2]を加算(0～2)
CALL ADD_CRI_VAR("성봉", LINE_CALC(ABL:LOCAL:순종 + ABL:LOCAL:봉사정신, "4→1,7→2,LIMIT/0～2"))

;죠교대상のＣＶ감각の合計値が[0→0, 1→0～1, 4→0～2, 7→0～3]を加算(0～3)
CALL ADD_CRI_VAR("도구", , LINE_CALC(ABL:LOCAL:Ｃ감각 + ABL:LOCAL:Ｖ감각, "1→1,7→3,LIMIT/0～3"))
CALL ADD_CRI_VAR("성교", , LINE_CALC(ABL:LOCAL:Ｃ감각 + ABL:LOCAL:Ｖ감각, "1→1,7→3,LIMIT/0～3"))

;죠교대상のＡ감각が[0→0～1, 1→0～2, 3→0～4, 5→0～6]を加算(0～6)
CALL ADD_CRI_VAR("도구", , LINE_CALC(ABL:LOCAL:Ａ감각, "1→2,5→6,LIMIT/1～6"))
;죠교대상のＡ감각が[0→0, 1→0～1, 3→0～3, 5→0～5]を加算(0～5)
CALL ADD_CRI_VAR("이상", , LINE_CALC(ABL:LOCAL:Ａ감각, "1→1,5→5,LIMIT/0～5"))
;죠교대상のＡ감각が[0→0, 3→1, 5→2]を加算(0～2)
CALL ADD_CRI_VAR("성교", LINE_CALC(ABL:LOCAL:Ａ감각, "3→1,5→2,LIMIT/0～2"))

;죠교대상のＢ감각が[0→0, 3→0～1, 5→0～2]を加算(0～2)
CALL ADD_CRI_VAR("애무", , LINE_CALC(ABL:LOCAL:Ｂ감각, "3→1,5→2,LIMIT/0～2"))
;죠교대상のＢ감각が[0→0, 1→0, 3→0～1]を加算(0～1)
CALL ADD_CRI_VAR("봉사", , LINE_CALC(ABL:LOCAL:Ｂ감각, "1→0,3→1,LIMIT/0～1"))

;죠교대상のマゾ相関能力(마조끼/고통중독/구속중독の合計)
;※なぜか레즈끼を見ていたので마조끼に変更
;が[0→0, 3→0～2, 6→0～4, 9→0～6, 12→0～8]を加算(0～8)
CALL ADD_CRI_VAR("가학", , LINE_CALC(ABL:LOCAL:마조끼 + ABL:LOCAL:고통중독 + ABL:LOCAL:구속중독, "0→0,12→8,LIMIT/0～8"))
;が[0→0, 3→0, 6→0～2, 9→0～4, 12→0～6]を加算(0～6)
CALL ADD_CRI_VAR("이상", , LINE_CALC(ABL:LOCAL:마조끼 + ABL:LOCAL:고통중독 + ABL:LOCAL:구속중독, "3→0,12→6,LIMIT/0～6"))
;が[0→0, 9→0, 12→0～2]を加算(0～2)
CALL ADD_CRI_VAR("사역", , LINE_CALC(ABL:LOCAL:마조끼 + ABL:LOCAL:고통중독 + ABL:LOCAL:구속중독, "9→0,12→2,LIMIT/0～2"))

;-------------------------------------------------------------------------
;性別関係
;引数は죠교자・죠교대상
;同性で対応の能力や素質がない場合は体の접촉を避けるように
;-------------------------------------------------------------------------
@ACT_MENU_SEX, ARG = -1, ARG:1 = -1
LOCAL = ARG == -1 ? TARGET # ARG
LOCAL:1 = ARG == -1 ? MASTER # ARG:1

;죠교자に바이が無く、남자同士
IF TALENT:LOCAL:남자 && TALENT:(LOCAL:1):남자 && !TALENT:LOCAL:바이
	;죠교자のＢＬ끼が[0→-2, 2→-1, 4→0]を加算(-2～0)
	CALL ADD_CRI_VAR("봉사", LINE_CALC(ABL:LOCAL:ＢＬ끼, "0→-2,4→0,LIMIT/-2～0"))
	;죠교자のＢＬ끼が[0→-4, 1→-3, 2→-2, 3→-1, 4→0]を加算(-4～0)
	CALL ADD_CRI_VAR("애무", LINE_CALC(ABL:LOCAL:ＢＬ끼, "0→-4,4→0,LIMIT/-4～0"))
	CALL ADD_CRI_VAR("성교", LINE_CALC(ABL:LOCAL:ＢＬ끼, "0→-4,4→0,LIMIT/-4～0"))
ENDIF
;죠교자に바이が無く、여자同士
IF !TALENT:LOCAL:남자 && !TALENT:(LOCAL:1):남자 && !TALENT:LOCAL:바이
	;죠교자の레즈끼が[0→-2, 2→-1, 4→0]を加算(-2～0)
	CALL ADD_CRI_VAR("봉사", LINE_CALC(ABL:LOCAL:레즈끼, "0→-2,4→0,LIMIT/-2～0"))
	;죠교자の레즈끼が[0→-4, 1→-3, 2→-2, 3→-1, 4→0]を加算(-4～0)
	CALL ADD_CRI_VAR("애무", LINE_CALC(ABL:LOCAL:레즈끼, "0→-4,4→0,LIMIT/-4～0"))
	CALL ADD_CRI_VAR("성교", LINE_CALC(ABL:LOCAL:레즈끼, "0→-4,4→0,LIMIT/-4～0"))
	CALL ADD_CRI_VAR("성봉", LINE_CALC(ABL:LOCAL:레즈끼, "0→-4,4→0,LIMIT/-4～0"))
	
	;죠교자の레즈중독が[0→0, 1→1, 3→2, 5→3]を加算(0～3)
	CALL ADD_CRI_VAR("애무", LINE_CALC(ABL:LOCAL:레즈중독, "1→1,5→3,LIMIT/0～3"))
	CALL ADD_CRI_VAR("성교", LINE_CALC(ABL:LOCAL:레즈중독, "1→1,5→3,LIMIT/0～3"))
	CALL ADD_CRI_VAR("성봉", LINE_CALC(ABL:LOCAL:레즈중독, "1→1,5→3,LIMIT/0～3"))
	CALL ADD_CRI_VAR("봉사", LINE_CALC(ABL:LOCAL:레즈중독, "1→1,5→3,LIMIT/0～3"))
ENDIF

;죠교자が남성혐오かつ、죠교대상이남자
IF TALENT:LOCAL:남성혐오 && TALENT:(LOCAL:1):남자
	CALL ADD_CRI_VAR("회화",  2)
	CALL ADD_CRI_VAR("애무", -3)
	CALL ADD_CRI_VAR("성교", -4)
	CALL ADD_CRI_VAR("봉사", -2)
	CALL ADD_CRI_VAR("가학",  1)
	CALL ADD_CRI_VAR("성봉", -4)
ENDIF

[SKIPSTART]
/////////////////////////////////////
本日の방침
	0=통상         (언제나처럼)
	1=항문       (항문을괴롭혀)
	2=징계・     (징계해)
	3=하드       (격렬하게)
	4=노닥노닥 (응석부리게)
	5=이상         (망가질때까지)
	6=야외         (밖에서해)
	7=애만들기       (임신해)
	8=휴식         (쉬게)
	9=抱いて       (抱かれたい)
	10=ご봉사      (봉사하게)
	11=귀여워해  (かわいがって)
/////////////////////////////////////
[SKIPEND]
@ACT_MENU_POLICY
SELECTCASE POLICY("금일")
	CASE "항문"
		;죠교자が후타나리であるか페니스밴드所持の場合は성교系を誘発するように
		IF (CFLAG:M호의 + CFLAG:호의 >= 10000) && (TALENT:TARGET:후타나리 || ITEM:페니스밴드)
			CALL ADD_CRI_VAR("회화", -10)
			CALL ADD_CRI_VAR("애무",  -8)
			CALL ADD_CRI_VAR("도구", -10)
			CALL ADD_CRI_VAR("성교",  10)
			CALL ADD_CRI_VAR("수치", -10)
			CALL ADD_CRI_VAR("봉사",  -6)
			CALL ADD_CRI_VAR("가학", -10)
			CALL ADD_CRI_VAR("이상", -10)
			CALL ADD_CRI_VAR("성봉", -10)
		;その他の場合
		ELSE
			CALL ADD_CRI_VAR("애무",  10)
			CALL ADD_CRI_VAR("도구",  10)
			CALL ADD_CRI_VAR("가학", ITEM:관장세트 ? 10 # -10)
			CALL ADD_CRI_VAR("성봉", -10)
		ENDIF
	CASE "징계・"
		;징계・時は징계・플래그が立つのみ
		;何らかの손入れをしてもいいのかも知れない
	CASE "하드"
		CALL ADD_CRI_VAR("도구",   5)
		CALL ADD_CRI_VAR("봉사",   8)
		CALL ADD_CRI_VAR("가학",  10)
	CASE "노닥노닥"
		;죠교대상が女かつ페니스밴드所持の場合は성봉系を誘発するように
		IF (CFLAG:M호의 + CFLAG:호의 >= 10000) && (ITEM:페니스밴드 && !PENIS(MASTER))
			CALL ADD_CRI_VAR("회화",  -7)
			CALL ADD_CRI_VAR("애무",  -8)
			CALL ADD_CRI_VAR("도구", -10)
			CALL ADD_CRI_VAR("성교",  -5)
			CALL ADD_CRI_VAR("수치", -10)
			CALL ADD_CRI_VAR("봉사",  -3)
			CALL ADD_CRI_VAR("가학", -10)
			CALL ADD_CRI_VAR("이상", -10)
			CALL ADD_CRI_VAR("성봉",  10)
		ELSE
			CALL ADD_CRI_VAR("애무",   8)
			CALL ADD_CRI_VAR("도구", -10)
			CALL ADD_CRI_VAR("성교",  -5)
			CALL ADD_CRI_VAR("수치", -10)
			CALL ADD_CRI_VAR("봉사",  10)
			CALL ADD_CRI_VAR("가학", -10)
			CALL ADD_CRI_VAR("이상", -10)
			CALL ADD_CRI_VAR("성봉",  -5)
		ENDIF
	CASE "이상"
		CALL ADD_CRI_VAR("가학",  10)
		CALL ADD_CRI_VAR("이상",  10, 10)
		CALL ADD_CRI_VAR("사역",  10)
	CASE "애만들기"
		CALL ADD_CRI_VAR("회화",  -8)
		CALL ADD_CRI_VAR("애무",  -8)
		CALL ADD_CRI_VAR("도구", -10)
		CALL ADD_CRI_VAR("성교", -10)
		CALL ADD_CRI_VAR("수치", -10)
		CALL ADD_CRI_VAR("봉사",  -4)
		CALL ADD_CRI_VAR("가학", -10)
		CALL ADD_CRI_VAR("이상", -10)
		CALL ADD_CRI_VAR("성봉",  10)
	CASE "抱いて"
		CALL ADD_CRI_VAR("회화", -10)
		CALL ADD_CRI_VAR("애무",  -9)
		CALL ADD_CRI_VAR("도구", -10)
		CALL ADD_CRI_VAR("성교",  10)
		CALL ADD_CRI_VAR("수치", -10)
		CALL ADD_CRI_VAR("봉사",  -2)
		CALL ADD_CRI_VAR("가학", -10)
		CALL ADD_CRI_VAR("이상", -10)
		CALL ADD_CRI_VAR("성봉", -10)
	CASE "ご봉사"
		CALL ADD_CRI_VAR("회화", -10)
		CALL ADD_CRI_VAR("애무", -10)
		CALL ADD_CRI_VAR("도구", -10)
		CALL ADD_CRI_VAR("성교", -10)
		CALL ADD_CRI_VAR("수치", -10)
		CALL ADD_CRI_VAR("봉사",  10)
		CALL ADD_CRI_VAR("가학", -10)
		CALL ADD_CRI_VAR("이상", -10)
		CALL ADD_CRI_VAR("성봉",  -5)
	CASE "귀여워해"
		CALL ADD_CRI_VAR("회화",   5)
		CALL ADD_CRI_VAR("애무",  10)
		CALL ADD_CRI_VAR("도구", -10)
		CALL ADD_CRI_VAR("수치", -10)
		CALL ADD_CRI_VAR("봉사", -10)
		CALL ADD_CRI_VAR("가학", -10)
		CALL ADD_CRI_VAR("이상", -10)
		CALL ADD_CRI_VAR("성봉", -10)
		;죠교대상이남자でない場合、条件を満たしていれば성교系に+보정
		SIF !TALENT:MASTER:남자 && (CFLAG:M호의 + CFLAG:호의 >= 10000)
			CALL ADD_CRI_VAR("성교", 5)
	CASEELSE
		;none
ENDSELECT

;------------------------------------------
;今回の죠교방침
;------------------------------------------
@ACT_MENU_COURCE

SELECTCASE POLICY("今")
	CASE "소프트"
		CALL ADD_CRI_VAR("회화",   7)
		CALL ADD_CRI_VAR("애무",   5)
		CALL ADD_CRI_VAR("성교",  -3)
		CALL ADD_CRI_VAR("가학", -10)
		CALL ADD_CRI_VAR("이상", -10)
		CALL ADD_CRI_VAR("성봉",  -3)
	CASE "노멀"
		CALL ADD_CRI_VAR("가학",  -5)
		CALL ADD_CRI_VAR("이상",  -5)
	CASE "하드"
		CALL ADD_CRI_VAR("회화",  -5)
		CALL ADD_CRI_VAR("애무",  -5)
		CALL ADD_CRI_VAR("봉사",  -3)
	CASE "이상"
		CALL ADD_CRI_VAR("회화",  -5)
		CALL ADD_CRI_VAR("애무",  -5)
		CALL ADD_CRI_VAR("봉사",  -3)
		CALL ADD_CRI_VAR("가학",  10)
		CALL ADD_CRI_VAR("이상",  10, 10)
		CALL ADD_CRI_VAR("사역",  10)
;	CASE "SM"
;		CALL ADD_CRI_VAR("회화", -10)
;		CALL ADD_CRI_VAR("애무", -10)
;		SIF !TALENT:MASTER:남자
;			CALL ADD_CRI_VAR("성교", -20)
;		CALL ADD_CRI_VAR("봉사", , -10)
;		CALL ADD_CRI_VAR("가학", 15)
;		CALL ADD_CRI_VAR("이상", 10)
;		CALL ADD_CRI_VAR("성봉", -20)
ENDSELECT

;-------------------------------------------------------------------------
;PALAM群
;ここは個別に
;-------------------------------------------------------------------------
;------------------------------------------
;윤활
;------------------------------------------
@ACT_MENU_MOIST_M, ARG = -1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG

;죠교대상の윤활PALAMが[0→-6, 1000→-4, 2000→-2, 3000→0]を加算(-6～0)
LOCAL:1 = LINE_CALC(PALAM:LOCAL:윤활, "0→-6,3000→0,LIMIT/-6～0")
;죠교자が신중함ならより減算
IF TALENT:신중함
	LOCAL:1 = LOCAL:1 * 8 / 5
;죠교자が민감なら軽減
ELSEIF TALENT:민감
	LOCAL:1 = LOCAL:1 * 2 / 5
ENDIF
;도구は성교よりも減算量が小さい
CALL ADD_CRI_VAR("성교", LOCAL:1)
CALL ADD_CRI_VAR("도구", LOCAL:1 * 2 / 5)

;----------------------------------
;욕정
;----------------------------------
@ACT_MENU_DESIRE_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;죠교대상の욕정PALAMが[0→3～6, 1000→2～4, 2000→1～2, 3000→0, 4000→-2, 5000→-4]を加算(-4～6)
CALL ADD_CRI_VAR("애무", LINE_CALC(PALAM:LOCAL:욕정, "0→3,3000→0,5000→-4,LIMIT/-4～3"), LINE_CALC(PALAM:LOCAL:욕정, "0→3,3000→0,LIMIT/0～3"))
;죠교대상の욕정PALAMが[0→-2, 1000→-1, 2000→2, 3000→4, 4000→6, 5000→4]を加算(-2～6)
CALL ADD_CRI_VAR("성교", LINE_CALC(PALAM:LOCAL:욕정, "0→-2,2000→0,2000→2,4000→6,5000→4,LIMIT/-2～6"))
CALL ADD_CRI_VAR("성봉", LINE_CALC(PALAM:LOCAL:욕정, "0→-2,2000→0,2000→2,4000→6,5000→4,LIMIT/-2～6"))
;죠교대상の욕정PALAMが[0→1, 1000→2, 2000→3, 3000→4, 4000→3, 5000→2]を加算(1～4)
CALL ADD_CRI_VAR("도구", LINE_CALC(PALAM:LOCAL:욕정, "0→1,3000→4,5000→2,LIMIT/1～4"))

;----------------------------------
;굴복
;----------------------------------
@ACT_MENU_YIELD_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;죠교대상の굴복PALAMが[4000→-2～-1, 5000→-4～-2, 6000→-5～-2, 7000→-7～-3, 8000→-8～-3]を加算(-8～0)
CALL ADD_CRI_VAR("가학", LINE_CALC(PALAM:LOCAL:굴복, "4000→-1,8000→-3,LIMIT/-3～0"), LINE_CALC(PALAM:LOCAL:굴복, "4000→-1,8000→-5,LIMIT/-5～0"))
;죠교대상の굴복PALAMが[4000→-2～0, 5000→-4～0, 6000→-6～0, 7000→-8～0, 8000→-10～0]を加算(-10～0)
CALL ADD_CRI_VAR("사역", , LINE_CALC(PALAM:LOCAL:굴복, "4000→-2,8000→-10,LIMIT/-10～0"))

;----------------------------------
;공순
;----------------------------------
@ACT_MENU_ALLEGIANCE_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;죠교대상の공순PALAMが[4000→0, 8000→3]を加算(0～3)
CALL ADD_CRI_VAR("애무", LINE_CALC(PALAM:LOCAL:공순, "4000→0,8000→3,LIMIT/0～3"))
;죠교대상の공순PALAMが[4000→1, 5000→2, 6000→3, 7000→4, 8000→5]を加算(0～6)
CALL ADD_CRI_VAR("성교", LINE_CALC(PALAM:LOCAL:공순, "4000→1,8000→5,LIMIT/0～6"))
CALL ADD_CRI_VAR("봉사", LINE_CALC(PALAM:LOCAL:공순, "4000→1,8000→5,LIMIT/0～6"))
CALL ADD_CRI_VAR("성봉", LINE_CALC(PALAM:LOCAL:공순, "4000→1,8000→5,LIMIT/0～6"))
;죠교대상の공순PALAMが[4000→0, 5000→-1, 6000→-2, 7000→-3, 8000→-4]を加算(-4～0)
CALL ADD_CRI_VAR("가학", LINE_CALC(PALAM:LOCAL:공순, "4000→0,8000→-4,LIMIT/-4～0"))

;----------------------------------
;죠교대상の사정ゲージ
;----------------------------------
@ACT_MENU_ECSTASY_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? MASTER # ARG
;죠교대상の사정BASEが[0%→-3, 100%→0]を加算(-3～0)
SIF PENIS(LOCAL) && !TEQUIP:성교봉사중
	CALL ADD_CRI_VAR("성봉", LINE_CALC(10 * BASE:LOCAL:사정 / MAXBASE:LOCAL:사정, "0→-3,10→0,LIMIT/-3～0"))

;----------------------------------
;발기도
;
;요강제발기모드時は성봉のペナルティなし
;に一応してみる
;---------------------------------
@ACT_MENU_ERECTION_M
;발기도(요강제발기모드)
IF TCVAR:MASTER:요강제발기
	;죠교대상の발기도が[400→5,1000→0]を加算(0～5)
	CALL ADD_CRI_VAR("애무", LINE_CALC(TCVAR:MASTER:발기도, "400→5,1000→0,LIMIT/0～5"))
ELSE
	;죠교대상の발기도が[400→-2,1000→-1]を加算(-3～0)
	CALL ADD_CRI_VAR("성봉", LINE_CALC(TCVAR:MASTER:발기도, "400→-2,1000→-1,LIMIT/-3～0"))
ENDIF

;-------------------------------------------------------------------------
;죠교자のベースを参照
;-------------------------------------------------------------------------
;----------------------------------
;죠교자の快感(쾌Ｃｖａｂの合計)
;---------------------------------
@ACT_MENU_PREASURE_T, ARG = -1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG

;죠교자快感の合計を計算
LOCAL:1 = PALAM:쾌Ｃ + PALAM:쾌Ｖ + PALAM:쾌Ａ + PALAM:쾌Ｂ
;合計が[0→0, 600→1, 1200→2, 1800→3, 2400→4]を加算(0～4)
CALL ADD_CRI_VAR("봉사", LINE_CALC(LOCAL:1, "0→0,2400→4,LIMIT/0～4"))
CALL ADD_CRI_VAR("성봉", LINE_CALC(LOCAL:1, "0→0,2400→4,LIMIT/0～4"))
;合計が[600→0, 1200→1, 1800→2, 2400→3]を加算(0～4)
CALL ADD_CRI_VAR("성교", LINE_CALC(LOCAL:1, "600→0,2400→3,LIMIT/0～4"))
;合計が[1200→0, 1800→-1, 2400→-2]を加算(-2～0)
CALL ADD_CRI_VAR("애무", LINE_CALC(LOCAL:1, "1200→0,2400→-2,LIMIT/-2～0"))
CALL ADD_CRI_VAR("도구", LINE_CALC(LOCAL:1, "1200→0,2400→-2,LIMIT/-2～0"))

;-------------------------------------
;죠교자の체력
;-------------------------------------
@ACT_MENU_PHYSICAL_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;죠교자の체력が[0→4, 500→2, 1000→0, 1500→-2]を加算(-2～4)
CALL ADD_CRI_VAR("회화", LINE_CALC(BASE:LOCAL:체력, "0→4,1500→-2,LIMIT/-2～4"))
;죠교자の체력が[0→2, 500→1, 1000→0]を加算(0～2)
CALL ADD_CRI_VAR("봉사", LINE_CALC(BASE:LOCAL:체력, "0→2,1000→0,LIMIT/0～2"))
;죠교자の체력が[0→-2, 500→-1, 1000→0]を加算(-2～0)
CALL ADD_CRI_VAR("애무", LINE_CALC(BASE:LOCAL:체력, "0→-2,1000→0,LIMIT/-2～0"))
CALL ADD_CRI_VAR("가학", LINE_CALC(BASE:LOCAL:체력, "0→-2,1000→0,LIMIT/-2～0"))
;죠교자の체력が[0→-3, 500→-2, 1000→-1, 1500→0]を加算(-3～0)
CALL ADD_CRI_VAR("성교", LINE_CALC(BASE:LOCAL:체력, "0→-3,1500→0,LIMIT/-3～0"))

;-------------------------------------
;죠교자の기력
;-------------------------------------
@ACT_MENU_MENTAL_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;죠교자の기력が[0→3, 250→2, 500→1, 1000→0]を加算(0～3)
CALL ADD_CRI_VAR("봉사", LINE_CALC(BASE:LOCAL:기력, "0→3,500→1,1000→0,LIMIT/0～3"))
;죠교자の기력が[0→2, 250→1, 500→0, 1000→-1]を加算(-1～2)
CALL ADD_CRI_VAR("사역", LINE_CALC(BASE:LOCAL:기력, "0→2,500→0,1000→-1,LIMIT/-1～2"))
;죠교자の기력が[0→-2, 250→-1, 500→0, 1000→2]を加算(-2～2)
CALL ADD_CRI_VAR("애무", LINE_CALC(BASE:LOCAL:기력, "0→-2,1000→2,LIMIT/-2～2"))
CALL ADD_CRI_VAR("성교", LINE_CALC(BASE:LOCAL:기력, "0→-2,1000→2,LIMIT/-2～2"))
;죠교자の기력が[0→-2, 250→-1, 500→0, 1000→1]を加算(-2～1)
CALL ADD_CRI_VAR("도구", LINE_CALC(BASE:LOCAL:기력, "0→-2,500→0,1000→1,LIMIT/-2～1"))
;죠교자の기력が[0→-2, 250→-1, 500→0]を加算(-2～0)
CALL ADD_CRI_VAR("수치", LINE_CALC(BASE:LOCAL:기력, "0→-2,500→0,LIMIT/-2～0"))
CALL ADD_CRI_VAR("성봉", LINE_CALC(BASE:LOCAL:기력, "0→-2,500→0,LIMIT/-2～0"))
;죠교자の기력が[0→-3, 250→-2, 500→-1, 1000→1]を加算(-3～1)
CALL ADD_CRI_VAR("가학", LINE_CALC(BASE:LOCAL:기력, "0→-3,1000→1,LIMIT/-3～1"))
CALL ADD_CRI_VAR("이상", LINE_CALC(BASE:LOCAL:기력, "0→-3,1000→1,LIMIT/-3～1"))

;-------------------------------------
;죠교자の이성
;-------------------------------------
@ACT_MENU_REASON_T, ARG = -1
#LOCALSIZE 1
#DIM LCOUNT
LOCAL = ARG == -1 ? TARGET # ARG

;죠교자の이성が[0→0～4, 250→0～2, 500→0]を加算(0～4)
FOR LCOUNT, 0, 11
	CALL ADD_CRI_VAR, MENU(LCOUNT), , LINE_CALC(BASE:LOCAL:이성, "0→4,500→0,LIMIT/0～4")
NEXT
;죠교자の이성が750を超過していれば-4～0を加算
SIF BASE:LOCAL:이성 > 750
	CALL ADD_CRI_VAR("이상", , -4)

;-------------------------------------
;죠교자の흥미
;-------------------------------------
@ACT_MENU_INTEREST_T, ARG = -1
#LOCALSIZE 2
#DIM LCOUNT
LOCAL = ARG == -1 ? TARGET # ARG

;죠교자の흥미を취득する
LOCAL:1 = BASE:LOCAL:흥미
FOR LCOUNT, 0, 11
	;これらは[0→3, 250→1, 500→0, 750→-2, 1000→-3](-3～3)
	SIF MENU(LCOUNT) == "사역"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→3,1000→-3,LIMIT/-3～3"))
	;これらは[0→-3, 250→-2, 500→0, 750→1, 1000→3](-3～3)
	SIF GROUPMATCH(MENU(LCOUNT), "회화", "애무", "성교")
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→-3,1000→3,LIMIT/-3～3"))
	;これらは[0→-2, 250→-1, 500→0, 750→1, 1000→2](-2～2)
	SIF GROUPMATCH(MENU(LCOUNT), "도구", "봉사")
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→-2,1000→2,LIMIT/-2～2"))
	;これらは[0→-3, 250→-2, 500→-1, 750→0, 1000→1](-3～1)
	SIF MENU(LCOUNT) == "성봉"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→-3,1000→1,LIMIT/-3～1"))
NEXT

;-------------------------------------
;죠교자の초조함
;-------------------------------------
@ACT_MENU_IRRITATION_T, ARG = -1
#LOCALSIZE 2
#DIM LCOUNT
LOCAL = ARG == -1 ? TARGET # ARG

;죠교자の초조함を취득する
LOCAL:1 = BASE:LOCAL:초조함
FOR LCOUNT, 0, 11
	;これらは[200→2, 400→3, 600→4, 800→5](2～5)
	SIF MENU(LCOUNT) == "가학"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "200→2,800→5,LIMIT/2～5"))
	;これらは[0→1, 200→2, 400→3, 600→4, 800→5](1～5)
	SIF MENU(LCOUNT) == "사역"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→1,800→5,LIMIT/1～5"))
	;これらは[400→0, 600→-2, 800→-3](-3～0)
	SIF GROUPMATCH(MENU(LCOUNT), "도구", "성교")
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "400→0,800→-3,LIMIT/-3～0"))
	;これらは[200→0, 400→-1, 600→-3, 800→-5](-5～0)
	SIF GROUPMATCH(MENU(LCOUNT), "회화", "애무")
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "400→-1,800→-5,LIMIT/-5～0"))
	;これらは[0→0, 200→-1, 400→-2, 600→-3, 800→-4](-4～0)
	SIF MENU(LCOUNT) == "성봉"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→0,800→-4,LIMIT/-4～0"))
NEXT

;-------------------------------------
;죠교자の호의
;-------------------------------------
@ACT_MENU_LIKE_T

;죠교자の호의が[-2000→-6, 2000→0]を加算(-6～0)
CALL ADD_CRI_VAR("성봉", LINE_CALC(CFLAG:호의, "-2000→-6,2000→0,LIMIT/-6～0"))
;죠교자の호의が[-2000→-5, 2000→0]を加算(-5～0)
CALL ADD_CRI_VAR("성교", LINE_CALC(CFLAG:호의, "-2000→-5,2000→0,LIMIT/-5～0"))
;죠교자の호의が[-2000→-4, 2000→4]を加算(-4～4)
CALL ADD_CRI_VAR("애무", LINE_CALC(CFLAG:호의, "-2000→-4,2000→4,LIMIT/-4～4"))

;-------------------------------------
;죠교대상の호의
;主人公から죠교자への호의は죠교자自身が保持しているのに注意
;-------------------------------------
@ACT_MENU_LIKE_M

;죠교대상の호의が[-2000→-10, 2000→0]を加算(-10～0)
CALL ADD_CRI_VAR("성봉", LINE_CALC(CFLAG:M호의, "-2000→-10,2000→0,LIMIT/-10～0"))
;죠교대상の호의が[-2000→-12, 2000→0]を加算(-12～0)
CALL ADD_CRI_VAR("성교", LINE_CALC(CFLAG:M호의, "-2000→-12,2000→0,LIMIT/-12～0"))
;죠교대상の호의が[-2000→-4, 2000→4]を加算(-4～4)
CALL ADD_CRI_VAR("봉사", LINE_CALC(CFLAG:M호의, "-2000→-4,2000→4,LIMIT/-4～4"))

;-------------------------------------
;죠교자の상태
;-------------------------------------
@ACT_MENU_CONDITION_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;죠교자の상태(0=통상/1=피폐/2=쇠약/3=무기력/4=몽롱/5=정욕/6=분노/7=싫증/8=울굴)
SELECTCASE TCVAR:상태변화
	;피폐・쇠약
	CASE 1, 2
		;죠교자の疲れ
		CALL ADD_CRI_VAR("회화",   TCVAR:상태변화 * 2 + TFLAG:조교자소모도)
		CALL ADD_CRI_VAR("성교", -(TCVAR:상태변화 * 3 + TFLAG:조교자소모도))
		CALL ADD_CRI_VAR("가학", -(TCVAR:상태변화 * 2 + TFLAG:조교자소모도))
		CALL ADD_CRI_VAR("성봉", -(TCVAR:상태변화 * 3 + TFLAG:조교자소모도))
	;무기력
	CASE 3
		CALL ADD_CRI_VAR("애무", -3)
		CALL ADD_CRI_VAR("도구", -1)
		CALL ADD_CRI_VAR("성교", -5)
		CALL ADD_CRI_VAR("봉사",  5)
		CALL ADD_CRI_VAR("가학", -3)
		CALL ADD_CRI_VAR("이상", -1)
		CALL ADD_CRI_VAR("사역",  3)
	;몽롱
	CASE 4
		CALL ADD_CRI_VAR("애무", -3)
		CALL ADD_CRI_VAR("도구", -7)
		CALL ADD_CRI_VAR("성교", -3)
		CALL ADD_CRI_VAR("수치",   , 6)
		CALL ADD_CRI_VAR("봉사",   , 3)
		CALL ADD_CRI_VAR("가학",   , 2)
		CALL ADD_CRI_VAR("이상",   , 2)
		CALL ADD_CRI_VAR("성봉",   , 2)
	;정욕
	CASE 5
		CALL ADD_CRI_VAR("애무",  2)
		CALL ADD_CRI_VAR("도구",  1)
		CALL ADD_CRI_VAR("성교", TALENT:충동적 ? 7 # 4, 5)
		CALL ADD_CRI_VAR("수치", -3)
		CALL ADD_CRI_VAR("봉사",  2, 3)
		CALL ADD_CRI_VAR("가학",   , 2)
		CALL ADD_CRI_VAR("이상",   , 2)
		CALL ADD_CRI_VAR("성봉", TALENT:충동적 ? 7 # 4, 5)
	;분노
	CASE 6
		;TALENT:충동적 = 충동적　TALENT:자제심 = 자제심
		CALL ADD_CRI_VAR("가학", , 7 + TALENT:충동적 * 5 - TALENT:자제심 * 3)
	;싫증
	CASE 7
		CALL ADD_CRI_VAR("회화", -5)
		CALL ADD_CRI_VAR("이상",   , 5)
		CALL ADD_CRI_VAR("사역",   , 5)
	;울굴
	CASE 8
		CALL ADD_CRI_VAR("애무", ,  5)
		CALL ADD_CRI_VAR("도구", ,  5)
		CALL ADD_CRI_VAR("성교", ,  5)
		CALL ADD_CRI_VAR("수치", ,  5)
		CALL ADD_CRI_VAR("봉사", ,  5)
		CALL ADD_CRI_VAR("가학", ,  8)
		CALL ADD_CRI_VAR("이상", , 11)
		CALL ADD_CRI_VAR("사역", ,  8)
		CALL ADD_CRI_VAR("성봉", ,  5)
ENDSELECT

;-------------------------------------
;죠교대상の상태
;욕정している죠교대상をみて죠교자がどう思うか
;つまり素質の参照先は죠교자
;-------------------------------------
@ACT_MENU_CONDITION_M, ARG = -1
#DIM DYNAMIC TGT
TGT = ARG == -1 ? TARGET # ARG

;죠교대상の상태が정욕
IF IS_CONDITION(TARGET, "정욕")
	;죠교자が반항적/자제심/쾌감을부정/새드/심술궂음のうち２つ以上所持
	IF TRUECHECK(TALENT:TGT:반항적, TALENT:TGT:자제심, TALENT:TGT:쾌감을부정, TALENT:TGT:새드, TALENT:TGT:심술궂음) > 1
		CALL ADD_CRI_VAR("수치", 2)
		CALL ADD_CRI_VAR("봉사", 1)
		CALL ADD_CRI_VAR("성봉", 4)
	ELSE
		CALL ADD_CRI_VAR("성교", 3)
		CALL ADD_CRI_VAR("성봉", 4)
	ENDIF
ENDIF

;-------------------------------------
;죠교대상の疲労
;TFLAG:소모도 = 疲労度
;성봉がないのは違和感
;-------------------------------------
@ACT_MENU_TIRED_M

;죠교대상の소모플래그(疲れ)
IF TFLAG:소모도
	CALL ADD_CRI_VAR("성교", -TFLAG:소모도 / 2)
	CALL ADD_CRI_VAR("봉사", -TFLAG:소모도 / 2)
	CALL ADD_CRI_VAR("가학", -TFLAG:소모도)
	CALL ADD_CRI_VAR("이상", -TFLAG:소모도 / 2)
	CALL ADD_CRI_VAR("사역", -TFLAG:소모도 / 2)
ENDIF

;-------------------------------------
;初손회화보정
;-------------------------------------
@ACT_MENU_FIRST

;初손で高い確率회화
SIF !TFLAG:경과시간
	CALL ADD_CRI_VAR("회화", 10)

;-------------------------------------
;거울ボーナス
;-------------------------------------
@ACT_MENU_MILLER
SIF ITEM:거울
	CALL ADD_CRI_VAR("수치", , 3)

;──────────────────────────────
;状況や전회의コマンドによる変動 ;상황과 전회의 명령에 의한 변동
;直前の죠교대상の反応 ;이전 죠교 대상의 반응
;-------------------------------------
;더 하드하게 해
;-------------------------------------
@ACT_MENU_PLEASE_HARD

IF IS_NOWCOMNAME("더 하드하게 해")
	CALL ADD_CRI_VAR("애무", -2)
	CALL ADD_CRI_VAR("성교", -1)
	CALL ADD_CRI_VAR("봉사", -1)
	CALL ADD_CRI_VAR("가학",  4)
	CALL ADD_CRI_VAR("이상",  2)
ENDIF

;-------------------------------------
;용서를 빌다
;새드OR심술궂음
;-------------------------------------
@ACT_MENU_PLEASE_HELP

IF IS_NOWCOMNAME("용서를 빌다")
	;죠교대상が울굴以外
	IF TCVAR:상태변화 < 8
		CALL ADD_CRI_VAR("가학", -5, (TALENT:새드 || TALENT:심술궂음) ? 4 # 0)
		CALL ADD_CRI_VAR("이상", -5, (TALENT:새드 || TALENT:심술궂음) ? 4 # 0)
	ENDIF
ENDIF

;-------------------------------------
;気持よくして
;-------------------------------------
@ACT_MENU_PLEASE_PREASURE

IF IS_NOWCOMNAME("기분좋게 해")
	CALL ADD_CRI_VAR("애무", 2)
	CALL ADD_CRI_VAR("도구", 3)
	CALL ADD_CRI_VAR("성교", 2)
	CALL ADD_CRI_VAR("성봉", 3)
ENDIF

;-------------------------------------
;날뛴다
;-------------------------------------
@ACT_MENU_VIOLENT

IF IS_NOWCOMNAME("날뛴다")
	CALL ADD_CRI_VAR("애무", -2)
	CALL ADD_CRI_VAR("성교", -5)
	CALL ADD_CRI_VAR("가학",  5)
	CALL ADD_CRI_VAR("사역",  5)
	CALL ADD_CRI_VAR("성봉", -6)
ENDIF

;-------------------------------------
;도구解除
;-------------------------------------
@ACT_MENU_UNEQUIP

IF IS_NOWCOMNAME("도구를 제거한다")
	CALL ADD_CRI_VAR("가학", 10)
ENDIF

;-------------------------------------
;밀어넘어짐時の연속애무보정
;-------------------------------------
@ACT_MENU_PETTING_CONTINUE

;밀어넘어지는중で、前のターンのメニューが애무
IF TEQUIP:밀어넘어지는중 && TFLAG:전ACT분류 == 1
	CALL ADD_CRI_VAR("애무", 20)
ENDIF

;-------------------------------------
;シックスナイン誘発
;-------------------------------------
@ACT_MENU_69

;シックスナインになる경향(TFLAG:전ACT분류 = 前のターンの죠교メニュー)
SELECTCASE TFLAG:전ACT분류
	CASE 1
		CALL ADD_CRI_VAR("봉사", 1, 2)
	CASE 5
		CALL ADD_CRI_VAR("애무", 1, 2)
ENDSELECT

;--------------------------------------
;죠교자の服装
;-------------------------------------
@ACT_MENU_CLOTH_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;服装
;LOCAL:3（성교）、LOCAL:10（성교봉사）に영향
;죠교자が하반신겉옷を着ている
IF TEQUIP:LOCAL:하반신겉옷
	CALL ADD_CRI_VAR("성교", -2)
	CALL ADD_CRI_VAR("성봉", -2)
ENDIF
;죠교자が상반신겉옷を着ている
IF TEQUIP:LOCAL:상반신겉옷
	CALL ADD_CRI_VAR("성교", -1)
	CALL ADD_CRI_VAR("성봉", -1)
ENDIF
;죠교자が전신겉옷を着ている
IF TEQUIP:LOCAL:전신겉옷
	CALL ADD_CRI_VAR("성교", -3)
	CALL ADD_CRI_VAR("성봉", -3)
ENDIF

;--------------------------------------
;죠교대상の服装
;대상の服装は2箇所で定義されてた
;そのうち一箇所をコメントアウト
;-------------------------------------
@ACT_MENU_CLOTH_M, ARG = -1, ARG:1 = -1
#LOCALSIZE 2
LOCAL = ARG   == -1 ? TARGET # ARG
LOCAL = ARG:1 == -1 ? MASTER # ARG:1

;;죠교대상が하반신겉옷を着ている
;IF TEQUIP:(LOCAL:1):하반신겉옷
;	;죠교자に벽통과がない
;	SIF !TALENT:LOCAL:벽통과
;		CALL ADD_CRI_VAR("성교", -2)
;	CALL ADD_CRI_VAR("성봉", -2)
;ENDIF
;죠교대상が상반신겉옷を着ている
;IF TEQUIP:(LOCAL:1):상반신겉옷
;	;죠교자に벽통과がない
;	SIF !TALENT:LOCAL:벽통과
;		CALL ADD_CRI_VAR("성교", -1)
;	CALL ADD_CRI_VAR("성봉", -1)
;ENDIF
;죠교대상が전신겉옷を着ている
;IF TEQUIP:(LOCAL:1):전신겉옷
;	;죠교자に벽통과がない
;	SIF !TALENT:LOCAL:벽통과
;		CALL ADD_CRI_VAR("성교", -3)
;	CALL ADD_CRI_VAR("성봉", -3)
;ENDIF
;겉옷
;죠교대상の하반신겉옷で妨害されるとき
IF CLOTHESTYPE(1, 1) == 2
	CALL ADD_CRI_VAR("회화", 1, 4)
	;죠교자に벽통과がない
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("애무", , -2)
	CALL ADD_CRI_VAR("도구", -2, -3)
	;죠교자に벽통과がない
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("성교", -1, -5)
	CALL ADD_CRI_VAR("수치", , -7)
	CALL ADD_CRI_VAR("성봉", -1, -5)
;죠교대상の하반신겉옷があるとき
ELSEIF CLOTHESTYPE(1, 1)
	CALL ADD_CRI_VAR("회화", , 3)
	;죠교자に벽통과がない
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("애무", , -1)
	CALL ADD_CRI_VAR("도구", -1, -2)
	;죠교자に벽통과がない
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("성교", , -1)
	CALL ADD_CRI_VAR("성봉", , -1)
ENDIF
;죠교대상の상반신겉옷があるとき
IF CLOTHESTYPE(1, 3)
	CALL ADD_CRI_VAR("회화", , 2)
	;죠교자に벽통과がない
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("애무", , -2)
	CALL ADD_CRI_VAR("도구", , -2)
	CALL ADD_CRI_VAR("수치", , -4)
ENDIF

;속옷
;죠교대상の하반신속옷があるとき
IF CLOTHESTYPE(1, 0)
	CALL ADD_CRI_VAR("회화", , 1)
	;죠교자に벽통과がない
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("애무", , -1)
	CALL ADD_CRI_VAR("도구", -1, -1)
	;죠교자に벽통과がない
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("성교", , -4)
	CALL ADD_CRI_VAR("성봉", , -4)
ENDIF
;죠교대상の상반신속옷があるとき
IF CLOTHESTYPE(1, 2)
	CALL ADD_CRI_VAR("회화", , 2)
	;죠교자に벽통과がない
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("애무", , -2)
	CALL ADD_CRI_VAR("도구", , -4)
	CALL ADD_CRI_VAR("수치", , -2)
ENDIF

;----------------------------------------------------------
;TEQUIP関連
;---------------------------------------------------------
;-------------------------------------
;미약
;-------------------------------------
@ACT_MENU_DRAG

IF TEQUIP:미약
	CALL ADD_CRI_VAR("회화", , 4)
	CALL ADD_CRI_VAR("애무", TEQUIP:미약 / 2)
	CALL ADD_CRI_VAR("도구", TEQUIP:미약 / 2)
	CALL ADD_CRI_VAR("성교", TEQUIP:미약 / 2)
	CALL ADD_CRI_VAR("수치", TEQUIP:미약 / 3)
	CALL ADD_CRI_VAR("성봉", TEQUIP:미약 / 2)
ENDIF

;-------------------------------------
;바이브
;조수のA감각が高いと、성교했다がる？
;-------------------------------------
@ACT_MENU_VIBE

;바이브
IF TEQUIP:바이브
	CALL ADD_CRI_VAR("애무", ,  3)
	CALL ADD_CRI_VAR("도구", ,  6)
	CALL ADD_CRI_VAR("성교", , -9)
	SIF ASSI >= 0
		CALL ADD_CRI_VAR("성교", GET_ABL(ASSI, "Ａ감각"))
	CALL ADD_CRI_VAR("수치", 7)
	CALL ADD_CRI_VAR("가학", , 2)
ENDIF

;-------------------------------------
;A바이브
;-------------------------------------
@ACT_MENU_AVIBE

;항문바이브
IF TEQUIP:항문바이브
	CALL ADD_CRI_VAR("애무", , 3)
	CALL ADD_CRI_VAR("도구", , 6)
	;바이브がない場合섹스했다がる
	CALL ADD_CRI_VAR("성교", TEQUIP:바이브 ? 0 # 8)
	CALL ADD_CRI_VAR("수치", 8)
	CALL ADD_CRI_VAR("가학", , 5)
	CALL ADD_CRI_VAR("이상", , 4)
ENDIF

;-------------------------------------
;항문비즈
;-------------------------------------
@ACT_MENU_ABEADS

;항문비즈
IF TEQUIP:항문비즈
	CALL ADD_CRI_VAR("애무", , 5)
	CALL ADD_CRI_VAR("도구", , 8)
	CALL ADD_CRI_VAR("성교", TEQUIP:바이브 ? 0 # 2)
	CALL ADD_CRI_VAR("수치", 5)
	CALL ADD_CRI_VAR("가학", , 3)
	CALL ADD_CRI_VAR("이상", , 1)
ENDIF

;-------------------------------------
;항문プラグ
;-------------------------------------
@ACT_MENU_APLUG

;관장＋항문プラグ
IF TEQUIP:관장그릇＋플러그
	CALL ADD_CRI_VAR("회화", , 5)
	CALL ADD_CRI_VAR("애무", , 3)
	CALL ADD_CRI_VAR("도구", , 3)
	CALL ADD_CRI_VAR("성교", , TEQUIP:바이브 ? 0 # 1)
	CALL ADD_CRI_VAR("수치", 8)
	CALL ADD_CRI_VAR("가학", , 7)
	CALL ADD_CRI_VAR("이상", , 7)
ENDIF

;-------------------------------------
;클리캡
;-------------------------------------
@ACT_MENU_CRICAP

;클리캡
IF TEQUIP:클리캡
	CALL ADD_CRI_VAR("애무", , 2)
	CALL ADD_CRI_VAR("도구", , 2)
	CALL ADD_CRI_VAR("수치", 4)
ENDIF

;-------------------------------------
;오나홀
;-------------------------------------
@ACT_MENU_ONAHOLE

IF TEQUIP:오나홀
	CALL ADD_CRI_VAR("애무", , 2)
	CALL ADD_CRI_VAR("도구", , 2)
	CALL ADD_CRI_VAR("수치", 4)
	CALL ADD_CRI_VAR("이상", 1)
ENDIF

;-------------------------------------
;유두캡
;-------------------------------------
@ACT_MENU_NIPPLECAP

IF TEQUIP:유두캡
	CALL ADD_CRI_VAR("애무", , 2)
	CALL ADD_CRI_VAR("도구", , 2)
	CALL ADD_CRI_VAR("수치", 4 + TALENT:MASTER:남자)
	CALL ADD_CRI_VAR("이상", , TALENT:MASTER:남자 ? 5 # 0)
ENDIF

;-------------------------------------
;밧줄
;-------------------------------------
@ACT_MENU_ROPE

IF TEQUIP:밧줄
	CALL ADD_CRI_VAR("회화",  3)
	CALL ADD_CRI_VAR("성교", , -1)
	CALL ADD_CRI_VAR("봉사", -7)
	CALL ADD_CRI_VAR("가학",  3)
	CALL ADD_CRI_VAR("이상",  , 2)
	CALL ADD_CRI_VAR("성봉", -3)
ENDIF

;-------------------------------------
;눈가리개
;-------------------------------------
@ACT_MENU_EYEMASK

IF TEQUIP:눈가리개
	CALL ADD_CRI_VAR("회화",   , 4)
	CALL ADD_CRI_VAR("수치",  3, 4)
	CALL ADD_CRI_VAR("봉사", -5)
	CALL ADD_CRI_VAR("가학",  1)
	CALL ADD_CRI_VAR("이상",   , 3)
ENDIF

;-------------------------------------
;볼개그
;-------------------------------------
@ACT_MENU_GAG

IF TEQUIP:볼개그
	CALL ADD_CRI_VAR("회화", -10)
	CALL ADD_CRI_VAR("봉사",  -9)
	CALL ADD_CRI_VAR("가학",   2)
	CALL ADD_CRI_VAR("이상", , 4)
ENDIF

;-------------------------------------
;삼각목마
;-------------------------------------
@ACT_MENU_HORSE

IF TEQUIP:삼각목마
	CALL ADD_CRI_VAR("회화",   , 6)
	CALL ADD_CRI_VAR("애무", -6)
	CALL ADD_CRI_VAR("도구", -6)
	CALL ADD_CRI_VAR("수치", -3)
	CALL ADD_CRI_VAR("가학",  7, 2)
	CALL ADD_CRI_VAR("이상",  3, 7)
ENDIF

;-------------------------------------
;야외플레이
;-------------------------------------
@ACT_MENU_OUT

SIF TEQUIP:야외플레이
	CALL ADD_CRI_VAR("수치", , 6)

;-------------------------------------
;거울
;-------------------------------------
@ACT_MENU_T_MILLER

SIF TEQUIP:거울
	CALL ADD_CRI_VAR("수치", , 8)

;-------------------------------------
;도구解除制限
;체력があるのにフルアーマーしてるなら도구メニューを選ばないように(도구解除になるから)
;ちょっと処理を変えてみるテスト。拙かったらごめんなさい。
;・남자ならＶ系無しでもフルアーマーとみなすように
;-------------------------------------
@ACT_MENU_KILL_UNEQUIP, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? MASTER # ARG

IF (BASE:LOCAL:체력 + BASE:LOCAL:기력) > 500
	SIF (TALENT:LOCAL:남자 || TEQUIP:바이브 || TEQUIP:페니스밴드) && TEQUIP:항문바이브 && (TEQUIP:클리캡 || TEQUIP:오나홀) && TEQUIP:유두캡
		CALL ADD_CRI_VAR("도구", -15)
ENDIF

;-------------------------------------
;죠교자자위중
;-------------------------------------
@ACT_MENU_MASTURBATION_T

;죠교자자위중
IF GETBIT(TEQUIP:자위중, 1)
	CALL ADD_CRI_VAR("회화",  , 4)
	CALL ADD_CRI_VAR("도구",  , 4)
	CALL ADD_CRI_VAR("성교",  , 4)
	CALL ADD_CRI_VAR("수치", 2, 4)
	CALL ADD_CRI_VAR("봉사", 2, 4)
	CALL ADD_CRI_VAR("성봉",  , 4)
ENDIF

;-------------------------------------
;죠교대상자위중
;-------------------------------------
@ACT_MENU_MASTURBATION_M

;자위중
IF GETBIT(TEQUIP:자위중, 0)
	CALL ADD_CRI_VAR("회화", 2, 4)
	CALL ADD_CRI_VAR("도구", 2, 4)
	CALL ADD_CRI_VAR("성교", ,  4)
	CALL ADD_CRI_VAR("수치", 2, 4)
	CALL ADD_CRI_VAR("봉사",  , 4)
	CALL ADD_CRI_VAR("성봉",  -10)
ENDIF

;-------------------------------------
;징계・中
;-------------------------------------
@ACT_MENU_OSHIOKI

;징계・플래그
IF TFLAG:징계・플래그
	CALL ADD_CRI_VAR("가학", , 4)
	CALL ADD_CRI_VAR("이상", , 2)
	CALL ADD_CRI_VAR("사역", , 1)
	CALL ADD_CRI_VAR("봉사", -999)
ENDIF

;──────────────────────────────
;実行不可能の判定
;──────────────────────────────
;-------------------------------------
;삽입
;とりあえず、現行の삽입続行仕様(pa0903版時点)にあわせる(COMABLEで続行しないコマンドを蹴りだす)
;というかこの仕様がある場合、下손に許可されないもんに落とすとコマンド全滅でエラーになるはず
;
;삽입続行制限可で実行可能なコマンドがないものはこの時点で蹴る
;보정は성교(성봉) > 애무 > 회화・가학
;분노보정はこの仕様だと解除に向かいやすくなるわけでもないので置いとく
;怒時で삽입解除とかそういう別이벤트を挟んでやったほうがよさげ？
;ペニバン時はコマンド禁止による措置が動かないので、追加で続行させやすくするために보정を与える
;-------------------------------------
@ACT_MENU_INSERT

;성교/성교봉사중
IF TEQUIP:성교중 || TEQUIP:성교봉사중
	;페니스밴드삽입
	IF TEQUIP:성교중 && TEQUIP:죠교자페니스밴드
		CALL ADD_CRI_VAR("성교", 5)
	;페니스밴드삽입以外
	ELSE
		CALL ADD_CRI_VAR("도구", -999)
		CALL ADD_CRI_VAR("수치", -999)
		CALL ADD_CRI_VAR("봉사", -999)
		CALL ADD_CRI_VAR("가학", , 2)
		CALL ADD_CRI_VAR("이상", -999)
		CALL ADD_CRI_VAR("사역", -999)
	ENDIF
	CALL ADD_CRI_VAR("회화", , 2)
	CALL ADD_CRI_VAR("애무", , 4)
	;성교중
	IF TEQUIP:성교중
		CALL ADD_CRI_VAR("성교", 5, 5)
		SIF !TEQUIP:죠교자페니스밴드
			CALL ADD_CRI_VAR("성봉", -999)
	;성교봉사중
	ELSE
		CALL ADD_CRI_VAR("성봉", 5, 5)
		CALL ADD_CRI_VAR("성교", -999)
	ENDIF
ENDIF

;----------------------------------------------------------
;애태움中、Ｃ刺激を中断しないように
;----------------------------------------------------------
@ACT_MENU_IRRITATE

;애태움中
IF TCVAR:MASTER:애태움도
	;애태움終了の瞬間はそれに集中。もしくは성교봉사중ではない場合
	;(성교봉사に関しては삽입보정の内容で問題ないんじゃないかな)
	IF TCVAR:MASTER:애태움도 == 1 || !TEQUIP:성교봉사중
		;今後再考の필요有りか
		CALL ADD_CRI_VAR("회화", -999)
		CALL ADD_CRI_VAR("도구", -999)
		CALL ADD_CRI_VAR("성교", -999)
		CALL ADD_CRI_VAR("수치", -999)
		CALL ADD_CRI_VAR("봉사", -999)
		CALL ADD_CRI_VAR("가학", -999)
		CALL ADD_CRI_VAR("이상", -999)
		CALL ADD_CRI_VAR("사역", -999)
	ENDIF
ENDIF

;----------------------------------------------------------
;A섹스回避
;----------------------------------------------------------
@ACT_MENU_AGUARD

;죠교방침が이상の場合、50%の確率で判定を行う
IF IS_POLICY("今", "이상") && RAND:2
	;ＡＳ可だが、慣れてないのでガードする。ただし50%(?!)
	SIF EXP:MASTER:Ａ경험 < 2 && !TEQUIP:항문바이브 && !TEQUIP:항문비즈 && !TEQUIP:관장그릇＋플러그
		CALL ADD_CRI_VAR("성교", -999)
ENDIF

;----------------------------------------------------------
;항문섹스中、소프트방침で他の体位に変更しないように
;----------------------------------------------------------
@ACT_MENU_ASEX_LIMITATION

IF IS_POLICY("今", "소프트")
	SIF TEQUIP:성교중 == 6
		CALL ADD_CRI_VAR("성교", -999)
	SIF TEQUIP:성교봉사중 == 6
		CALL ADD_CRI_VAR("성봉", -999)
ENDIF

;----------------------------------------------------------
;実行判定
;----------------------------------------------------------
@ACT_MENU_ABLE

;不可能判定
FOR LOCAL, 0, 11
	CALL ACTM_ABLE(LOCAL)
	SIF !RESULT
		CALL DIM_CRI_VAR(MENU(LOCAL), -999)
NEXT

;-------------------------------------
;ヒート値
;-------------------------------------
@ACT_MENU_HEAT
#LOCALSIZE 1
#DIM LCOUNT

FOR LCOUNT, 0, 11
	SIF LCOUNT == 9
		CONTINUE
	LOCAL = TFLAG:(@"%MENU(LCOUNT)%히트") * LIMIT(TFLAG:가능한ACT계열수 - 1, 0, 4) / 4
	CALL ADD_CRI_VAR(MENU(LCOUNT), -LOCAL)
NEXT

;-------------------------------------
;ACT보정値
;-------------------------------------
@ACT_MENU_CORRECTION
#DIM LCOUNT

FOR LCOUNT, 0, 11
	;보정値を加算
	CALL ACT_M_RES_MENU(LCOUNT)
	CALL ADD_CRI_VAR(MENU(LCOUNT), RESULT)
NEXT
