;────────────────────────────────────
;가학 (平손60/むち61/はり62/なわ63/目隠64/입枷65/매도66/木馬67/관장68/イラ56)
;────────────────────────────────────
@ACT_M6(ARG = 0)
;1以上なら判定を行わない
#DIM LCOUNT, 1

;関数初期化
CALL ACT_M_INIT(6)

;処理開始

LOCALS = ACT_ACTM6

CALL CHA_CRI_VAR, "基準値"
CALLFORM %LOCALS%_REFERENCE

CALL CHA_CRI_VAR, @"今回の방침(%POLICY("今")%)"
CALLFORM %LOCALS%_COURCE

CALL CHA_CRI_VAR, "죠교자の素質"
CALLFORM %LOCALS%_TALENT_T

CALL CHA_CRI_VAR, "죠교대상の素質"
CALLFORM %LOCALS%_TALENT_M

CALL CHA_CRI_VAR, "죠교자の能力"
CALLFORM %LOCALS%_ABL_T

CALL CHA_CRI_VAR, "죠교대상の能力"
CALLFORM %LOCALS%_ABL_M

CALL CHA_CRI_VAR, "죠교대상の반항"
CALLFORM %LOCALS%_RESISTANCE_M

CALL CHA_CRI_VAR, "죠교자の죄책감"
CALLFORM %LOCALS%_SIN_T

CALL CHA_CRI_VAR, "죠교자のALI"
CALLFORM %LOCALS%_ALI_T

CALL CHA_CRI_VAR, "죠교대상の체력"
CALLFORM %LOCALS%_PHYSICAL_M

CALL CHA_CRI_VAR, "죠교대상の기력"
CALLFORM %LOCALS%_MENTAL_M

CALL CHA_CRI_VAR, "죠교대상の소모"
CALLFORM %LOCALS%_EXHAUSTION_M

CALL CHA_CRI_VAR, "죠교자の체력"
CALLFORM %LOCALS%_PHYSICAL_T

CALL CHA_CRI_VAR, "죠교자の기력"
CALLFORM %LOCALS%_MENTAL_T

CALL CHA_CRI_VAR, "죠교자の초조함"
CALLFORM %LOCALS%_IRRITATION_T

CALL CHA_CRI_VAR, @"죠교자の상태(%CONDITION(TCVAR:상태변화)%)"
CALLFORM %LOCALS%_CONDITION_T

CALL CHA_CRI_VAR, "기출의화제"
CALLFORM %LOCALS%_ALREADY_TALKED

CALL CHA_CRI_VAR, "날뛴다"
CALLFORM %LOCALS%_VIOLENT

CALL CHA_CRI_VAR, "도구をはず당했다"
CALLFORM %LOCALS%_BE_TAKEN_OFF_TOOLS

CALL CHA_CRI_VAR, "도구をはず했다"
CALLFORM %LOCALS%_TAKE_OFF_TOOLS

CALL CHA_CRI_VAR, "죠교대상자위중"
CALLFORM %LOCALS%_MASTURBATION_M

CALL CHA_CRI_VAR, "죠교자자위중"
CALLFORM %LOCALS%_MASTURBATION_T

CALL CHA_CRI_VAR, "삽입보정"
CALLFORM %LOCALS%_INSERT

CALL CHA_CRI_VAR, "징계・"
CALLFORM %LOCALS%_OSHIOKI

CALL CHA_CRI_VAR, "죠교대상の衣服"
CALLFORM %LOCALS%_CLOTH_M

CALL CHA_CRI_VAR, "연속행동보정"
CALLFORM %LOCALS%_PREV

CALL CHA_CRI_VAR, "구상보정値"
CALL ACT_ACTM_KOJO_CORRECTION_T, 6

CALL CHA_CRI_VAR, "ACT보정値"
CALLFORM %LOCALS%_CORRECTION(ARG)

;ARGが1以上なら処理中断
SIF ARG
	RETURN RESULT

CALL CHA_CRI_VAR, "実行判定"
CALLFORM %LOCALS%_ABLE

;────────────────────────────────────
;最終判定
;────────────────────────────────────
CALL DEF_CRI_VAR("最大前者")
LOCAL = GET_CRI_VAR("決定")
IF LOCAL == -1
	SIF FLAG:디버그
;OriginalString : (가학ACT분류に実行可能なACTが存在しません)
		PRINTL (가학ACT분류에 실행 가능한ACT하지만 존재하지 않습니다)
	TFLAG:ACT = -1
	CALL ACT_MENU_RESULT(2, "가학")
ELSE
	TFLAG:ACT = GET_ACTNUM(RESULTS)
ENDIF
CALL ANA_CRI_VAR()

;-------------------------------------------------------------------------
;基準値
;-------------------------------------------------------------------------
@ACT_ACTM6_REFERENCE

;要らないかもと思ったので試しに無効化@これみ
[SKIPSTART]
CALL ADD_CRI_VAR("스펭킹", 15)
CALL ADD_CRI_VAR("채찍", 12)
CALL ADD_CRI_VAR("바늘", 6)
CALL ADD_CRI_VAR("밧줄", 8)
CALL ADD_CRI_VAR("눈가리개", 8)
CALL ADD_CRI_VAR("볼개그", 8)
CALL ADD_CRI_VAR("매도", 5)
CALL ADD_CRI_VAR("삼각목마", 5)
CALL ADD_CRI_VAR("관장세트", 5)
CALL ADD_CRI_VAR("이라마치오", 5)
[SKIPEND]

;------------------------------------------
;今回の죠교방침
;------------------------------------------
@ACT_ACTM6_COURCE

;죠교방침(1=휴식/2=소프트/3=노멀/4=하드/5=이상)

;죠교방침が하드
IF IS_POLICY("今", "하드")
	CALL ADD_CRI_VAR("관장세트", 5)
	SIF TEQUIP:밧줄 == 0
		CALL ADD_CRI_VAR("밧줄", 7)
	SIF TEQUIP:눈가리개 == 0
		CALL ADD_CRI_VAR("눈가리개", 5)
	SIF TEQUIP:볼개그 == 0
		CALL ADD_CRI_VAR("볼개그", 5)
ENDIF

;죠교방침が이상
SIF IS_POLICY("今", "이상")
	CALL ADD_CRI_VAR("삼각목마", 5)

;-------------------------------------------------------------------------
;죠교자の素質を参照
;-------------------------------------------------------------------------
@ACT_ACTM6_TALENT_T

;죠교자が겁쟁이/굳셈
IF TALENT:겁쟁이
	CALL ADD_CRI_VAR("눈가리개", , TEQUIP:눈가리개 ? 0 # 4)
ELSEIF TALENT:굳셈
	CALL ADD_CRI_VAR("매도", , 4)
	CALL ADD_CRI_VAR("삼각목마", , TEQUIP:삼각목마 ? 0 # 5)
	CALL ADD_CRI_VAR("관장세트", , 5)
ENDIF

;죠교자が프라이드높음/低い
IF TALENT:프라이드높음
	CALL ADD_CRI_VAR("매도", 2, 2)
ELSEIF TALENT:프라이드낮음
	CALL ADD_CRI_VAR("매도", -2, -2)
ENDIF

;죠교자が일선을넘지않음で삼각목마未장착
SIF TALENT:일선을넘지않음 && !TEQUIP:삼각목마
	CALL ADD_CRI_VAR("삼각목마", -5)

;죠교자が튀고싶어함
SIF TALENT:튀고싶어함
	CALL ADD_CRI_VAR("이라마치오", , 6)

;죠교자が억압/해방
IF TALENT:억압
	CALL ADD_CRI_VAR("눈가리개", TEQUIP:눈가리개 ? 0 # 3)
	CALL ADD_CRI_VAR("관장세트", -5)
ELSEIF TALENT:해방
	CALL ADD_CRI_VAR("눈가리개", TEQUIP:눈가리개 ? 0 # -3)
	CALL ADD_CRI_VAR("관장세트", , 7)
ENDIF

;죠교자が수줍음で눈가리개未장착
SIF TALENT:수줍음 && !TEQUIP:눈가리개
	CALL ADD_CRI_VAR("눈가리개", 5)

;죠교자が고통에약함
SIF TALENT:고통에약함
	CALL ADD_CRI_VAR("스펭킹", -5)

;죠교자が바느질
SIF TALENT:바느질
	CALL ADD_CRI_VAR("바늘", 5)

;죠교자が긴박능숙で밧줄未장착
SIF TALENT:긴박능숙 && !TEQUIP:밧줄
	CALL ADD_CRI_VAR("밧줄", 5)

;죠교자が도구능숙
SIF TALENT:도구능숙
	CALL ADD_CRI_VAR("관장세트", 5)

;죠교자が냄새민감
SIF TALENT:냄새민감
	CALL ADD_CRI_VAR("관장세트", -8)

;죠교자が새드
IF TALENT:새드
	CALL ADD_CRI_VAR("채찍", , 2)
	CALL ADD_CRI_VAR("바늘", , 4)
	CALL ADD_CRI_VAR("밧줄", , TEQUIP:밧줄 ? 0 # 2)
	CALL ADD_CRI_VAR("눈가리개", , TEQUIP:눈가리개 ? 0 # 2)
	CALL ADD_CRI_VAR("볼개그", , TEQUIP:볼개그 ? 0 # 4)
	CALL ADD_CRI_VAR("매도", , 6)
	CALL ADD_CRI_VAR("삼각목마", , TEQUIP:삼각목마 ? 0 # 6)
	CALL ADD_CRI_VAR("관장세트", , 4)
ENDIF

;죠교자が신중함/민감
IF TALENT:신중함
	CALL ADD_CRI_VAR("밧줄", , TEQUIP:밧줄 ? 0 # 4)
	CALL ADD_CRI_VAR("삼각목마", TEQUIP:삼각목마 ? 0 # -1, TEQUIP:삼각목마 ? 0 # -4)
ELSEIF TALENT:민감
	CALL ADD_CRI_VAR("밧줄", , TEQUIP:밧줄 ? 0 # -4)
	CALL ADD_CRI_VAR("삼각목마", TEQUIP:삼각목마 ? 0 # 1, TEQUIP:삼각목마 ? 0 # 4)
ENDIF

;죠교자が심술궂음/마음씨착한
IF TALENT:심술궂음
	CALL ADD_CRI_VAR("눈가리개", , TEQUIP:눈가리개 ? 0 # 4)
	CALL ADD_CRI_VAR("볼개그", , TEQUIP:볼개그 ? 0 # 4)
	CALL ADD_CRI_VAR("매도", , 4)
ELSEIF TALENT:마음씨착한
	CALL ADD_CRI_VAR("눈가리개", , TEQUIP:눈가리개 ? 0 # -4)
	CALL ADD_CRI_VAR("볼개그", , TEQUIP:볼개그 ? 0 # -4)
	CALL ADD_CRI_VAR("매도", , -4)
	CALL ADD_CRI_VAR("삼각목마", TEQUIP:삼각목마 ? 10 # 0)
ENDIF

;죠교자が유치
SIF TALENT:유치
	CALL ADD_CRI_VAR("스펭킹", 1, 4)

;죠교자が광기
IF TALENT:광기
	CALL ADD_CRI_VAR("삼각목마", , TEQUIP:삼각목마 ? 0 # 12)
	CALL ADD_CRI_VAR("관장세트", , 10)
	CALL ADD_CRI_VAR("이라마치오", , 10)
ENDIF

;죠교자がＣ민감
SIF TALENT:Ｃ민감
	CALL ADD_CRI_VAR("이라마치오", 2, 4)

;-------------------------------------------------------------------------
;죠교대상の素質を参照
;-------------------------------------------------------------------------
@ACT_ACTM6_TALENT_M

;죠교대상が반항적/솔직
IF TALENT:MASTER:반항적
	CALL ADD_CRI_VAR("밧줄", TEQUIP:밧줄 ? 0 # 2, TEQUIP:밧줄 ? 0 # 4)
	CALL ADD_CRI_VAR("눈가리개", , TEQUIP:눈가리개 ? 0 # 8)
	CALL ADD_CRI_VAR("볼개그", , TEQUIP:볼개그 ? 0 # 8)
ELSEIF TALENT:MASTER:솔직
	CALL ADD_CRI_VAR("밧줄", TEQUIP:밧줄 ? 0 # -2)
	CALL ADD_CRI_VAR("눈가리개", , TEQUIP:눈가리개 ? 0 # -4)
	CALL ADD_CRI_VAR("볼개그", , TEQUIP:볼개그 ? 0 # -4)
ENDIF

;죠교대상が얌전한/건방짐
IF TALENT:MASTER:건방짐
	CALL ADD_CRI_VAR("밧줄", , TEQUIP:밧줄 ? 0 # 4)
	CALL ADD_CRI_VAR("눈가리개", , TEQUIP:눈가리개 ? 0 # 4)
	CALL ADD_CRI_VAR("볼개그", TEQUIP:볼개그 ? 0 # 2, TEQUIP:볼개그 ? 0 # 4)
ELSEIF TALENT:MASTER:얌전한
	CALL ADD_CRI_VAR("밧줄", , TEQUIP:밧줄 ? 0 # -4)
	CALL ADD_CRI_VAR("눈가리개", , TEQUIP:눈가리개 ? 0 # -4)
	CALL ADD_CRI_VAR("볼개그", TEQUIP:볼개그 ? 0 # -2)
ENDIF

;죠교대상が프라이드높음/低い
IF TALENT:MASTER:프라이드높음
	CALL ADD_CRI_VAR("볼개그", , TEQUIP:볼개그 ? 0 # 4)
ELSEIF TALENT:MASTER:프라이드낮음
	CALL ADD_CRI_VAR("볼개그", , TEQUIP:볼개그 ? 0 # -4)
ENDIF

;죠교대상が수줍음/수치없음
IF TALENT:MASTER:수줍음
	CALL ADD_CRI_VAR("매도", 2, 4)
ELSEIF TALENT:MASTER:수치없음
	CALL ADD_CRI_VAR("매도", -2, -4)
ENDIF

;죠교대상が고통에약함/強い
IF TALENT:MASTER:고통에약함
	CALL ADD_CRI_VAR("스펭킹", 2, 2)
	CALL ADD_CRI_VAR("채찍", 3, 3)
	CALL ADD_CRI_VAR("바늘", 2, 6)
ELSEIF TALENT:MASTER:고통에강함
	CALL ADD_CRI_VAR("스펭킹", -3)
	CALL ADD_CRI_VAR("채찍", -4)
	CALL ADD_CRI_VAR("바늘", -4)
ENDIF

;────────────────────────────────────
;能力、パラメーターによる変動
;────────────────────────────────────
;-------------------------------------------------------------------------
;죠교자の能力を参照
;-------------------------------------------------------------------------
@ACT_ACTM6_ABL_T

;죠교자の기교が[0→0, 3→3～5, 6→0]を加算(0～5)
CALL ADD_CRI_VAR("채찍", LINE_CALC(ABL:기교, "0→0,3→3,6→0,LIMIT/0～3"), LINE_CALC(ABL:기교, "0→0,3→2,6→0,LIMIT/0～2"))
;죠교자の기교が[0→0, 5→3～5]を加算(0～5)
CALL ADD_CRI_VAR("바늘", LINE_CALC(ABL:기교, "0→0,5→3,LIMIT/0～3"), LINE_CALC(ABL:기교, "0→0,5→2,LIMIT/0～2"))

;죠교자のＣ감각が[0→0, 5→3～5]を加算(0～5)
CALL ADD_CRI_VAR("이라마치오", LINE_CALC(ABL:Ｃ감각, "0→0,5→3,LIMIT/0～3"), LINE_CALC(ABL:Ｃ감각, "0→0,5→2,LIMIT/0～2"))

;죠교자の회화が[0→0, 5→3～5]を加算(0～5)
CALL ADD_CRI_VAR("매도", LINE_CALC(ABL:회화, "0→0,5→3,LIMIT/0～3"), LINE_CALC(ABL:회화, "0→0,5→2,LIMIT/0～2"))

;죠교자の도구が[0→0, 5→3～5]を加算(0～5)
CALL ADD_CRI_VAR("관장세트", LINE_CALC(ABL:도구, "0→0,5→3,LIMIT/0～3"), LINE_CALC(ABL:도구, "0→0,5→2,LIMIT/0～2"))

;죠교자の봉사が[0→0, 5→3～5]を加算(0～5)
CALL ADD_CRI_VAR("이라마치오", LINE_CALC(ABL:봉사, "0→0,5→3,LIMIT/0～3"), LINE_CALC(ABL:봉사, "0→0,5→2,LIMIT/0～2"))

;-------------------------------------------------------------------------
;죠교대상の能力を参照
;-------------------------------------------------------------------------
@ACT_ACTM6_ABL_M

;죠교대상の봉사정신が[0→0, 5→3～7]を加算(0～7)
CALL ADD_CRI_VAR("이라마치오", LINE_CALC(ABL:MASTER:봉사정신, "0→0,5→3,LIMIT/0～3"), LINE_CALC(ABL:MASTER:봉사정신, "0→0,5→4,LIMIT/0～4"))

;죠교대상の고통중독が[0→0, 3→2～6, 6→0]を加算(0～6)
CALL ADD_CRI_VAR("스펭킹", LINE_CALC(ABL:MASTER:고통중독, "0→0,3→2,6→0,LIMIT/0～2"), LINE_CALC(ABL:MASTER:고통중독, "0→0,3→4,6→0,LIMIT/0～4"))
;죠교대상の고통중독が[0→0, 4→2～7, 5→1～5]を加算(0～7)
CALL ADD_CRI_VAR("채찍", LINE_CALC(ABL:MASTER:고통중독, "0→0,4→2,5→1,LIMIT/0～2"), LINE_CALC(ABL:MASTER:고통중독, "0→0,4→5,5→4,LIMIT/0～5"))
;죠교대상の고통중독が[0→0, 5→3～8]を加算(0～8)
CALL ADD_CRI_VAR("바늘", LINE_CALC(ABL:MASTER:고통중독, "0→0,5→3,LIMIT/0～3"), LINE_CALC(ABL:MASTER:고통중독, "0→0,5→5,LIMIT/0～5"))
;※취소に보정は付かない
;죠교대상の고통중독が[0→0, 5→2～10]を加算(0～10)
SIF !TEQUIP:삼각목마
	CALL ADD_CRI_VAR("삼각목마", LINE_CALC(ABL:MASTER:고통중독, "0→0,5→2,LIMIT/0～2"), LINE_CALC(ABL:MASTER:고통중독, "0→0,5→8,LIMIT/0～8"))

;※취소に보정は付かない
;죠교대상の구속중독が[0→0, 5→5～9]を加算(0～9)
SIF !TEQUIP:밧줄
	CALL ADD_CRI_VAR("밧줄", LINE_CALC(ABL:MASTER:구속중독, "0→0,5→5,LIMIT/0～5"), LINE_CALC(ABL:MASTER:구속중독, "0→0,5→4,LIMIT/0～4"))
;죠교대상の구속중독が[0→0, 5→3～7]を加算(0～7)
SIF !TEQUIP:눈가리개
	CALL ADD_CRI_VAR("눈가리개", LINE_CALC(ABL:MASTER:구속중독, "0→0,5→3,LIMIT/0～3"), LINE_CALC(ABL:MASTER:구속중독, "0→0,5→4,LIMIT/0～4"))
;죠교대상の구속중독が[0→0, 5→2～10]を加算(0～10)
SIF !TEQUIP:볼개그
	CALL ADD_CRI_VAR("볼개그", LINE_CALC(ABL:MASTER:구속중독, "0→0,5→2,LIMIT/0～2"), LINE_CALC(ABL:MASTER:구속중독, "0→0,5→8,LIMIT/0～8"))

;-------------------------------------
;죠교대상の반항
;-------------------------------------
@ACT_ACTM6_RESISTANCE_M

;죠교대상の반항が[0→0, 7500→5～7]を加算(0～7)
SIF !TEQUIP:밧줄
	CALL ADD_CRI_VAR("밧줄", LINE_CALC(PALAM:MASTER:반항, "0→0,5→5,LIMIT/0～5"), LINE_CALC(PALAM:MASTER:반항, "0→0,5→2,LIMIT/0～2"))
;죠교대상の반항が[0→0, 7500→4～7]を加算(0～7)
SIF !TEQUIP:눈가리개
	CALL ADD_CRI_VAR("눈가리개", LINE_CALC(PALAM:MASTER:반항, "0→0,5→4,LIMIT/0～4"), LINE_CALC(PALAM:MASTER:반항, "0→0,5→3,LIMIT/0～3"))
;죠교대상の반항が[0→0, 7500→3～4]を加算(0～4)
SIF !TEQUIP:볼개그
	CALL ADD_CRI_VAR("볼개그", LINE_CALC(PALAM:MASTER:반항, "0→0,5→3,LIMIT/0～3"), LINE_CALC(PALAM:MASTER:반항, "0→0,5→1,LIMIT/0～1"))

;-------------------------------------
;죠교자の죄책감
;-------------------------------------
@ACT_ACTM6_SIN_T

;죠교자の죄책감が[80→0, 100→-1]を加算(-1～0)
CALL ADD_CRI_VAR("바늘", LINE_CALC(CFLAG:죄책감, "80→0,100→-1,LIMIT/-1～0"))
;죠교자の죄책감が[60→0, 100→-2]を加算(-2～0)
CALL ADD_CRI_VAR("볼개그", LINE_CALC(CFLAG:죄책감, "60→0,100→-2,LIMIT/-2～0"))
;죠교자の죄책감が[40→0, 100→-3]を加算(-3～0)
CALL ADD_CRI_VAR("관장세트", LINE_CALC(CFLAG:죄책감, "40→0,100→-3,LIMIT/-3～0"))
;죠교자の죄책감が[0→0, 100→-5]を加算(-5～0)
CALL ADD_CRI_VAR("삼각목마", LINE_CALC(CFLAG:죄책감, "0→0,100→-5,LIMIT/-5～0"))

;-------------------------------------
;죠교자のALI
;-------------------------------------
@ACT_ACTM6_ALI_T

;아라이멘도が[-50→-3, 50→3]を加算(-3～3)
CALL ADD_CRI_VAR("스펭킹", LINE_CALC(CFLAG:아라이멘도, "-50→-3,50→3,LIMIT/-3～3"))
;아라이멘도が[-50→3, 50→-3]を加算(-3～3)
CALL ADD_CRI_VAR("삼각목마", LINE_CALC(CFLAG:아라이멘도, "-50→3,50→-3,LIMIT/-3～3"))
;아라이멘도が[0→0, 50→2]を加算(0～2)
CALL ADD_CRI_VAR("매도", LINE_CALC(CFLAG:아라이멘도, "0→0,50→2,LIMIT/0～2"))
;아라이멘도が[-50→3, 0→0]を加算(0～3)
CALL ADD_CRI_VAR("관장세트", LINE_CALC(CFLAG:아라이멘도, "-50→3,0→0,LIMIT/0～3"))

;────────────────────────────────────
;ゲージや상태による変動
;────────────────────────────────────
;-------------------------------------
;죠교대상の체력
;-------------------------------------
@ACT_ACTM6_PHYSICAL_M

;죠교대상の체력が[0→-1, 300→0]を加算(-1～0)
CALL ADD_CRI_VAR("스펭킹", LINE_CALC(BASE:MASTER:체력, "0→-1,300→0,LIMIT/-1～0"))
;죠교대상の체력が[0→-3, 600→0]を加算(-3～0)
CALL ADD_CRI_VAR("채찍", LINE_CALC(BASE:MASTER:체력, "0→-3,600→0,LIMIT/-3～0"))
;죠교대상の체력が[0→-3, 900→0]を加算(-3～0)
CALL ADD_CRI_VAR("이라마치오", LINE_CALC(BASE:MASTER:체력, "0→-3,900→0,LIMIT/-3～0"))
;죠교대상の체력が[0→-4, 900→0]を加算(-4～0)
CALL ADD_CRI_VAR("바늘", LINE_CALC(BASE:MASTER:체력, "0→-4,900→0,LIMIT/-4～0"))
;※취소時は正の보정
;죠교대상の체력が[0→4/-4, 900→0]を加算(-4～4)
LOCAL = TEQUIP:삼각목마 ? 1 # -1
CALL ADD_CRI_VAR("삼각목마", LOCAL * LINE_CALC(BASE:MASTER:체력, "0→4,900→0,LIMIT/0～4"))
;죠교대상の체력が[0→0～8/-4, 900→0]を加算(-4～8)
IF TEQUIP:관장그릇＋플러그
	CALL ADD_CRI_VAR("관장세트", , LINE_CALC(BASE:MASTER:체력, "0→8,900→0,LIMIT/0～8"))
ELSE
	CALL ADD_CRI_VAR("관장세트", LINE_CALC(BASE:MASTER:체력, "0→-4,900→0,LIMIT/-4～0"))
ENDIF

;-------------------------------------
;죠교대상の기력
;-------------------------------------
@ACT_ACTM6_MENTAL_M

;※취소時は負の보정
;죠교대상の기력が[0→0, 800→0～4/-8]を加算(-8～4)
IF TEQUIP:밧줄
	CALL ADD_CRI_VAR("밧줄", LINE_CALC(BASE:MASTER:기력, "0→0,800→-8,LIMIT/-8～0"))
ELSE
	CALL ADD_CRI_VAR("밧줄", , LINE_CALC(BASE:MASTER:기력, "0→0,800→4,LIMIT/0～4"))
ENDIF
IF TEQUIP:눈가리개
	CALL ADD_CRI_VAR("눈가리개", LINE_CALC(BASE:MASTER:기력, "0→0,800→-8,LIMIT/-8～0"))
ELSE
	CALL ADD_CRI_VAR("눈가리개", , LINE_CALC(BASE:MASTER:기력, "0→0,800→4,LIMIT/0～4"))
ENDIF
IF TEQUIP:볼개그
	CALL ADD_CRI_VAR("볼개그", LINE_CALC(BASE:MASTER:기력, "0→0,800→-8,LIMIT/-8～0"))
ELSE
	CALL ADD_CRI_VAR("볼개그", , LINE_CALC(BASE:MASTER:기력, "0→0,800→4,LIMIT/0～4"))
ENDIF

;-------------------------------------
;죠교대상の소모
;-------------------------------------
@ACT_ACTM6_EXHAUSTION_M

;피폐で구속などを解く(죠교대상の소모)
IF TFLAG:소모도
	CALL ADD_CRI_VAR("밧줄", TEQUIP:밧줄 ? 4 + TFLAG:소모도 / 2 # -3 - TFLAG:소모도 / 4)
	CALL ADD_CRI_VAR("눈가리개", TEQUIP:눈가리개 ? 2 + TFLAG:소모도 / 3 # -1 - TFLAG:소모도 / 6)
	CALL ADD_CRI_VAR("볼개그", TEQUIP:볼개그 ? 3 + TFLAG:소모도 / 3 # -2 - TFLAG:소모도 / 5)
	CALL ADD_CRI_VAR("삼각목마", TEQUIP:삼각목마 ? 9 + TFLAG:소모도 # -9 - TFLAG:소모도)
ENDIF

;-------------------------------------
;죠교자の체력
;-------------------------------------
@ACT_ACTM6_PHYSICAL_T

;죠교자の체력が[0→-3, 1500→0]を加算(-3～0)
CALL ADD_CRI_VAR("스펭킹", LINE_CALC(BASE:체력, "0→-3,1500→0,LIMIT/-3～0"))
CALL ADD_CRI_VAR("채찍", LINE_CALC(BASE:체력, "0→-3,1500→0,LIMIT/-3～0"))

;-------------------------------------
;죠교자の기력
;-------------------------------------
@ACT_ACTM6_MENTAL_T

;죠교자の기력が[0→-1, 250→0]を加算(-1～0)
CALL ADD_CRI_VAR("스펭킹", LINE_CALC(BASE:기력, "0→-1,250→0,LIMIT/-1～0"))
CALL ADD_CRI_VAR("채찍", LINE_CALC(BASE:기력, "0→-1,250→0,LIMIT/-1～0"))
CALL ADD_CRI_VAR("바늘", LINE_CALC(BASE:기력, "0→-1,250→0,LIMIT/-1～0"))
CALL ADD_CRI_VAR("관장세트", LINE_CALC(BASE:기력, "0→-1,250→0,LIMIT/-1～0"))
;죠교자の기력が[0→-2, 500→0]を加算(-2～0)
CALL ADD_CRI_VAR("이라마치오", LINE_CALC(BASE:기력, "0→-2,500→0,LIMIT/-2～0"))
;죠교자の기력が[0→-3, 750→0]を加算(-3～0)
CALL ADD_CRI_VAR("매도", LINE_CALC(BASE:기력, "0→-3,750→0,LIMIT/-3～0"))

;-------------------------------------
;죠교자の초조함
;-------------------------------------
@ACT_ACTM6_IRRITATION_T

;죠교자の초조함が[0→0, 800→6]を加算(0～6)
CALL ADD_CRI_VAR("스펭킹", LINE_CALC(BASE:초조함, "0→0,800→6,LIMIT/0～6"))
;죠교자の초조함が[0→0, 800→10]を加算(0～10)
CALL ADD_CRI_VAR("채찍", LINE_CALC(BASE:초조함, "0→0,800→10,LIMIT/0～10"))
;죠교자の초조함が[0→0, 800→8]を加算(0～8)
CALL ADD_CRI_VAR("바늘", LINE_CALC(BASE:초조함, "0→0,800→8,LIMIT/0～8"))
;죠교자の초조함が[0→0, 800→-6]を加算(-6～0)
CALL ADD_CRI_VAR("매도", LINE_CALC(BASE:초조함, "0→0,800→-6,LIMIT/-6～0"))

;-------------------------------------
;죠교자の상태
;-------------------------------------
@ACT_ACTM6_CONDITION_T

;죠교자の상태(0=통상/1=피폐/2=쇠약/3=무기력/4=몽롱/5=정욕/6=분노/7=싫증/8=울굴)
SELECTCASE GET_CONDITION(TARGET)
	CASE "정욕"
		CALL ADD_CRI_VAR("이라마치오", , 4)
	CASE "분노"
		CALL ADD_CRI_VAR("매도", , 4)
	CASE "울굴"
		CALL ADD_CRI_VAR("삼각목마", 2, 8)
ENDSELECT


;────────────────────────────────────
;전회의행동や状況による変動
;────────────────────────────────────
;-------------------------------------
;そんな話はもう終わった플래그(1=호의,2=지식,4=慰め,8=脅す,16=합의,32=嗤う,64=매도)
;-------------------------------------
@ACT_ACTM6_ALREADY_TALKED

;매도の話は했다
SIF TFLAG:기출의화제 & 64
	CALL ADD_CRI_VAR("매도", LIMIT(ABL:회화 / 2 - 5, -5, 0))

;-------------------------------------
;날뛴다
;-------------------------------------
@ACT_ACTM6_VIOLENT

IF IS_NOWCOMNAME("날뛴다")
	CALL ADD_CRI_VAR("바늘", -2)
	CALL ADD_CRI_VAR("밧줄", TEQUIP:밧줄 ? 0 # 10)
	CALL ADD_CRI_VAR("눈가리개", TEQUIP:눈가리개 ? 0 # 6)
	CALL ADD_CRI_VAR("삼각목마", TEQUIP:볼개그 ? 0 # 3)
ENDIF

;-------------------------------------
;勝손に도구をはず당했다
;-------------------------------------
@ACT_ACTM6_BE_TAKEN_OFF_TOOLS

;勝손に도구をはず당했다＆밧줄未장착
SIF IS_NOWCOMNAME("도구를 제거한다") && !TEQUIP:밧줄
	CALL ADD_CRI_VAR("밧줄", 5)

;-------------------------------------
;要求されて도구をはず했다
;-------------------------------------
@ACT_ACTM6_TAKE_OFF_TOOLS

;취소に応じて장착を躊躇
SIF !TEQUIP:밧줄
	CALL ADD_CRI_VAR("밧줄", -CANCEL_TEQUIP_COUNT("구속") / 20)
SIF !TEQUIP:눈가리개
	CALL ADD_CRI_VAR("눈가리개", -CANCEL_TEQUIP_COUNT("구속") / 20)
SIF !TEQUIP:볼개그
	CALL ADD_CRI_VAR("볼개그", -CANCEL_TEQUIP_COUNT("구속") / 20)
SIF !TEQUIP:삼각목마
	CALL ADD_CRI_VAR("삼각목마", -CANCEL_TEQUIP_COUNT("삼각목마") / 20)

;※二人자위중なら両方の보정が有効となる
;-------------------------------------
;죠교대상자위중
;-------------------------------------
@ACT_ACTM6_MASTURBATION_M

SIF GETBIT(TEQUIP:자위중, 0)
	CALL ADD_CRI_VAR("매도", , 10)

;-------------------------------------
;죠교자자위중
;-------------------------------------
@ACT_ACTM6_MASTURBATION_T

SIF GETBIT(TEQUIP:자위중, 1)
	CALL ADD_CRI_VAR("매도", , 5)

;-------------------------------------
;삽입
;-------------------------------------
@ACT_ACTM6_INSERT

IF TEQUIP:성교중 || TEQUIP:성교봉사중
	;기승위/배면좌위/역강간/후배위시킨다
	SIF TEQUIP:성교중 == 3 || TEQUIP:성교중 == 5 || TEQUIP:성교봉사중 == 3 || TEQUIP:성교봉사중 == 2
		CALL ADD_CRI_VAR("스펭킹", -5)
	;후배위中は叩いて欲しい
	SIF TEQUIP:성교중 == 2
		CALL ADD_CRI_VAR("스펭킹", 5)
	CALL ADD_CRI_VAR("바늘", -5)
	CALL ADD_CRI_VAR("관장세트", -5)
	CALL ADD_CRI_VAR("이라마치오", -5)
ENDIF

;-------------------------------------
;징계・中
;-------------------------------------
@ACT_ACTM6_OSHIOKI

IF TFLAG:징계・플래그
	CALL ADD_CRI_VAR("삼각목마", , 9)
	CALL ADD_CRI_VAR("관장세트", 1, 6)
	CALL ADD_CRI_VAR("이라마치오", 3, 3)
ENDIF

;-------------------------------------
;죠교대상の衣服
;-------------------------------------
@ACT_ACTM6_CLOTH_M

;죠교대상の하반신겉옷に妨害されるとき
IF CLOTHESTYPE(1, 1) ==2
	CALL ADD_CRI_VAR("삼각목마", -5)
	CALL ADD_CRI_VAR("관장세트", -2)
ENDIF

;죠교대상の하반신속옷があるとき
SIF CLOTHESTYPE(1, 0)
	CALL ADD_CRI_VAR("삼각목마", -2)

;죠교대상の상반신겉옷があるとき
IF CLOTHESTYPE(1, 3)
	CALL ADD_CRI_VAR("채찍", -5)
	CALL ADD_CRI_VAR("바늘", -5)
ENDIF

;────────────────────────────────────
;同じ행동연속실행の確率をダウンします
;────────────────────────────────────
@ACT_ACTM6_PREV

SELECTCASE TFLAG:전ACT
	CASE 56, 60 TO 68
		CALL ADD_CRI_VAR(GET_ACTNAME(TFLAG:전ACT), -3 * TFLAG:동일행동보너스, -4 * TFLAG:동일행동보너스)
ENDSELECT

;────────────────────────────────────
;보정値計算
;────────────────────────────────────
@ACT_ACTM6_CORRECTION(ARG)
#DIM LCOUNT

;ARGが0以外なら보정値を計算し、以後の処理を行わない
IF ARG
	;56に関してはここで計算しない
	FOR LCOUNT, 60, 69
		CALL ACT_M_RES_CALC(LCOUNT, GET_CRI_VAR(GET_ACTNAME(LCOUNT) + "累計"))
	NEXT
ELSE
	;보정値を判定値に加算する
	CALL ACT_ACTM_CORRECTION(6)
ENDIF

;────────────────────────────────────
;その他変動や実行不可能の判定
;────────────────────────────────────
@ACT_ACTM6_ABLE
#DIM LCOUNT

;不可能判定
CALL ACT_ACTM_ABLE(6)

