;────────────────────────────────────
;이상 (Fist70/ＡFi71/両Fi72/방치73/公衆74/Ｐ装75/嗤う9/발で18/발舐55/木馬67)
;────────────────────────────────────
@ACT_M7(ARG = 0)
;1以上なら判定を行わない
#DIM LCOUNT, 1

;関数初期化
CALL ACT_M_INIT(7)

;処理開始

LOCALS = ACT_ACTM7

CALL CHA_CRI_VAR, "基準値"
CALLFORM %LOCALS%_REFERENCE

CALL CHA_CRI_VAR, "죠교자の素質"
CALLFORM %LOCALS%_TALENT_T

CALL CHA_CRI_VAR, "죠교대상の素質"
CALLFORM %LOCALS%_TALENT_M

CALL CHA_CRI_VAR, "죠교대상のフィスト適性"
CALLFORM %LOCALS%_FIST

CALL CHA_CRI_VAR, "죠교자の能力"
CALLFORM %LOCALS%_ABL_T

CALL CHA_CRI_VAR, "죠교대상の能力"
CALLFORM %LOCALS%_ABL_M

CALL CHA_CRI_VAR, "죠교자のALI"
CALLFORM %LOCALS%_ALI_T

CALL CHA_CRI_VAR, "죠교자の기력"
CALLFORM %LOCALS%_MENTAL_T

CALL CHA_CRI_VAR, "죠교자の이성"
CALLFORM %LOCALS%_REASON_T

CALL CHA_CRI_VAR, "죠교자の흥미"
CALLFORM %LOCALS%_INTEREST_T

CALL CHA_CRI_VAR, "죠교대상の체력"
CALLFORM %LOCALS%_PHYSICAL_M

CALL CHA_CRI_VAR, @"죠교자の상태(%CONDITION(TCVAR:상태변화)%)"
CALLFORM %LOCALS%_CONDITION_T

CALL CHA_CRI_VAR, "미약"
CALLFORM %LOCALS%_DRAG

CALL CHA_CRI_VAR, "밧줄"
CALLFORM %LOCALS%_ROPE

CALL CHA_CRI_VAR, "눈가리개"
CALLFORM %LOCALS%_EYEMASK

CALL CHA_CRI_VAR, "볼개그"
CALLFORM %LOCALS%_GAG

CALL CHA_CRI_VAR, "삼각목마"
CALLFORM %LOCALS%_HORSE

CALL CHA_CRI_VAR, "야외플레이"
CALLFORM %LOCALS%_OUT

CALL CHA_CRI_VAR, "도구をはず했다"
CALLFORM %LOCALS%_TAKE_OFF_TOOLS

CALL CHA_CRI_VAR, "죠교자자위중"
CALLFORM %LOCALS%_MASTURBATION_T

CALL CHA_CRI_VAR, "죠교대상の衣服"
CALLFORM %LOCALS%_CLOTH_M

CALL CHA_CRI_VAR, "더 하드하게 해"
CALLFORM %LOCALS%_PLEASE_HARD

CALL CHA_CRI_VAR, "용서를 빌다"
CALLFORM %LOCALS%_PLEASE_HELP

CALL CHA_CRI_VAR, "징계・"
CALLFORM %LOCALS%_OSHIOKI

CALL CHA_CRI_VAR, "연속행동보정"
CALLFORM %LOCALS%_PREV

CALL CHA_CRI_VAR, "구상보정値"
CALL ACT_ACTM_KOJO_CORRECTION_T, 7

CALL CHA_CRI_VAR, "ACT보정値"
CALLFORM %LOCALS%_CORRECTION(ARG)

;ARGが1以上なら処理中断
SIF ARG
	RETURN RESULT

CALL CHA_CRI_VAR, "実行判定"
CALLFORM %LOCALS%_ABLE

;────────────────────────────────────
;最終判定
;────────────────────────────────────
CALL DEF_CRI_VAR("最大前者")
LOCAL = GET_CRI_VAR("決定")
IF LOCAL == -1
	SIF FLAG:디버그
;OriginalString : (이상ACT분류に実行可能なACTが存在しません)
		PRINTL (이상ACT분류에 실행 가능한ACT하지만 존재하지 않습니다)
	TFLAG:ACT = -1
	CALL ACT_MENU_RESULT(2, "이상")
ELSE
	TFLAG:ACT = GET_ACTNUM(RESULTS)
ENDIF
CALL ANA_CRI_VAR()

;-------------------------------------------------------------------------
;基準値
;-------------------------------------------------------------------------
@ACT_ACTM7_REFERENCE

;要らないかもと思ったので試しに無効化@これみ
[SKIPSTART]
CALL ADD_CRI_VAR("피스트퍽", 10)
CALL ADD_CRI_VAR("애널피스트", 10)
CALL ADD_CRI_VAR("양쪽피스트", 10)
CALL ADD_CRI_VAR("방치플레이", 10)
CALL ADD_CRI_VAR("공중육변기플레이", 10)
CALL ADD_CRI_VAR("기분나쁘게 웃다", 8)
CALL ADD_CRI_VAR("풋잡하기", 5)
CALL ADD_CRI_VAR("발핥기강제", 5)
CALL ADD_CRI_VAR("삼각목마", 5)
[SKIPEND]

;-------------------------------------------------------------------------
;죠교자の素質を参照
;-------------------------------------------------------------------------
@ACT_ACTM7_TALENT_T

;죠교자が겁쟁이/굳셈
IF TALENT:겁쟁이
	CALL ADD_CRI_VAR("공중육변기플레이", -5)
ELSEIF TALENT:굳셈
	CALL ADD_CRI_VAR("공중육변기플레이", , 5)
ENDIF

;죠교자が얌전한/건방짐
IF TALENT:얌전한
	CALL ADD_CRI_VAR("피스트퍽", -5)
	CALL ADD_CRI_VAR("애널피스트", -6)
	CALL ADD_CRI_VAR("양쪽피스트", -7)
ELSEIF TALENT:건방짐
	CALL ADD_CRI_VAR("방치플레이", , 4)
	CALL ADD_CRI_VAR("기분나쁘게 웃다", , 6)
	CALL ADD_CRI_VAR("풋잡하기", 2, 2)
ENDIF

;죠교자が프라이드높음/低い
IF TALENT:프라이드높음
	CALL ADD_CRI_VAR("발핥기강제", , 5)
ELSEIF TALENT:프라이드낮음
	CALL ADD_CRI_VAR("발핥기강제", , -5)
ENDIF

;죠교자が자제심/충동적
IF TALENT:자제심
	CALL ADD_CRI_VAR("방치플레이", , 4)
	CALL ADD_CRI_VAR("공중육변기플레이", -5)
ELSEIF TALENT:충동적
	CALL ADD_CRI_VAR("공중육변기플레이", , 5)
	CALL ADD_CRI_VAR("삼각목마", 4)
ENDIF

;죠교자が무관심/호기심
IF TALENT:무관심
	CALL ADD_CRI_VAR("공중육변기플레이", 5)
ELSEIF TALENT:호기심
	CALL ADD_CRI_VAR("풋잡하기", , 6)
ENDIF

;죠교자が감정결여/感情豊富な
IF TALENT:감정결여
	CALL ADD_CRI_VAR("공중육변기플레이", 3)
ELSEIF TALENT:정서적
	CALL ADD_CRI_VAR("기분나쁘게 웃다", , 6)
ENDIF

;죠교자が튀고싶어함
IF TALENT:튀고싶어함
	CALL ADD_CRI_VAR("풋잡하기", 5)
	CALL ADD_CRI_VAR("발핥기강제", 3)
ENDIF

;죠교자が억압/해방
IF TALENT:억압
	CALL ADD_CRI_VAR("공중육변기플레이", -5)
	CALL ADD_CRI_VAR("기분나쁘게 웃다", -5)
	CALL ADD_CRI_VAR("풋잡하기", -5)
	CALL ADD_CRI_VAR("발핥기강제", -5)
ELSEIF TALENT:해방
	CALL ADD_CRI_VAR("공중육변기플레이", , 6)
	CALL ADD_CRI_VAR("풋잡하기", , 4)
ENDIF

;죠교자が수줍음/수치없음
IF TALENT:수줍음
	CALL ADD_CRI_VAR("공중육변기플레이", -5)
ELSEIF TALENT:수치없음
	CALL ADD_CRI_VAR("공중육변기플레이", 5)
ENDIF

;죠교자が헌신적/수동적
IF TALENT:헌신적
	CALL ADD_CRI_VAR("풋잡하기", 5)
ELSEIF TALENT:수동적
	CALL ADD_CRI_VAR("발핥기강제", 5)
ENDIF

;죠교자が남성혐오で죠교대상が남자。進展度に応じて緩和
SIF TALENT:남성혐오 && TALENT:MASTER:남자
	CALL ADD_CRI_VAR("풋잡하기", CFLAG:M호의 + CFLAG:호의 >= 10000? -4 # -7)

;죠교자が새드
IF TALENT:새드
	CALL ADD_CRI_VAR("발핥기강제", , 6)
	CALL ADD_CRI_VAR("삼각목마", 3, 4)
ENDIF

;죠교자が신중함/민감
IF TALENT:신중함
	CALL ADD_CRI_VAR("공중육변기플레이", -5)
ELSEIF TALENT:민감
	CALL ADD_CRI_VAR("공중육변기플레이", 5)
ENDIF

;죠교자が심술궂음/마음씨착한
IF TALENT:심술궂음
	CALL ADD_CRI_VAR("기분나쁘게 웃다", 3)
	CALL ADD_CRI_VAR("풋잡하기", 3)
ELSEIF TALENT:마음씨착한
	CALL ADD_CRI_VAR("공중육변기플레이", -5)
	CALL ADD_CRI_VAR("삼각목마", -5)
ENDIF

;죠교자が광기
IF TALENT:광기
	CALL ADD_CRI_VAR("피스트퍽", , 4)
	CALL ADD_CRI_VAR("애널피스트", , 4)
	CALL ADD_CRI_VAR("양쪽피스트", , 4)
	CALL ADD_CRI_VAR("방치플레이", , 4)
	CALL ADD_CRI_VAR("공중육변기플레이", , 4)
	CALL ADD_CRI_VAR("기분나쁘게 웃다", , 7)
	CALL ADD_CRI_VAR("풋잡하기", , 4)
	CALL ADD_CRI_VAR("발핥기강제", , 4)
	CALL ADD_CRI_VAR("삼각목마", , 4)
ENDIF

;죠교자がＣ민감
SIF TALENT:Ｃ민감
	CALL ADD_CRI_VAR("풋잡하기", 2, 4)

;----------------------------------------------------------------------
;フィスト適性
;경험と双方の素質が絡むので素質から独立
;
;ただ、작은체형장신によって乗算をかける意義があんまり感じられないし
;それぞれ경험と素質で、普通に加減すればいいと思うのと
;경험がないとき、マイナスになるっていうのを採用するんなら基礎値の概念使って
;----------------------------------------------------------------------
@ACT_ACTM7_FIST
;작은체형体型/巨躯/ＶＡ경험/拡張경험/淫系素質などでフィストの処理をしてみる
VARSET LOCAL

;Ｖ경험/拡張경험/淫系素質によって増減
IF EXP:MASTER:Ｖ경험 < 10
	LOCAL:0 = -6
ELSEIF EXP:MASTER:Ｖ경험 < 20 && EXP:MASTER:Ｖ확장경험 < 1
	LOCAL:0 = -4
ELSEIF EXP:MASTER:Ｖ경험 < 40 && EXP:MASTER:Ｖ확장경험 < 3
	LOCAL:0 = -2
ENDIF
SIF TALENT:MASTER:음호
	LOCAL:0 += 4

;Ａ경험/拡張경험/淫系素質によって増減
IF EXP:MASTER:Ａ경험 < 10
	LOCAL:1 = -6
ELSEIF EXP:MASTER:Ａ경험 < 20 && EXP:MASTER:Ａ확장경험 < 1
	LOCAL:1 = -4
ELSEIF EXP:MASTER:Ａ경험 < 40 && EXP:MASTER:Ａ확장경험 < 3
	LOCAL:1 = -2
ENDIF
SIF TALENT:MASTER:음고
	LOCAL:1 += 4

;작은체형体型/장신によって増減
IF TALENT:MASTER:작은체형 || TALENT:장신
	LOCAL:0 = LOCAL:0 * 3 / 4
	LOCAL:1 = LOCAL:1 * 3 / 4
ELSEIF TALENT:MASTER:장신 || TALENT:작은체형
	LOCAL:0 = LOCAL:0 * 4 / 3
	LOCAL:1 = LOCAL:1 * 4 / 3
ENDIF

;加算処理
CALL ADD_CRI_VAR("피스트퍽", LOCAL:0)
CALL ADD_CRI_VAR("애널피스트", LOCAL:1)
CALL ADD_CRI_VAR("양쪽피스트", LOCAL:0 + LOCAL:1)

;-------------------------------------------------------------------------
;죠교대상の素質を参照
;-------------------------------------------------------------------------
@ACT_ACTM7_TALENT_M

;죠교대상が겁쟁이/굳셈
IF TALENT:MASTER:겁쟁이
	CALL ADD_CRI_VAR("기분나쁘게 웃다", 3)
ELSEIF TALENT:MASTER:굳셈
	CALL ADD_CRI_VAR("공중육변기플레이", 3)
ENDIF

;죠교대상が반항적/솔직
IF TALENT:MASTER:반항적
	CALL ADD_CRI_VAR("공중육변기플레이", 4)
	CALL ADD_CRI_VAR("삼각목마", 3)
ELSEIF TALENT:MASTER:솔직
	CALL ADD_CRI_VAR("발핥기강제", 3)
ENDIF

;죠교대상が수줍음/수치없음
IF TALENT:MASTER:수줍음
	CALL ADD_CRI_VAR("방치플레이", 3)
ELSEIF TALENT:MASTER:수치없음
	CALL ADD_CRI_VAR("방치플레이", -3)
ENDIF

;죠교대상が고통에약함/고통에강함
IF TALENT:MASTER:고통에약함
	CALL ADD_CRI_VAR("삼각목마", 5)
ELSEIF TALENT:MASTER:고통에강함
	CALL ADD_CRI_VAR("삼각목마", -5)
ENDIF

;죠교대상が淫茎
SIF TALENT:MASTER:음핵／음경
	CALL ADD_CRI_VAR("풋잡하기", , 9)

;죠교대상がＣ민감/둔감
IF TALENT:MASTER:Ｃ민감
	CALL ADD_CRI_VAR("풋잡하기", 1, 6)
ELSEIF TALENT:MASTER:Ｃ둔감
	CALL ADD_CRI_VAR("풋잡하기", -1, -6)
ENDIF

;죠교대상がＶ민감/둔감
IF TALENT:MASTER:Ｖ민감
	CALL ADD_CRI_VAR("페니스밴드장착", TEQUIP:죠교자페니스밴드 ? 0 # 3, TEQUIP:죠교자페니스밴드 ? 0 # 1)
ELSEIF TALENT:MASTER:Ｖ둔감
	CALL ADD_CRI_VAR("페니스밴드장착", TEQUIP:페니스밴드 ? 0 # -3, TEQUIP:죠교자페니스밴드 ? 0 # -1)
ENDIF

;죠교자がＶ민감/둔감
IF TALENT:TARGET:Ｖ민감
	CALL ADD_CRI_VAR("페니스밴드장착", TEQUIP:죠교자페니스밴드 ? 0 # 3, TEQUIP:죠교자페니스밴드 ? 0 # 1)
ELSEIF TALENT:TARGET:Ｖ둔감
	CALL ADD_CRI_VAR("페니스밴드장착", TEQUIP:죠교자페니스밴드 ? 0 # -3, TEQUIP:죠교자페니스밴드 ? 0 # -1)
ENDIF

;죠교자が처녀
SIF TALENT:TARGET:처녀
	CALL ADD_CRI_VAR("페니스밴드장착", -10)

;죠교대상が蓬莱人
SIF TALENT:불사
	CALL ADD_CRI_VAR("공중육변기플레이", , 9)

;죠교대상が후타나리
SIF TALENT:MASTER:후타나리
	CALL ADD_CRI_VAR("풋잡하기", TALENT:호기심 * 3 - TALENT:무관심, 4)

;죠교대상が처녀
IF TALENT:MASTER:처녀
	CALL ADD_CRI_VAR("피스트퍽", -10)
	CALL ADD_CRI_VAR("양쪽피스트", -10)
	CALL ADD_CRI_VAR("공중육변기플레이", -10)
ENDIF

;죠교대상が동정
SIF TALENT:MASTER:동정
	CALL ADD_CRI_VAR("공중육변기플레이", -5)

;죠교대상が남자
SIF TALENT:MASTER:남자
	CALL ADD_CRI_VAR("공중육변기플레이", -3, -4)

;────────────────────────────────────
;能力、パラメーターによる変動
;────────────────────────────────────
;-------------------------------------------------------------------------
;죠교자の能力を参照
;-------------------------------------------------------------------------
@ACT_ACTM7_ABL_T

;죠교자の회화が[0→0, 5→0～5]を加算(0～5)
CALL ADD_CRI_VAR("기분나쁘게 웃다", , LINE_CALC(ABL:회화, "0→0,5→5,LIMIT/0～5"))

;죠교자の애무が[0→0, 5→2～4]を加算(0～4)
CALL ADD_CRI_VAR("풋잡하기", LINE_CALC(ABL:애무, "0→0,5→2,LIMIT/0～2"), LINE_CALC(ABL:애무, "0→0,5→2,LIMIT/0～2"))

;죠교자の봉사が[0→0, 5→3～5]を加算(0～5)
CALL ADD_CRI_VAR("발핥기강제", LINE_CALC(ABL:봉사, "0→0,5→3,LIMIT/0～3"), LINE_CALC(ABL:봉사, "0→0,5→2,LIMIT/0～2"))

;-------------------------------------------------------------------------
;죠교대상の能力を参照
;-------------------------------------------------------------------------
@ACT_ACTM7_ABL_M

;죠교대상のＶ감각が[0→0, 5→3]を加算(0～3)
CALL ADD_CRI_VAR("피스트퍽", LINE_CALC(ABL:MASTER:Ｖ감각, "0→0,5→3,LIMIT/0～3"))
;죠교대상のＶ감각が[0→0, 5→2]を加算(0～2)
CALL ADD_CRI_VAR("양쪽피스트", LINE_CALC(ABL:MASTER:Ｖ감각, "0→0,5→2,LIMIT/0～2"))

;죠교대상のＡ감각が[0→0, 5→3]を加算(0～3)
CALL ADD_CRI_VAR("애널피스트", LINE_CALC(ABL:MASTER:Ａ감각, "0→0,5→3,LIMIT/0～3"))
;죠교대상のＡ감각が[0→0, 5→2]を加算(0～2)
CALL ADD_CRI_VAR("양쪽피스트", LINE_CALC(ABL:MASTER:Ａ감각, "0→0,5→2,LIMIT/0～2"))

;-------------------------------------
;죠교자のALI
;-------------------------------------
@ACT_ACTM7_ALI_T

;아라이멘도が[-50→3～8, 50→-8～-3]を加算(-8～8)
CALL ADD_CRI_VAR("공중육변기플레이", LINE_CALC(CFLAG:아라이멘도, "-50→3,50→-3,LIMIT/-3～3"), LINE_CALC(CFLAG:아라이멘도, "-50→5,50→-5,LIMIT/-5～5"))
;아라이멘도が[-50→-8～-3, 50→3～8]を加算(-8～8)
CALL ADD_CRI_VAR("풋잡하기", LINE_CALC(CFLAG:아라이멘도, "-50→-3,50→3,LIMIT/-3～3"), LINE_CALC(CFLAG:아라이멘도, "-50→-5,50→5,LIMIT/-5～5"))
;아라이멘도が[-30→0, 0→3, 30→0]を加算(0～3)
CALL ADD_CRI_VAR("발핥기강제", LINE_CALC(CFLAG:아라이멘도, "-30→0,0→3,30→0,LIMIT/0～3"))
;아라이멘도が[-20→0, 0→2, 20→0]を加算(0～2)
CALL ADD_CRI_VAR("피스트퍽", LINE_CALC(CFLAG:아라이멘도, "-20→0,0→2,20→0,LIMIT/0～2"))

;────────────────────────────────────
;ゲージや상태による変動
;────────────────────────────────────
;-------------------------------------
;죠교자の기력
;-------------------------------------
@ACT_ACTM7_MENTAL_T

;죠교자の기력が[0→3+α*3, 750→0, 1000→-3]を加算(-3～3+α*3)(α=조교자소모도)
CALL ADD_CRI_VAR("방치플레이", LINE_CALC(BASE:기력, "0→3,750→0,1000→-3,LIMIT/-3～3") + TFLAG:조교자소모도 * LINE_CALC(BASE:기력, "0→3,750→0,LIMIT/0～3"))
CALL ADD_CRI_VAR("공중육변기플레이", LINE_CALC(BASE:기력, "0→3,750→0,1000→-3,LIMIT/-3～3") + TFLAG:조교자소모도 * LINE_CALC(BASE:기력, "0→3,750→0,LIMIT/0～3"))

;-------------------------------------
;죠교자の이성
;-------------------------------------
@ACT_ACTM7_REASON_T

;죠교자の이성が[0→2～7, 750→0]を加算(0～7)
CALL ADD_CRI_VAR("기분나쁘게 웃다", LINE_CALC(BASE:이성, "0→2,750→0,LIMIT/0～2"), LINE_CALC(BASE:이성, "0→5,750→0,LIMIT/0～5"))
;죠교자の이성が[500→0, 1000→3]を加算(0～3)
CALL ADD_CRI_VAR("풋잡하기", LINE_CALC(BASE:이성, "500→0,1000→3,LIMIT/0～3"))
;죠교자の이성が[500→0, 1000→2～5]を加算(0～5)
CALL ADD_CRI_VAR("발핥기강제", LINE_CALC(BASE:이성, "500→0,1000→2,LIMIT/0～2"), LINE_CALC(BASE:이성, "500→0,1000→5,LIMIT/0～5"))

;-------------------------------------
;죠교자の흥미
;-------------------------------------
@ACT_ACTM7_INTEREST_T

;죠교자の흥미が[0→-3, 550→0]を加算(-3～0)
CALL ADD_CRI_VAR("피스트퍽", LINE_CALC(BASE:흥미, "0→-3,550→0,LIMIT/-3～0"))
CALL ADD_CRI_VAR("애널피스트", LINE_CALC(BASE:흥미, "0→-3,550→0,LIMIT/-3～0"))
CALL ADD_CRI_VAR("양쪽피스트", LINE_CALC(BASE:흥미, "0→-3,550→0,LIMIT/-3～0"))
;죠교자の흥미が[0→3, 700→-3]を加算(-3～3)
CALL ADD_CRI_VAR("방치플레이", LINE_CALC(BASE:흥미, "0→3,700→-3,LIMIT/-3～3"))
;죠교자の흥미が[0→5, 700→-5]を加算(-5～5)
CALL ADD_CRI_VAR("공중육변기플레이", LINE_CALC(BASE:흥미, "0→5,700→-5,LIMIT/-5～5"))
;죠교자の흥미が[0→-5, 700→0]を加算(-5～0)
CALL ADD_CRI_VAR("풋잡하기", LINE_CALC(BASE:흥미, "0→-5,700→0,LIMIT/-5～0"))
CALL ADD_CRI_VAR("발핥기강제", LINE_CALC(BASE:흥미, "0→-5,700→0,LIMIT/-5～0"))

;-------------------------------------
;죠교대상の체력
;-------------------------------------
@ACT_ACTM7_PHYSICAL_M

;죠교대상の체력が[0→-1, 500→0]を加算(-1～0)
CALL ADD_CRI_VAR("피스트퍽", LINE_CALC(BASE:MASTER:체력, "0→-1,500→0,LIMIT/-1～0"))
;죠교대상の체력が[0→-3, 1000→0]を加算(-3～0)
CALL ADD_CRI_VAR("애널피스트", LINE_CALC(BASE:MASTER:체력, "0→-3,1000→0,LIMIT/-3～0"))
;죠교대상の체력が[0→-4, 1000→0]を加算(-4～0)
CALL ADD_CRI_VAR("양쪽피스트", LINE_CALC(BASE:MASTER:체력, "0→-4,1000→0,LIMIT/-4～0"))
;※취소時は正の보정
;죠교대상の체력が[0→9/-9, 1500→0]を加算(-9～9)
LOCAL = TEQUIP:삼각목마 ? 1 # -1
CALL ADD_CRI_VAR("삼각목마", LOCAL * LINE_CALC(BASE:MASTER:체력, "0→9,1500→0,LIMIT/0～9"))

;-------------------------------------
;죠교자の상태
;-------------------------------------
@ACT_ACTM7_CONDITION_T

;죠교자の상태(0=통상/1=피폐/2=쇠약/3=무기력/4=몽롱/5=정욕/6=분노/7=싫증/8=울굴)
SELECTCASE GET_CONDITION(TARGET)
	CASE "피폐", "쇠약"
		CALL ADD_CRI_VAR("피스트퍽", -TFLAG:조교자소모도)
		CALL ADD_CRI_VAR("애널피스트", -TFLAG:조교자소모도)
		CALL ADD_CRI_VAR("양쪽피스트", -TFLAG:조교자소모도 - 3)
		CALL ADD_CRI_VAR("풋잡하기", -TFLAG:조교자소모도 * 2)
		CALL ADD_CRI_VAR("발핥기강제", -TFLAG:조교자소모도 / 2)
		CALL ADD_CRI_VAR("삼각목마", -TFLAG:조교자소모도, -5)
	CASE "싫증"
		CALL ADD_CRI_VAR("방치플레이", 5)
		CALL ADD_CRI_VAR("공중육변기플레이", 5)
ENDSELECT

;────────────────────────────────────
;전회의행동や状況による変動
;────────────────────────────────────
;-------------------------------------
;미약
;-------------------------------------
@ACT_ACTM7_DRAG
SIF TEQUIP:미약
	CALL ADD_CRI_VAR("방치플레이", TALENT:심술궂음 * 3 - TALENT:마음씨착한 * 3, 4)

;-------------------------------------
;밧줄
;-------------------------------------
@ACT_ACTM7_ROPE
SIF TEQUIP:밧줄
	CALL ADD_CRI_VAR("방치플레이", 5)

;-------------------------------------
;눈가리개
;-------------------------------------
@ACT_ACTM7_EYEMASK
SIF TEQUIP:눈가리개
	CALL ADD_CRI_VAR("방치플레이", 3)

;-------------------------------------
;볼개그
;-------------------------------------
@ACT_ACTM7_GAG
SIF TEQUIP:볼개그
	CALL ADD_CRI_VAR("방치플레이", , 3)

;-------------------------------------
;삼각목마
;-------------------------------------
@ACT_ACTM7_HORSE
SIF TEQUIP:삼각목마
	CALL ADD_CRI_VAR("방치플레이", , 7)

;-------------------------------------
;야외플레이
;-------------------------------------
@ACT_ACTM7_OUT
IF TEQUIP:야외플레이
	CALL MUL_CRI_VAR("방치플레이", 150)
	CALL MUL_CRI_VAR("공중육변기플레이", 125)
ENDIF

;-------------------------------------
;要求されて도구をはず했다
;-------------------------------------
@ACT_ACTM7_TAKE_OFF_TOOLS
;취소に応じて장착を躊躇
SIF !TEQUIP:삼각목마
	CALL ADD_CRI_VAR("삼각목마", -CANCEL_TEQUIP_COUNT("삼각목마") / 20)

;-------------------------------------
;죠교자자위중
;-------------------------------------
@ACT_ACTM7_MASTURBATION_T
;죠교자のみ자위중
SIF TEQUIP:자위중 == 2
	CALL ADD_CRI_VAR("방치플레이", , 9)

;-------------------------------------
;죠교대상の衣服
;-------------------------------------
@ACT_ACTM7_CLOTH_M
;죠교대상の하반신속옷があるとき
SIF CLOTHESTYPE(1, 0)
	CALL ADD_CRI_VAR("삼각목마", -2)

;죠교대상の하반신겉옷に妨害されるとき
IF CLOTHESTYPE(1, 1) == 2
	;죠교자に벽통과がない
	IF !TALENT:벽통과
		CALL ADD_CRI_VAR("피스트퍽", -5)
		CALL ADD_CRI_VAR("애널피스트", -5)
		CALL ADD_CRI_VAR("양쪽피스트", -5)
	ENDIF
	CALL ADD_CRI_VAR("삼각목마", -5)
ENDIF

;-------------------------------------
;더 하드하게 해
;-------------------------------------
@ACT_ACTM7_PLEASE_HARD
IF IS_NOWCOMNAME("더 하드하게 해")
	CALL ADD_CRI_VAR("방치플레이", -10)
	CALL ADD_CRI_VAR("공중육변기플레이", 2)
	CALL ADD_CRI_VAR("기분나쁘게 웃다", -3)
	CALL ADD_CRI_VAR("풋잡하기", -3)
ENDIF

;-------------------------------------
;용서를 빌다
;-------------------------------------
@ACT_ACTM7_PLEASE_HELP
;許しを乞ったかつ、죠교자の상태が울굴以外
SIF IS_NOWCOMNAME("용서를 빌다") && TCVAR:상태변화 < 8
	CALL ADD_CRI_VAR("방치플레이", 5)

;-------------------------------------
;징계・中
;-------------------------------------
@ACT_ACTM7_OSHIOKI
IF TFLAG:징계・플래그
	CALL ADD_CRI_VAR("공중육변기플레이", TFLAG:징계・플래그 / 2)
	CALL ADD_CRI_VAR("삼각목마", TFLAG:징계・플래그 / 2)
ENDIF

;────────────────────────────────────
;同じ행동연속실행の確率をダウンします
;────────────────────────────────────
@ACT_ACTM7_PREV
SELECTCASE TFLAG:전ACT
	CASE 9, 18, 55, 67, 70 TO 74
		CALL ADD_CRI_VAR(GET_ACTNAME(TFLAG:전ACT), -3 * TFLAG:동일행동보너스, -4 * TFLAG:동일행동보너스)
ENDSELECT

;────────────────────────────────────
;보정値計算
;────────────────────────────────────
@ACT_ACTM7_CORRECTION(ARG)
#DIM LCOUNT

;ARGが0以外なら보정値を計算し、以後の処理を行わない
IF ARG
	;9,18,55,67に関してはここで計算しない
	FOR LCOUNT, 70, 75
		CALL ACT_M_RES_CALC(LCOUNT, GET_CRI_VAR(GET_ACTNAME(LCOUNT) + "累計"))
	NEXT
ELSE
	;보정値を判定値に加算する
	CALL ACT_ACTM_CORRECTION(7)
ENDIF

;────────────────────────────────────
;その他変動や実行不可能の判定
;────────────────────────────────────
@ACT_ACTM7_ABLE
#DIM LCOUNT

;不可能判定
CALL ACT_ACTM_ABLE(7)

